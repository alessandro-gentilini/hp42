LBL "PHIL"
LBL 01
CLMENU
XEQ 85
KEY 1 XEQ 92
"LENG"
KEY 2 GTO 02
"QUAD"
KEY 3 GTO 03
"PERF"
KEY 4 GTO 04
"DCRD"
KEY 5 GTO 05
"CAL"
KEY 6 XEQ 95
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"PX>D"
KEY 1 XEQ 91
"D>PX"
KEY 2 XEQ 66
"CAL"
KEY 6 XEQ 95
KEY 9 GTO 01
MENU
STOP
GTO 02
LBL 03
CLMENU
"UL"
KEY 1 XEQ 57
"UR"
KEY 2 XEQ 58
"LR"
KEY 3 XEQ 59
"LL"
KEY 4 XEQ 60
"CAL"
KEY 6 XEQ 95
KEY 7 GTO 06
KEY 8 GTO 06
KEY 9 GTO 01
MENU
STOP
GTO 03
LBL 06
CLMENU
"∡TB"
KEY 1 XEQ 62
"∡LR"
KEY 2 XEQ 63
"WIDE"
KEY 3 XEQ 64
"TALL"
KEY 4 XEQ 65
KEY 7 GTO 03
KEY 8 GTO 03
KEY 9 GTO 01
MENU
STOP
GTO 06
LBL 04
CLMENU
"D<>P"
KEY 1 XEQ 96
"P<>K"
KEY 2 XEQ "P<>K"
XEQ 87
KEY 3 XEQ 98
XEQ 89
KEY 4 XEQ 99
XEQ 86
KEY 5 XEQ 88
XEQ 67
KEY 6 XEQ 95
KEY 9 GTO 01
MENU
STOP
GTO 04
LBL 05
CLMENU
"PX>DC"
KEY 1 XEQ 75
"DC>PX"
KEY 2 XEQ 49
"T>MM"
KEY 3 XEQ "T>MM"
"MM>T"
KEY 4 XEQ "MM>T"
"ORIG"
KEY 5 XEQ 90
KEY 9 GTO 01
MENU
STOP
GTO 05
LBL 00
EXITALL
RTN
LBL 57
FUNC 00
L4STK
REAL?
COMPLEX
FS? 64
RCL "PhilP1"
STO "PhilP1"
"P1: "
ARCL ST X
AVIEW
RTN
LBL 58
FUNC 00
L4STK
REAL?
COMPLEX
FS? 64
RCL "PhilP2"
STO "PhilP2"
"P2: "
ARCL ST X
AVIEW
RTN
LBL 59
FUNC 00
L4STK
REAL?
COMPLEX
FS? 64
RCL "PhilP3"
STO "PhilP3"
"P3: "
ARCL ST X
AVIEW
RTN
LBL 60
FUNC 00
L4STK
REAL?
COMPLEX
FS? 64
RCL "PhilP4"
STO "PhilP4"
"P4: "
ARCL ST X
AVIEW
RTN
LBL 61
FUNC 01
L4STK
0
LSTO "_TMPC"
SF 25
RCL "PhilP1"
FS?C 25
ISG "_TMPC"
NOP
SF 25
RCL "PhilP2"
FS?C 25
ISG "_TMPC"
NOP
SF 25
RCL "PhilP3"
FS?C 25
ISG "_TMPC"
NOP
SF 25
RCL "PhilP4"
FS?C 25
ISG "_TMPC"
NOP
RCL "_TMPC"
RTN
LBL 62
FUNC 01
L4STK
XEQ 61
4
X≠Y?
GTO 56
RCL "PhilP2"
RCL- "PhilP1"
XEQ 37
RCL "PhilP3"
RCL- "PhilP4"
XEQ 37
-
RTN
LBL 63
FUNC 01
L4STK
XEQ 61
4
X≠Y?
GTO 56
RCL "PhilP4"
RCL- "PhilP1"
XEQ 37
RCL "PhilP3"
RCL- "PhilP2"
XEQ 37
-
RTN
LBL 64
FUNC 03
L4STK
XEQ 61
4
X≠Y?
GTO 56
RCL "PhilP2"
RCL- "PhilP1"
XEQ 91
RCL "PhilP3"
RCL- "PhilP4"
XEQ 91
RCL ST Y
RCL ST Y
+
2
÷
RTN
LBL 65
FUNC 03
L4STK
XEQ 61
4
X≠Y?
GTO 56
RCL "PhilP4"
RCL- "PhilP1"
XEQ 91
RCL "PhilP3"
RCL- "PhilP2"
XEQ 91
RCL ST Y
RCL ST Y
+
2
÷
RTN
LBL 56
"ERR: Enter"
├" Corners"
AVIEW
RTN
LBL 90
FUNC 00
L4STK
REAL?
COMPLEX
FC? 64
STO "PhilCo"
XEQ 45
"Orig: "
ARCL ST X
AVIEW
RTN
LBL 75
L4STK
REAL?
COMPLEX
XEQ 68
RTN
LBL 68
FUNC 11
L4STK
XEQ 45
-
XEQ 91
RTN
LBL 49
L4STK
REAL?
COMPLEX
XEQ 69
RTN
LBL 69
FUNC 11
L4STK
XEQ 66
XEQ 45
+
RTN
LBL 45
FUNC 01
L4STK
0
0
COMPLEX
SF 25
RCL "PhilCo"
FC?C 25
STO "PhilCo"
RTN
LBL "MM>T"
L4STK
REAL?
COMPLEX
XEQ 38
RTN
LBL 38
FUNC 11
L4STK
3
÷
COMPLEX
IP
ABS
X<>Y
IP
ABS
1
+
X<>Y
65
+
86
X>Y?
GTO 46
X<>Y
R↓
63
X<>Y
LBL 46
R↓
"Thirkell: "
XTOA
R↓
AIP
R↓
AVIEW
RTN
LBL "T>MM"
FUNC 01
L4STK
LBL 40
LASTO "TMPS"
ALENG
2
X>Y?
GTO 47
R↓
3
X<Y?
GTO 47
R↓
R↓
ATOX
65
X>Y?
GTO 47
R↓
84
X<Y?
GTO 47
R↓
3
×
ANUM
FC? 22
GTO 47
1
X>Y?
GTO 47
R↓
17
X<Y?
GTO 47
R↓
ENTER
FP
X≠0?
GTO 47
R↓
1
-
3
×
X<>Y
COMPLEX
CLA
ARCL "TMPS"
├"→"
ARCL ST X
├" mm"
AVIEW
RTN
LBL 47
"Enter Thir"
├"kell; R/S"
AON
STOP
AOFF
GTO 40
RTN
LBL 85
FUNC 00
L4STK
RECT
"ANGL"
XEQ 79
XEQ 77
XEQ 78
XEQ 76
RTN
LBL 67
FUNC 00
L4STK
XEQ 77
X≠0?
RTNNO
"CAL"
RTNYES
LBL 86
FUNC 00
L4STK
CLA
XEQ 77
X≠0?
RTNNO
"I:"
XEQ 79
70
+
XEQ IND ST X
R↓
RTNYES
LBL 89
FUNC 00
L4STK
"D:"
XEQ 77
50
+
XEQ IND ST X
R↓
RTN
LBL 87
FUNC 00
L4STK
"C:"
XEQ 78
AIP
R↓
RTN
LBL 92
L4STK
REAL?
COMPLEX
XEQ 37
RTN
LBL 37
FUNC 11
L4STK
FC? 73
GTO 42
GTO 31
LBL 42
POLAR
COMPLEX
RECT
GTO 44
LBL 31
POLAR
COMPLEX
RECT
LBL 44
X<>Y
R↓
RTN
LBL 95
FUNC 00
L4STK
FS? 64
GTO 29
X<>Y
ABS
X<>Y
÷
STO "PhilIC"
LBL 29
RCL "PhilIC"
"Cal: "
ARCL ST X
AVIEW
RTN
LBL 96
FUNC 11
L4STK
XEQ 39
XEQ 78
1
-
X<>Y
÷
20
×
RTN
LBL "P<>K"
FUNC 11
L4STK
127
×
100000
X<>Y
÷
RTN
RTN
LBL 98
FUNC 00
L4STK
STO "PhilPC"
RTN
LBL 99
FUNC 00
L4STK
XEQ 77
1
+
6
MOD
STO "PhilDU"
R↓
RTN
LBL 88
FUNC 00
L4STK
XEQ 79
1
+
5
MOD
STO "PhilIU"
R↓
RTN
LBL 39
FUNC 01
L4STK
XEQ 77
X=0?
GTO 34
GTO 35
LBL 34
R↓
XEQ 91
XEQ 79
80
+
XEQ IND ST X
GTO 36
LBL 35
80
+
1
-
XEQ IND ST X
LBL 36
RTN
LBL 91
FUNC 11
L4STK
ABS
XEQ 76
÷
RTN
LBL 66
FUNC 11
L4STK
ABS
XEQ 76
×
RTN
LBL 50
├"px"
RTN
LBL 51
├"mm"
RTN
LBL 52
├"mil"
RTN
LBL 53
├"μm"
RTN
LBL 54
├"in"
RTN
LBL 55
├"cm"
RTN
LBL 70
├"mm"
RTN
LBL 71
├"mil"
RTN
LBL 72
├"μm"
RTN
LBL 73
├"in"
RTN
LBL 74
├"cm"
RTN
LBL 80
R↓
RTN
LBL 81
R↓
25.4
×
1000
÷
RTN
LBL 82
R↓
1e3
÷
RTN
LBL 83
R↓
25.4
×
RTN
LBL 84
R↓
10
×
RTN
LBL 76
FUNC 01
SF 25
RCL "PhilIC"
FS?C 25
RTN
94.4881889764
STO "PhilIC"
RTN
LBL 77
FUNC 01
SF 25
RCL "PhilDU"
FS?C 25
RTN
0
STO "PhilDU"
RTN
LBL 78
FUNC 01
SF 25
RCL "PhilPC"
FS?C 25
RTN
10
STO "PhilPC"
RTN
LBL 79
FUNC 01
SF 25
RCL "PhilIU"
FS?C 25
RTN
0
STO "PhilIU"
RTN
LBL 94
FUNC 00
CLV "PhilIU"
CLV "PhilDU"
CLV "PhilPC"
CLV "PhilIC"
XEQ 79
XEQ 77
XEQ 78
XEQ 76
RTN
END

