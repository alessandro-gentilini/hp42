LBL "PERF"
LBL 01
CLMENU
"CD→K"
KEY 1 XEQ 99
"CK→D"
KEY 2 XEQ 98
"CD→P"
KEY 3 XEQ 97
"CP→D"
KEY 4 XEQ 97
"P→K"
KEY 5 XEQ 96
"K→P"
KEY 6 XEQ 96
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00
EXITALL
RTN
LBL 99
FUNC 21
L4STK
X<>Y
IP
1
-
÷
127
÷
5000
×
RTN
LBL 98
FUNC 21
L4STK
X<>Y
IP
1
-
×
127
×
5000
÷
RTN
LBL 97
FUNC 21
L4STK
X<>Y
IP
1
-
X<>Y
÷
20
×
RTN
LBL 96
FUNC 11
L4STK
127
×
100000
X<>Y
÷
RTN
END

LBL "IMET"
LBL 01
SF 25
RCL "IMETC"
FS?C 25
GTO 20
94.4881889764
STO "IMETC"
"WARN: "
"├CAL Default!"
AVIEW
LBL 20
R↓
CLMENU
"LENG"
KEY 1 XEQ 03
"ANGLE"
KEY 2 XEQ 04
"AREA"
KEY 3 XEQ 05
"CAL?"
KEY 5 XEQ 06
"CAL"
KEY 6 XEQ 07
KEY 7 GTO 02
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"D→P"
KEY 1 XEQ 08
"D→K"
KEY 2 XEQ 09
"C:"
SF 25
RCL "IMETP"
FS?C 25
GTO 25
10
STO "IMETP"
LBL 25
AIP
R↓
KEY 4 XEQ 11
"D:"
SF 25
RCL "IMETD"
FC? 25
GTO 14
GTO 15
LBL 14
0
STO "IMETD"
LBL 15
50
+
XEQ IND ST X
R↓
KEY 5 XEQ 12
RCL "IMETD"
X≠0?
GTO 16
"I:"
SF 25
RCL "IMETI"
FC? 25
GTO 17
GTO 18
LBL 17
0
STO "IMETI"
LBL 18
70
+
XEQ IND ST X
R↓
KEY 6 XEQ 13
LBL 16
R↓
KEY 7 GTO 01
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 00
EXITALL
RTN
LBL 03
ABS
RCL÷ "IMETC"
RTN
LBL 04
CPX?
GTO 19
"ERR: "
"├Cplx Arg Req!"
AVIEW
RTN
LBL 19
FC? 73 @@ IF-RECT
GTO 22
GTO 21
LBL 22 @@ IF-THEN RECT MODE
POLAR
COMPLEX
RECT
GTO 23
LBL 21 @@ IF-ELSE POLAR MODE
POLAR
COMPLEX
RECT
LBL 23 @@ IF-END
X<>Y
R↓
RTN
LBL 05
CPX?
GTO 19
"ERR: "
"├Cplx Arg Req!"
AVIEW
RTN
LBL 19
FC? 73 @@ IF-RECT
GTO 22
GTO 21
LBL 22 @@ IF-THEN RECT MODE
COMPLEX
GTO 23
LBL 21 @@ IF-ELSE POLAR MODE
POLAR
COMPLEX
RECT
LBL 23 @@ IF-END
ABS
X<>Y
ABS
×
RCL÷ "IMETC"
RCL÷ "IMETC"
RTN
LBL 06
RCL "IMETC"
RTN
LBL 07
X<>Y
ABS
X<>Y
÷
STO "IMETC"
RTN
LBL 08
XEQ 35
RCL "IMETP"
1
-
X<>Y
÷
20
×
RTN
LBL 09
XEQ 35
RCL "IMETP"
1
-
÷
127
÷
5000
×
RTN
LBL 11
STO "IMETP"
RTN
LBL 12
RCL "IMETD"
1
+
6
MOD
STO "IMETD"
R↓
RTN
LBL 13
RCL "IMETI"
1
+
5
MOD
STO "IMETI"
R↓
RTN
LBL 35
RCL "IMETD"
X=0?
GTO 24
GTO 25
LBL 24
R↓
XEQ 03
RCL "IMETI"
80
+
XEQ IND ST X
GTO 26
LBL 25
80
+
1
-
XEQ IND ST X
LBL 26
RTN
LBL 50
"├px"
RTN
LBL 51
"├mm"
RTN
LBL 52
"├mil"
RTN
LBL 53
"├μm"
RTN
LBL 54
"├in"
RTN
LBL 55
"├cm"
RTN
LBL 70
"├mm"
RTN
LBL 71
"├mil"
RTN
LBL 72
"├μm"
RTN
LBL 73
"├in"
RTN
LBL 74
"├cm"
RTN
LBL 80
R↓
RTN
LBL 81
R↓
25.4
×
1000
÷
RTN
LBL 82
R↓
1e3
÷
RTN
LBL 83
R↓
25.4
×
RTN
LBL 84
R↓
10
×
RTN
END

