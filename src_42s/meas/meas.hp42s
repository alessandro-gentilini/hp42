LBL "MEAS"
LBL 01
CLMENU
XEQ 70
KEY 1 XEQ 98
"M-"
KEY 2 XEQ 97
"EDIT"
KEY 3 XEQ 38
"REP"
KEY 4 XEQ 96
"CLRM"
KEY 6 XEQ 95
KEY 7 GTO 05
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"μ"
KEY 1 XEQ 94
"kuN"
KEY 2 XEQ 93
"MR"
KEY 3 XEQ 92
"kuR"
KEY 4 XEQ 91
"MIN"
KEY 5 XEQ 89
"MAX"
KEY 6 XEQ 88
KEY 7 GTO 01
KEY 8 GTO 03
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03
CLMENU
"PSD"
KEY 1 XEQ 87
"SSD"
KEY 2 XEQ 86
"RNG"
KEY 3 XEQ 85
"RtVA"
KEY 4 XEQ 90
"N"
KEY 5 XEQ 84
"SUMS"
KEY 6 XEQ 83
KEY 7 GTO 02
KEY 8 GTO 04
KEY 9 GTO 00
MENU
STOP
GTO 03
LBL 04
CLMENU
"Q25"
KEY 1 XEQ 35
"MED"
KEY 2 XEQ 77
"Q75"
KEY 3 XEQ 36
"IQR"
KEY 4 XEQ 34
"kuQ"
KEY 5 XEQ 46
KEY 7 GTO 03
KEY 8 GTO 05
KEY 9 GTO 00
MENU
STOP
GTO 04
LBL 05
CLMENU
XEQ 79
KEY 1 XEQ 82
XEQ 48
KEY 2 XEQ 81
XEQ 78
KEY 3 XEQ 80
"REP"
KEY 4 XEQ 96
"RESET"
KEY 6 XEQ "RSTMES"
KEY 7 GTO 04
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 05
LBL 00
EXITALL
RTN
LBL "RSTMES"
CLV "MeDAT"
CLV "MeCFA"
CLV "MeRDM"
CLV "MeREP"
CLV "MeRSC"
CLV "MeTGN"
XEQ 49 @NM@ DEFVAR
"MeDAT & Set"
├"tings RESET"
AVIEW
RTN
LBL 38 @NM@ EDIT
FUNC 00
SF 25
RCL "MeDAT"
FC?C 25
GTO 99
R↓
EDITN "MeDAT"
"Enter data; R/S"
├" to end"
PROMPT
EXITALL
XEQ 47 @NM@ SMLREP
RTN
LBL 70 @NM@ M+_LBL
FUNC 00
XEQ 49 @NM@ DEFVAR
"M+"
RTN
LBL 49 @NM@ DEFVAR
FUNC 00
0
SF 25
RCL "MeRSC"
FC?C 25
STO "MeRSC"
SF 25
RCL "MeCFA"
FC?C 25
STO "MeCFA"
SF 25
RCL "MeTGN"
FC?C 25
STO "MeTGN"
SF 25
RCL "MeRDM"
FC?C 25
STO "MeRDM"
SF 25
RCL "MeREP"
FC?C 25
STO "MeREP"
R↓
RTN
LBL 78 @NM@ A_LBL
FUNC 00
"A:"
RCL "MeTGN"
X=0?
├"-"
X≠0?
AIP
R↓
RTN
LBL 79 @NM@ S_LAB
FUNC 00
"S:"
RCL "MeRSC"
50
+
XEQ IND ST X
R↓
RTN
LBL 48 @NM@ k_LAB
FUNC 00
"k:"
RCL "MeCFA"
65
+
XEQ IND ST X
R↓
RTN
LBL 80 @NM@ A_ACT
FUNC 00
0
X≤Y?
R↓
99
X≥Y?
R↓
STO "MeTGN"
X=0?
RTN
"WARN: Too"
├" small:"
3
X>Y?
├" Q"
R↓
2
X>Y?
├" & N"
R↓
3
X>Y?
AVIEW
R↓
R↓
RTN
LBL 81 @NM@ k_ACT
FUNC 00
1
RCL+ "MeCFA"
RCL "MeRSC"
1
X=Y?
GTO 58
R↓
R↓
4
GTO 57
LBL 58
R↓
R↓
2
LBL 57
MOD
STO "MeCFA"
R↓
RTN
LBL 82 @NM@ S_ACT
FUNC 00
1
RCL+ "MeRSC"
3
MOD
STO "MeRSC"
R↓
0
STO "MeCFA"
R↓
RTN
LBL 87 @NM@ PSD
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
XEQ 83 @NM@ SUMS
XEQ 84 @NM@ N
X<>Y
X↑2
RCL÷ ST Y
RCL÷ ST Y
RCL ST Z
RCL ST Z
÷
X<>Y
-
SQRT
RTN
LBL 97 @NM@ M-
FUNC 00
SF 25
RCL "MeDAT"
FC?C 25
GTO 72
DIM?
R↓
1
X=Y?
GTO 72
SF 25
INDEX "MeDAT"
FC?C 25
GTO 72
J-
RCLEL
DELR
XEQ 47 @NM@ SMLREP
RTN
LBL 72
XEQ 95 @NM@ CLRM
RTN
LBL 95 @NM@ CLRM
FUNC 00
SF 25
INDEX "MeDAT"
CLV "MeDAT"
CF 25
"MeDAT Cleared"
AVIEW
RTN
LBL 98 @NM@ M+
FUNC 00
REAL?
GTO 56
"ERR: Bad Value"
AVIEW
RTN
LBL 56
SF 25
INDEX "MeDAT"
FS?C 25
GTO 55
1
1
DIM "MeDAT"
INDEX "MeDAT"
R↓
R↓
STOEL
XEQ 47 @NM@ SMLREP
RTN
LBL 55
GROW
J-
J+
WRAP
STOEL
XEQ 84 @NM@ N
RCL "MeTGN"
X≠Y?
GTO 43
XEQ 96 @NM@ REP
RTN
LBL 43
XEQ 47 @NM@ SMLREP
RTN
LBL 47 @NM@ SMLREP
FUNC 00
XEQ 84 @NM@ N
"N="
AIP
AVIEW
RCL "MeTGN"
X≤Y?
RTN
ALENG
8
X<>Y
-
LBL 53
├" "
DSE ST X
GTO 53
├"Report in "
R↓
X<>Y
-
AIP
AVIEW
RTN
LBL 96 @NM@ REP
FUNC 00
SF 25
RCL "MeDAT"
FC?C 25
GTO 99
DIM?
R↓
RCL "MeRSC"
30
+
GTO IND ST X
LBL 30
R↓
2
X>Y?
GTO 75
R↓
R↓
"μ="
XEQ 94 @NM@ μ
ARCL ST X
├" "
RCL "MeCFA"
65
+
XEQ IND ST X
├"×u="
XEQ 93 @NM@ kuN
ARCL ST X
GTO 44
LBL 31
R↓
2
X>Y?
GTO 75
R↓
R↓
"MR="
XEQ 92 @NM@ MR
ARCL ST X
├"  "
RCL "MeCFA"
65
+
XEQ IND ST X
├"×u="
XEQ 91 @NM@ kuR
ARCL ST X
GTO 44
LBL 32
R↓
3
X>Y?
GTO 75
R↓
R↓
"Q2="
XEQ 77 @NM@ MED
ARCL ST X
├"  "
RCL "MeCFA"
65
+
XEQ IND ST X
├"×u="
XEQ 46 @NM@ kuQ
ARCL ST X
GTO 44
LBL 44
ALENG
21
X<Y?
GTO 45
├"[LF]↓="
XEQ 89 @NM@ MIN
ARCL ST X
├"  ↑="
XEQ 88 @NM@ MAX
ARCL ST X
ALENG
41
X<Y?
GTO 45
AVIEW
RTN
LBL 45
RCL "MeRSC"
40
+
GTO IND ST X
LBL 40
R↓
"μ="
XEQ 94 @NM@ μ
ARCL ST X
├"[LF]"
RCL "MeCFA"
65
+
XEQ IND ST X
├"×u="
XEQ 93 @NM@ kuN
ARCL ST X
AVIEW
RTN
LBL 41
R↓
"MR="
XEQ 92 @NM@ MR
ARCL ST X
├"[LF]"
RCL "MeCFA"
65
+
XEQ IND ST X
├"×u="
XEQ 91 @NM@ kuR
ARCL ST X
AVIEW
RTN
LBL 42
R↓
"Q2="
XEQ 77 @NM@ MED
ARCL ST X
├"[LF]"
RCL "MeCFA"
65
+
XEQ IND ST X
├"×u="
XEQ 46 @NM@ kuQ
ARCL ST X
AVIEW
RTN
LBL 92 @NM@ MR
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
XEQ 88 @NM@ MAX
XEQ 89 @NM@ MIN
+
2
÷
RTN
LBL 88 @NM@ MAX
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
[MAX]
X<>Y
R↓
RTN
LBL 89 @NM@ MIN
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
[MIN]
X<>Y
R↓
RTN
LBL 91 @NM@ kuR
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
XEQ 90 @NM@ RtVA
RCL "MeCFA"
60
+
XEQ IND ST X
X<>Y
R↓
×
RTN
LBL 90 @NM@ RtVA
FUNC 0
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
XEQ 85 @NM@ RNG
2
÷
3
SQRT
÷
RTN
LBL 85 @NM@ RNG
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
XEQ 88 @NM@ MAX
XEQ 89 @NM@ MIN
-
RTN
LBL 93 @NM@ kuN
FUNC 01
SF 25
RCL "MeDAT"
FC?C 25
GTO 99
DIM?
R↓
2
XEQ 86 @NM@ SSD
RCL "MeCFA"
60
+
XEQ IND ST X
X<>Y
R↓
×
RTN
LBL 86 @NM@ SSD
FUNC 01
SF 25
RCL "MeDAT"
FC?C 25
GTO 99
DIM?
R↓
2
X>Y?
GTO 75
XEQ 83 @NM@ SUMS
XEQ 84 @NM@ N
X<>Y
X↑2
RCL÷ ST Y
RCL ST Z
X<>Y
-
X<>Y
1
-
÷
SQRT
X<>Y
R↓
RTN
LBL 94 @NM@ μ
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
XEQ 83 @NM@ SUMS
X<>Y
R↓
XEQ 84 @NM@ N
÷
RTN
LBL 83 @NM@ SUMS
FUNC 02
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
0
0
LBL 33
RCLEL
STO+ ST Y
X↑2
STO+ ST Z
R↓
J+
FC? 77
GTO 33
RTN
LBL 84 @NM@ N
FUNC 01
SF 25
RCL "MeDAT"
FS?C 25
GTO 54
0
RTN
LBL 54
DIM?
R↓
RTN
LBL 77 @NM@ MED
FUNC 01
XEQ 74
R↓
RTN
LBL 35 @NM@ Q1
FUNC 01
XEQ 74
R↓
R↓
RTN
LBL 36 @NM@ Q3
FUNC 01
XEQ 74
RTN
LBL 34 @NM@ IQR
FUNC 01
XEQ 74
X<>Y
R↓
X<>Y
-
RTN
LBL 46 @NM@ kuQ
FUNC 01
XEQ 74
X<>Y
R↓
X<>Y
-
2
/
RCL "MeCFA"
60
+
XEQ IND ST X
X<>Y
R↓
×
RTN
LBL 74 @NM@ QUART
FUNC 03
L4STK
SF 25
RCL "MeDAT"
FC?C 25
GTO 99
LSTO "_TMP"
DIM?
R↓
3
X>Y?
GTO 75
R↓
INDEX "_TMP"
LBL 71
[MIN]
R↓
RCLIJ
R↓
R<>R
J+
FC? 77
GTO 71
RCL "_TMP"
DIM?
XEQ 76 @NM@ MED-RNG
X<>Y
RCL ST X
1
+
2
÷
ENTER
FP
X=0?
GTO 37
R↓
IP
1
XEQ 76 @NM@ MED-RNG
R↓
ISG ST X
NOP
XEQ 76 @NM@ MED-RNG
X<>Y
R↓
RTN
LBL 37
R↓
DSE ST X
NOP
1
XEQ 76 @NM@ MED-RNG
R↓
ISG ST X
NOP
ISG ST X
NOP
XEQ 76 @NM@ MED-RNG
X<>Y
R↓
RTN
LBL 76 @NM@ MED-RNG
FUNC 11
+
2
÷
RCL ST X
FP
X=0?
GTO 73
R↓
IP
ENTER
1
STOIJ
R↓
RCLEL
X<>Y
1
+
1
STOIJ
R↓
R↓
RCLEL
+
2
/
RTN
LBL 73
R↓
1
STOIJ
R↓
R↓
RCLEL
RTN
LBL 99
"ERR: No MeDAT!"
AVIEW
RTN
LBL 75
"ERR: MeDAT "
├"Too Small!"
AVIEW
RTN
LBL 60
FUNC 01
1
RTN
LBL 61
RCL "MeRSC"
1
X≠Y?
GTO 59
R↓
3
SQRT
RTN
LBL 59
R↓
R↓
2
RTN
LBL 62
3
RTN
LBL 63
4
RTN
LBL 64
999
RTN
LBL 65
├"1"
RTN
LBL 66
RCL "MeRSC"
1
X=Y?
├"√3"
X≠Y?
├"2"
R↓
R↓
RTN
LBL 67
├"3"
RTN
LBL 68
├"4"
RTN
LBL 69
├"999"
RTN
LBL 50
├"N"
RTN
LBL 51
├"R"
RTN
LBL 52
├"Q"
RTN
END

