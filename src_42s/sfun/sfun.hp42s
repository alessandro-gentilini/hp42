LBL "SFUN"
LBL 01
CLMENU
"L&P"
KEY 1 GTO 03
"CPLX"
KEY 2 GTO 04
"TRIG"
KEY 3 GTO 05
"HYP"
KEY 4 GTO 06
"PERC"
KEY 5 GTO 07
"INT"
KEY 6 GTO 08
KEY 7 GTO 02
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"BINO"
KEY 1 GTO 09
"ERR"
KEY 2 GTO 10
"MISC"
KEY 3 GTO 11
KEY 7 GTO 01
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03
CLMENU
"lnYX"
KEY 1 XEQ "lnYX"
"ln2"
KEY 3 XEQ "ln2"
"LN"
KEY 4 XEQ 14
"LOG"
KEY 5 XEQ 15
KEY 7 GTO 13
KEY 8 GTO 12
KEY 9 GTO 01
MENU
STOP
GTO 03
LBL 12
CLMENU
"Y↑X"
KEY 1 XEQ 16
"2↑X"
KEY 3 XEQ 58
"E↑X"
KEY 4 XEQ 17
"10↑X"
KEY 5 XEQ 18
KEY 7 GTO 03
KEY 8 GTO 13
KEY 9 GTO 01
MENU
STOP
GTO 12
LBL 13
CLMENU
"Y√X"
KEY 1 XEQ "YROOT"
"2√X"
KEY 3 XEQ 57
"3√X"
KEY 4 XEQ 56
"5√X"
KEY 5 XEQ 55
"7√X"
KEY 6 XEQ 54
KEY 7 GTO 12
KEY 8 GTO 03
KEY 9 GTO 01
MENU
STOP
GTO 13
LBL 04
CLMENU
"RPART"
KEY 1 XEQ "RPART"
"IPART"
KEY 2 XEQ "IPART"
"CONJ"
KEY 3 XEQ "CONJ"
"CABS"
KEY 4 XEQ "CABS"
"CARG"
KEY 5 XEQ "CARG"
KEY 9 GTO 01
MENU
STOP
GTO 04
LBL 05
CLMENU
"SIN"
KEY 1 XEQ 21
"COS"
KEY 2 XEQ 22
"TAN"
KEY 3 XEQ 23
"ASIN"
KEY 4 XEQ 24
"ACOS"
KEY 5 XEQ 25
"ATAN"
KEY 6 XEQ 26
KEY 7 GTO 20
KEY 8 GTO 19
KEY 9 GTO 01
MENU
STOP
GTO 05
LBL 19
CLMENU
"CSC"
KEY 1 XEQ "CSC"
"SEC"
KEY 2 XEQ "SEC"
"COT"
KEY 3 XEQ "COT"
"ACSC"
KEY 4 XEQ "ACSC"
"ASEC"
KEY 5 XEQ "ASEC"
"ACOT"
KEY 6 XEQ "ACOT"
KEY 7 GTO 05
KEY 8 GTO 20
KEY 9 GTO 01
MENU
STOP
GTO 19
LBL 20
CLMENU
"TAN2"
KEY 1 XEQ "TAN2"
"HYPOT"
KEY 2 XEQ "HYPOT"
"HAV"
KEY 3 XEQ "HAV"
"AHAV"
KEY 4 XEQ "AHAV"
KEY 7 GTO 19
KEY 8 GTO 05
KEY 9 GTO 01
MENU
STOP
GTO 20
LBL 06
CLMENU
"SINH"
KEY 1 XEQ 28
"COSH"
KEY 2 XEQ 29
"TANH"
KEY 3 XEQ 30
"ASINH"
KEY 4 XEQ 31
"ACOSH"
KEY 5 XEQ 32
"ATANH"
KEY 6 XEQ 33
KEY 7 GTO 27
KEY 8 GTO 27
KEY 9 GTO 01
MENU
STOP
GTO 06
LBL 27
CLMENU
"CSCH"
KEY 1 XEQ "CSCH"
"SECH"
KEY 2 XEQ "SECH"
"COTH"
KEY 3 XEQ "COTH"
"ACSCH"
KEY 4 XEQ "ACSCH"
"ASECH"
KEY 5 XEQ "ASECH"
"ACOTH"
KEY 6 XEQ "ACOTH"
KEY 7 GTO 06
KEY 8 GTO 06
KEY 9 GTO 01
MENU
STOP
GTO 27
LBL 07
CLMENU
"%T"
KEY 1 XEQ "%T"
"%CH"
KEY 2 XEQ 34
"%"
KEY 3 XEQ 35
KEY 9 GTO 01
MENU
STOP
GTO 07
LBL 08
CLMENU
"FLOR"
KEY 1 XEQ "FLOOR"
"CEIL"
KEY 2 XEQ "CEIL"
"ROND"
KEY 3 XEQ "ROUND"
"TRUN"
KEY 4 XEQ 37
"FP"
KEY 6 XEQ 38
KEY 7 GTO 36
KEY 8 GTO 36
KEY 9 GTO 01
MENU
STOP
GTO 08
LBL 36
CLMENU
"GCD"
KEY 1 XEQ "GCD"
"LCM"
KEY 2 XEQ "LCM"
"DIV"
KEY 5 XEQ 39
"REM"
KEY 6 XEQ 40
KEY 7 GTO 08
KEY 8 GTO 08
KEY 9 GTO 01
MENU
STOP
GTO 36
LBL 09
CLMENU
"COMB"
KEY 1 XEQ 41
"PERM"
KEY 2 XEQ 42
"!"
KEY 3 XEQ 43
"PSI"
KEY 4 XEQ "DIGAMM"
"GAM"
KEY 5 XEQ 44
"BETA"
KEY 6 XEQ "BETA"
KEY 9 GTO 02
MENU
STOP
GTO 09
LBL 10
CLMENU
"ERF"
KEY 1 XEQ "ERF"
"ERFC"
KEY 2 XEQ "ERFC"
"NPDF"
KEY 4 XEQ "NPDF"
"NCDF"
KEY 5 XEQ "NCDF"
KEY 9 GTO 02
MENU
STOP
GTO 10
LBL 11
CLMENU
"||"
KEY 1 XEQ "||"
KEY 9 GTO 02
MENU
STOP
GTO 11
LBL 00
EXITALL
RTN
LBL 14
LN
RTN
LBL 15
LOG
RTN
LBL 16
Y↑X
RTN
LBL 17
E↑X
RTN
LBL 18
10↑X
RTN
LBL 21
SIN
RTN
LBL 22
COS
RTN
LBL 23
TAN
RTN
LBL 24
ASIN
RTN
LBL 25
ACOS
RTN
LBL 26
ATAN
RTN
LBL 28
SINH
RTN
LBL 29
COSH
RTN
LBL 30
TANH
RTN
LBL 31
ASINH
RTN
LBL 32
ACOSH
RTN
LBL 33
ATANH
RTN
LBL 34
%CH
RTN
LBL 35
%
RTN
LBL 37
IP
RTN
LBL 38
FP
RTN
LBL 39
BASE÷
RTN
LBL 40
MOD
RTN
LBL 41
COMB
RTN
LBL 42
PERM
RTN
LBL 43
N!
RTN
LBL 44
GAMMA
RTN
LBL "RPART"
FUNC 11
L4STK
ENTER
XEQ 89
X=0?
RTNERR 4
68
+
GTO IND ST X
LBL 69
LBL 70
R↓
RTN
LBL 71
LBL 72
R↓
XEQ 98
X<>Y
RTN
LBL "IPART"
FUNC 11
L4STK
ENTER
XEQ 89
X=0?
RTNERR 4
72
+
GTO IND ST X
LBL 73
0
RTN
LBL 74
R↓
DIM?
NEWMAT
RTN
LBL 75
LBL 76
R↓
XEQ 98
RTN
LBL "CONJ"
FUNC 11
L4STK
ENTER
XEQ 89
X=0?
RTNERR 4
76
+
GTO IND ST X
LBL 77
LBL 78
R↓
RTN
LBL 79
LBL 80
R↓
COMPLEX
+/-
COMPLEX
RTN
LBL "CABS"
FUNC 11
L4STK
ENTER
XEQ 89
X=0?
RTNERR 4
80
+
GTO IND ST X
LBL 81
LBL 82
R↓
ABS
RTN
LBL 83
LBL 84
R↓
XEQ 47
X<>Y
RTN
LBL "CARG"
FUNC 11
L4STK
ENTER
XEQ 89
X=0?
RTNERR 4
84
+
GTO IND ST X
LBL 85
0
RTN
LBL 86
R↓
DIM?
NEWMAT
RTN
LBL 87
LBL 88
R↓
XEQ 47
RTN
LBL 98
FUNC 12
L4STK
ENTER
FS? 73
GTO 97
COMPLEX
RTN
LBL 97
RECT
COMPLEX
POLAR
RTN
LBL 47
FUNC 12
L4STK
ENTER
FS? 73
GTO 99
POLAR
COMPLEX
RECT
RTN
LBL 99
COMPLEX
RTN
LBL 89
FUNC 11
L4STK
MAT?
GTO 90
GTO 91
LBL 90
CLA
ARCL ST X
67
POSA
X>0?
GTO 96
GTO 97
LBL 96
4
RTN
LBL 97
2
RTN
LBL 91
REAL?
GTO 92
GTO 93
LBL 92
1
RTN
LBL 93
CPX?
GTO 94
GTO 95
LBL 94
3
RTN
LBL 95
0
RTN
LBL "%T"
FUNC 22
L4STK
RCL÷ ST Y
100
×
RTN
LBL "ROUND"
FUNC 11
L4STK
ENTER
FP
ABS
0.5
-
X<>Y
SIGN
LASTX
IP
0=? ST Z
GTO 67
0<? ST Z
+
RTN
LBL 67
XEQ 68
+
RTN
LBL 68
FUNC 00
L4STK
2
÷
FP
X=0?
RTNNO
RTNYES
LBL "FLOOR"
FUNC 11
L4STK
FP
LASTX
IP
0≤? ST Y
RTN
1
-
RTN
LBL "CEIL"
FUNC 11
L4STK
FP
LASTX
IP
0≥? ST Y
RTN
1
+
RTN
LBL "GCD"
FUNC 21
L4STK
ABS
IP
X<>Y
ABS
IP
X>Y?
X<>Y
LBL 66
STO ST Z
MOD
X>0?
GTO 66
R↓
RTN
LBL "LCM"
FUNC 21
L4STK
ABS
IP
X=0?
RTN
X<>Y
ABS
IP
X=0?
RTN
RCL ST Y
RCL ST Y
×
RCL ST Z
RCL ST Z
XEQ "GCD"
÷
RTN
LBL "BETA"
FUNC 21
L4STK
RCL ST Y
GAMMA
RCL ST Y
GAMMA
×
RCL ST Z
RCL ST Z
+
GAMMA
÷
RTN
LBL "DIGAMM"
FUNC 11
L4STK
LSTO "_X"
2
+
LSTO "_S"
LN
2
RCL× "_S"
1/X
-
RCL "_S"
X↑2
LSTO "_SS"
12
×
1/X
-
RCL "_SS"
RCL× "_SS"
STO ST Z
120
×
1/X
+
X<>Y
RCL× "_SS"
STO ST Z
252
×
1/X
-
X<>Y
RCL× "_SS"
STO ST Z
240
×
1/X
+
X<>Y
RCL× "_SS"
STO ST Z
660
×
1/X
5
×
-
X<>Y
RCL× "_SS"
STO ST Z
32760
×
1/X
691
×
+
X<>Y
RCL× "_SS"
12
×
1/X
-
RCL "_X"
1/X
-
RCL "_X"
1
+
1/X
-
RTN
LBL "NPDF"
FUNC 11
L4STK
X↑2
-2
÷
E↑X
2
PI
×
SQRT
÷
RTN
LBL "NCDF"
FUNC 11
L4STK
0.2316419
RCL× ST Y
1
+
1/X
X<>Y
X↑2
-2
÷
E↑X
2
PI
×
SQRT
÷
RCL  ST Y
×
0.319381530
RCL× ST Y
RCL ST Z
STO× ST Z
R↓
-0.356563782
RCL× ST Z
+
RCL ST Z
STO× ST Z
R↓
1.781477937
RCL× ST Z
+
RCL ST Z
STO× ST Z
R↓
-1.821255978
RCL× ST Z
+
RCL ST Z
STO× ST Z
R↓
1.330274429
RCL× ST Z
+
1
X<>Y
-
RTN
LBL "ERF"
FUNC 11
L4STK
ENTER
ENTER
2
SQRT
×
ABS
XEQ "NCDF"
2
×
1
-
X<>Y
X<0?
GTO 64
GTO 65
LBL 64
R↓
+/-
RTN
LBL 65
R↓
RTN
LBL "ERFC"
FUNC 11
L4STK
XEQ "ERF"
1
X<>Y
-
RTN
LBL "CSCH"
FUNC 11
L4STK
SINH
1/X
RTN
LBL "SECH"
FUNC 11
L4STK
COSH
1/X
RTN
LBL "COTH"
FUNC 11
L4STK
TANH
1/X
RTN
LBL "ACSCH"
FUNC 11
L4STK
1/X
ASINH
RTN
LBL "ASECH"
FUNC 11
L4STK
1/X
ACOSH
RTN
LBL "ACOTH"
FUNC 11
L4STK
1/X
ATANH
RTN
LBL "AHAV"
FUNC 11
L4STK
SQRT
ASIN
2
×
RTN
LBL "HAV"
FUNC 11
L4STK
COS
-2
÷
0.5
+
RTN
LBL "HYPOT"
FUNC 21
L4STK
ABS
X<>Y
ABS
COMPLEX
ABS
RTN
LBL "CSC"
FUNC 11
L4STK
SIN
1/X
RTN
LBL "SEC"
FUNC 11
L4STK
COS
1/X
RTN
LBL "COT"
FUNC 11
L4STK
TAN
1/X
RTN
LBL "ACSC"
FUNC 11
L4STK
1/X
ASIN
RTN
LBL "ASEC"
FUNC 11
L4STK
1/X
ACOS
RTN
LBL "ACOT"
FUNC 11
L4STK
1/X
ATAN
RTN
LBL "TAN2"
FUNC 21
L4STK
X>0?
GTO 59
X=0?
GTO 60
X<>Y
X<0?
GTO 61
X<>Y
÷
ATAN
PI
+
RTN
LBL 61
X<>Y
÷
ATAN
PI
-
RTN
LBL 60
X<>Y
X=0?
GTO 62
X>0?
GTO 63
PI
-2
÷
RTN
LBL 63
PI
2
÷
RTN
LBL 62
÷
RTN
LBL 59
÷
ATAN
RTN
LBL "lnYX"
FUNC 21
L4STK
LN
X<>Y
LN
÷
RTN
LBL "ln2"
FUNC 11
L4STK
LN
2
LN
÷
RTN
LBL 58
FUNC 11
L4STK
2
X<>Y
Y^X
RTN
LBL 57
FUNC 11
2
X<>Y
XEQ "YROOT"
RTN
LBL 56
FUNC 11
3
X<>Y
XEQ "YROOT"
RTN
LBL 55
FUNC 11
5
X<>Y
XEQ "YROOT"
RTN
LBL 54
FUNC 11
7
X<>Y
XEQ "YROOT"
RTN
LBL "YROOT"
FUNC 21
L4STK
REAL?
GTO 48
GTO 51
LBL 48
X≥0?
GTO 51
RCL ST Y
REAL?
GTO 49
GTO 50
LBL 49
FP
X≠0?
GTO 50
R↓
ABS
RCL ST Y
X<>Y
XEQ 51
+/-
X<>Y
2
÷
FP
X=0?
GTO 53
GTO 52
LBL 53
R↓
-1
SQRT
×
RTN
LBL 52
R↓
RTN
LBL 50
R↓
LBL 51
X<>Y
1/X
Y↑X
RTN
LBL "||"
FUNC 21
L4STK
1/X
X<>Y
1/X
+
1/X
RTN
END

