LBL "BASE"
LBL 01
CLMENU
"S&R"
KEY 1 GTO 04
"BITS"
KEY 2 GTO 05
"FUN"
KEY 3 GTO 06
"MASK"
KEY 4 GTO 07
"ARTH"
KEY 5 GTO 08
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 03
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
XEQ 92
KEY 1 XEQ 09
XEQ 93
KEY 2 XEQ 10
XEQ 94
KEY 3 XEQ 11
XEQ 95
KEY 4 XEQ 12
"BMNU"
KEY 6 XEQ 75
KEY 7 GTO 01
KEY 8 GTO 03
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03
CLMENU
XEQ 96
KEY 1 XEQ 13
XEQ 97
KEY 2 XEQ 14
XEQ 98
KEY 3 XEQ 15
XEQ 99
KEY 5 XEQ 74
KEY 7 GTO 02
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 03
LBL 04
CLMENU
"SLn"
KEY 1 XEQ "SLn"
"SRn"
KEY 2 XEQ "SRn"
"RLn"
KEY 3 XEQ "RLn"
"RRn"
KEY 4 XEQ "RRn"
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 17
KEY 8 GTO 16
KEY 9 GTO 01
MENU
STOP
GTO 04
LBL 16
CLMENU
"LJ"
KEY 1 XEQ "LJ"
"RJ"
KEY 2 XEQ "RJ"
"SHFXY"
KEY 3 XEQ "SHFXY"
"ROTXY"
KEY 4 XEQ 18
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 04
KEY 8 GTO 17
KEY 9 GTO 01
MENU
STOP
GTO 16
LBL 17
CLMENU
"SL"
KEY 1 XEQ "SL"
"SR"
KEY 2 XEQ "SR"
"RL"
KEY 3 XEQ "RL"
"RR"
KEY 4 XEQ "RR"
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 16
KEY 8 GTO 04
KEY 9 GTO 01
MENU
STOP
GTO 17
LBL 05
CLMENU
"GETB"
KEY 1 XEQ "GETB"
"SB"
KEY 2 XEQ "SETB"
"CB"
KEY 3 XEQ "CLRB"
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 9 GTO 01
MENU
STOP
GTO 05
LBL 06
CLMENU
"B#"
KEY 1 XEQ "B#"
"MSSB"
KEY 2 XEQ "MSSB"
"LSSB"
KEY 3 XEQ "LSSB"
"ILN2"
KEY 4 XEQ "ILN2"
"ISQRT"
KEY 5 XEQ "ISQRT"
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 9 GTO 01
MENU
STOP
GTO 06
LBL 07
CLMENU
"MSKL"
KEY 1 XEQ "MSKL"
"MSKR"
KEY 2 XEQ "MSKR"
"MSKn"
KEY 3 XEQ "MSKn"
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 9 GTO 01
MENU
STOP
GTO 07
LBL 08
CLMENU
"AND"
KEY 1 XEQ 20
"OR"
KEY 2 XEQ 21
"XOR"
KEY 3 XEQ 22
"NOT"
KEY 4 XEQ 23
"NEG"
KEY 5 XEQ 24
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 19
KEY 8 GTO 19
KEY 9 GTO 01
MENU
STOP
GTO 08
LBL 19
CLMENU
"+"
KEY 1 XEQ 25
"-"
KEY 2 XEQ 26
"×"
KEY 3 XEQ 27
"÷"
KEY 4 XEQ 28
"MOD"
KEY 5 XEQ 29
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 08
KEY 8 GTO 08
KEY 9 GTO 01
MENU
STOP
GTO 19
LBL 00
EXITALL
RTN
LBL 09
HEXM
RTN
LBL 10
DECM
RTN
LBL 11
OCTM
RTN
LBL 12
BINM
RTN
LBL 13
WSIZE
RTN
LBL 14
BSIGNED
RTN
LBL 15
BWRAP
RTN
LBL 18
ROTXY
RTN
LBL 20
AND
RTN
LBL 21
OR
RTN
LBL 22
XOR
RTN
LBL 23
NOT
RTN
LBL 24
BASE+/-
RTN
LBL 25
BASE+
RTN
LBL 26
BASE-
RTN
LBL 27
BASE×
RTN
LBL 28
BASE÷
RTN
LBL 29
MOD
RTN
LBL "B#"
FUNC 11
L4STK
IP
0
X<>Y
1
NOT
X<>Y
LBL 51
RCL ST Y
X<>Y
AND
LASTX
X=Y?
GTO 52
R↓
1
STO+ ST T
LBL 52
R↓
X=0?
GTO 53
X<>Y
-1
ROTXY
X<>Y
GTO 51
LBL 53
R↓
R↓
RTN
LBL "MSKn"
FUNC 11
L4STK
IP
X<>Y
IP
X>0?
GTO 54
0
RTN
LBL 54
WSIZE?
X>Y?
GTO 55
0
NOT
RTN
LBL 55
R↓
2
X<>Y
Y↑X
1
-
X<>Y
+/-
ROTXY
RTN
LBL "MSKR"
FUNC 11
L4STK
IP
X>0?
GTO 56
0
RTN
LBL 56
WSIZE?
X>Y?
GTO 57
0
NOT
RTN
LBL 57
R↓
2
X<>Y
Y↑X
1
-
RTN
LBL "MSKL"
FUNC 11
L4STK
IP
X>0?
GTO 58
0
RTN
LBL 58
WSIZE?
X>Y?
GTO 59
0
NOT
RTN
LBL 59
R↓
ENTER
2
X<>Y
Y↑X
1
-
X<>Y
ROTXY
RTN
LBL "SETB"
FUNC 21
L4STK
IP
X≥0?
GTO 60
R↓
RTN
LBL 60
WSIZE?
X>Y?
GTO 61
R↓
R↓
RTN
LBL 61
R↓
1
X<>Y
+/-
ROTXY
OR
RTN
LBL "CLRB"
FUNC 21
L4STK
IP
X≥0?
GTO 62
R↓
RTN
LBL 62
WSIZE?
X>Y?
GTO 63
R↓
R↓
RTN
LBL 63
R↓
1
X<>Y
+/-
ROTXY
NOT
AND
RTN
LBL "GETB"
FUNC 21
L4STK
X≥0?
GTO 64
0
RTN
LBL 64
WSIZE?
X>Y?
GTO 65
0
RTN
LBL 65
R↓
BIT?
GTO 66
0
RTN
LBL 66
1
RTN
LBL "SL"
FUNC 11
L4STK
-1
ROTXY
1
NOT
AND
RTN
LBL "SR"
FUNC 11
L4STK
1
NOT
AND
1
ROTXY
RTN
LBL "RL"
FUNC 11
L4STK
-1
ROTXY
RTN
LBL "RR"
FUNC 11
L4STK
1
ROTXY
RTN
LBL "SLn"
FUNC 21
L4STK
IP
X>0?
GTO 67
R↓
RTN
LBL 67
WSIZE?
X>Y?
GTO 68
0
RTN
LBL 68
R↓
X<>Y
RCL ST Y
+/-
ROTXY
X<>Y
2
X<>Y
Y↑X
1
-
NOT
AND
RTN
LBL "SRn"
FUNC 21
L4STK
IP
X>0?
GTO 69
R↓
RTN
LBL 69
WSIZE?
X>Y?
GTO 70
0
RTN
LBL 70
R↓
X<>Y
RCL ST Y
2
X<>Y
Y↑X
1
-
NOT
AND
X<>Y
ROTXY
RTN
LBL "RLn"
FUNC 21
L4STK
IP
X>0?
GTO 71
R↓
RTN
LBL 71
+/-
ROTXY
RTN
LBL "RRn"
FUNC 21
L4STK
IP
X>0?
GTO 72
R↓
RTN
LBL 72
ROTXY
RTN
LBL "SHFXY"
FUNC 21
L4STK
IP
X<0?
GTO 49
GTO 50
LBL 49
+/-
XEQ "SLn"
RTN
LBL 50
XEQ "SRn"
RTN
LBL "RJ"
FUNC 12
L4STK
IP
0
X<>Y
LBL 47
0
BIT?
GTO 46
GTO 48
LBL 46
R↓
X<>Y
RTN
LBL 48
R↓
1
STO+ ST Z
ROTXY
GTO 47
LBL "LJ"
FUNC 12
L4STK
IP
0
X<>Y
LBL 41
WSIZE?
1
-
BIT?
GTO 42
GTO 43
LBL 42
R↓
X<>Y
RTN
LBL 43
R↓
1
STO+ ST Z
+/-
ROTXY
GTO 41
LBL "ISQRT"
FUNC 11
L4STK
ABS
SQRT
IP
RTN
LBL "ILN2"
FUNC 11
L4STK
ABS
LN
2
LN
÷
IP
RTN
LBL "MSSB"
FUNC 11
L4STK
IP
X≠0?
GTO 44
-1
RTN
LBL 44
XEQ "LJ"
1
+
WSIZE?
X<>Y
-
RTN
LBL "LSSB"
FUNC 11
L4STK
IP
X≠0?
GTO 45
-1
RTN
LBL 45
XEQ "RJ"
RTN
LBL 96
"W:"
WSIZE?
AIP
R↓
RTN
LBL 97
FS? 78
"S:+/-"
FC? 78
"S:+"
RTN
LBL 98
FS? 79
"BWR•"
FC? 79
"BWRP"
RTN
LBL 99
FC? 02
"BVA•"
FS? 02
"BVA"
RTN
LBL 92
"HEX"
FS? 71
├"•"
RTN
LBL 93
"DEC"
SF 81
FS? 71
CF 81
FS? 70
CF 81
FS? 68
CF 81
FS? 81
├"•"
RTN
LBL 94
"OCT"
SF 81
FS? 71
CF 81
FC? 70
CF 81
FS? 81
├"•"
RTN
LBL 95
"BIN"
SF 81
FC? 68
CF 81
FS? 69
CF 81
FS? 81
├"•"
RTN
LBL 74
FS?C 02
RTN
SF 02
RTN
LBL 75
EXITALL
"Press R/S To"
"├ Return"
AVIEW
HEXM
STOP
RTN
LBL "BVIEW"
FUNC 11
L4STK
0
SF 25
BIT?
FS?C 25
GTO 79
R↓
"ERR: Bad Int!"
AVIEW
RTN
LBL 79
R↓
LSTO "X"
20
1
FS? 68
STO+ ST Y
FS? 70
STO+ ST Y
FS? 71
STO+ ST Y
R↓
LSTO "SBMD"
R↓
CF 81
SF 25
RCL "GrMod"
FS?C 25
SF 81
FC? 81
GTO 88
0
STO "GrMod"
R↓
LBL 88
CLLCD
EXITALL
63
1000
÷
LSTO "CTR"
LBL 91
LBL 76
RCL "CTR"
IP
32
X>Y?
GTO 73
GTO 83
LBL 73
1
RCL "CTR"
4
×
1
+
GTO 90
LBL 83
9
RCL "CTR"
32
-
4
×
1
+
LBL 90
63
RCL "CTR"
IP
-
WSIZE?
X>Y?
GTO 80
GTO 81
LBL 80
R↓
+/-
1
X<>Y
ROTXY
RCL "X"
AND
X=0?
GTO 85
GTO 86
LBL 85
R↓
0
GTO 87
LBL 86
R↓
1
LBL 87
LSTO "CB"
GTO 82
LBL 81
R↓
R↓
0
LSTO "CB"
FS? 02
GTO 84
LBL 82
R↓
RCL "CTR"
IP
4
÷
IP
2
÷
FP
X=0?
GTO 40
GTO 39
LBL 40
R↓
0
GTO 77
LBL 39
R↓
2
LBL 77
RCL+ "CB"
XEQ 78
LBL 84
ISG  "CTR"
GTO 91
FC? 81
GTO 89
GETKEY
CLLCD
"BVIEW FINISHED"
AVIEW
LBL 89
RCL "SBMD"
20
X=Y?
DECM
R↓
21
X=Y?
BINM
R↓
22
X=Y?
OCTM
R↓
23
X=Y?
HEXM
R↓
RCL "X"
RTN
LBL 78
FUNC 30
L4STK
IP
35
+
XEQ IND ST X
R↓
AGRAPH
RTN
LBL 35
"•μ•"
RTN
LBL 36
"£•←"
RTN
LBL 37
"_Q_"
RTN
LBL 38
"R_P"
RTN
END

