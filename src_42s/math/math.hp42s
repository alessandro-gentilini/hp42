LBL "CPLXF"
LBL 01
CLMENU
"RPART"
KEY 1 XEQ "RPART"
"IPART"
KEY 2 XEQ "IPART"
"CONJ"
KEY 3 XEQ "CONJ"
"CABS"
KEY 4 XEQ "CABS"
"CARG"
KEY 5 XEQ "CARG"
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00
EXITALL
RTN
LBL "RPART"
FUNC 11
L4STK
ENTER
XEQ 80
10
+
GTO IND ST X
LBL 10
RTNERR 4
LBL 11
LBL 12
R↓
RTN
LBL 13
LBL 14
R↓
XEQ 90
X<>Y
RTN
LBL "IPART"
FUNC 11
L4STK
ENTER
XEQ 80
20
+
GTO IND ST X
LBL 20
RTNERR 4
LBL 21
0
RTN
LBL 22
R↓
DIM?
NEWMAT
RTN
LBL 23
LBL 24
R↓
XEQ 90
RTN
LBL "CONJ"
FUNC 11
L4STK
ENTER
XEQ 80
30
+
GTO IND ST X
LBL 30
RTNERR 4
LBL 31
LBL 32
R↓
RTN
LBL 33
LBL 34
R↓
COMPLEX
+/-
COMPLEX
RTN
LBL "CABS"
FUNC 11
L4STK
ENTER
XEQ 80
40
+
GTO IND ST X
LBL 40
RTNERR 4
LBL 41
LBL 42
R↓
ABS
RTN
LBL 43
LBL 44
R↓
XEQ 92
X<>Y
RTN
LBL "CARG"
FUNC 11
L4STK
ENTER
XEQ 80
50
+
GTO IND ST X
LBL 50
RTNERR 4
LBL 51
0
RTN
LBL 52
R↓
DIM?
NEWMAT
RTN
LBL 53
LBL 54
R↓
XEQ 92
RTN
LBL 90
FUNC 12
L4STK
ENTER
FS? 73
GTO 91
COMPLEX
RTN
LBL 91
RECT
COMPLEX
POLAR
RTN
LBL 92
FUNC 12
L4STK
ENTER
FS? 73
GTO 93
POLAR
COMPLEX
RECT
RTN
LBL 93
COMPLEX
RTN
LBL 80
FUNC 11
L4STK
MAT?
GTO 81
GTO 82
LBL 81
CLA
ARCL ST X
67
POSA
X>0?
GTO 87
GTO 88
LBL 87
4
RTN
LBL 88
2
RTN
LBL 82
REAL?
GTO 83
GTO 84
LBL 83
1
RTN
LBL 84
CPX?
GTO 85
GTO 86
LBL 85
3
RTN
LBL 86
0
RTN
END

LBL "PERCFN"
LBL 01
CLMENU
"%T"
KEY 1 XEQ "%T"
"%CH"
KEY 2 XEQ 02
"%"
KEY 3 XEQ 03
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00
EXITALL
RTN
LBL 02
%CH
RTN
LBL 03
%
RTN
LBL "%T"
FUNC 21
L4STK
X<>Y
÷
100
×
RTN
END

LBL "INTFUN"
LBL 01
CLMENU
"FLOR"
KEY 1 XEQ "FLOOR"
"CEIL"
KEY 2 XEQ "CEIL"
"ROND"
KEY 3 XEQ "ROUND"
"TRUN"
KEY 4 XEQ 03
"FP"
KEY 6 XEQ 04
KEY 7 GTO 02
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"GCD"
KEY 1 XEQ "GCD"
"LCM"
KEY 2 XEQ "LCM"
"DIV"
KEY 5 XEQ 05
"REM"
KEY 6 XEQ 06
KEY 7 GTO 01
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 00
EXITALL
RTN
LBL 03
IP
RTN
LBL 04
FP
RTN
LBL 05
BASE÷
RTN
LBL 06
MOD
RTN
LBL "ROUND"
FUNC 11
L4STK
ENTER
FP
ABS
0.5
-
X<>Y
SIGN
LASTX
IP
0=? ST Z
GTO 92
0<? ST Z
+
RTN
LBL 92
XEQ 93
+
RTN
LBL 93
FUNC 00
L4STK
2
÷
FP
X=0?
RTNNO
RTNYES
LBL "FLOOR"
FUNC 11
L4STK
FP
LASTX
IP
0≤? ST Y
RTN
1
-
RTN
LBL "CEIL"
FUNC 11
L4STK
FP
LASTX
IP
0≥? ST Y
RTN
1
+
RTN
LBL "GCD"
FUNC 21
L4STK
ABS
IP
X<>Y
ABS
IP
X>Y?
X<>Y
LBL 91
STO ST Z
MOD
X>0?
GTO 91
R↓
RTN
LBL "LCM"
FUNC 21
L4STK
ABS
IP
X=0?
RTN
X<>Y
ABS
IP
X=0?
RTN
RCL ST Y
RCL ST Y
×
RCL ST Z
RCL ST Z
XEQ "GCD"
÷
RTN
END

LBL "BINO"
LBL 01
CLMENU
"COMB"
KEY 1 XEQ 02
"PERM"
KEY 2 XEQ 03
"!"
KEY 3 XEQ "FACT"
"GAM"
KEY 5 XEQ 04
"BETA"
KEY 6 XEQ "BETA"
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00
EXITALL
RTN
LBL 02
COMB
RTN
LBL 03
PERM
RTN
LBL 04
GAMMA
RTN
LBL "BETA"
FUNC 21
L4STK
RCL ST Y
GAMMA
RCL ST Y
GAMMA
×
RCL ST Z
RCL ST Z
+
GAMMA
÷
RTN
LBL "FACT"
FUNC 11
L4STK
ENTER
FP
X≠0?
RTNERR 5
R↓
1
+
GAMMA
RTN
END

LBL "ERRFUN"
LBL 01
CLMENU
"ERF"
KEY 1 XEQ "ERF"
"ERFC"
KEY 2 XEQ "ERFC"
"NPDF"
KEY 4 XEQ "NPDF"
"NCDF"
KEY 5 XEQ "NCDF"
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00
EXITALL
RTN
LBL "NPDF"
FUNC 11
L4STK
X↑2
-2
÷
E↑X
2
PI
×
SQRT
÷
RTN
LBL "NCDF"
FUNC 11
L4STK
0.2316419
RCL× ST Y
1
+
1/X
X<>Y
X↑2
-2
÷
E↑X
2
PI
×
SQRT
÷
RCL  ST Y
×
0.319381530
RCL× ST Y
RCL ST Z
STO× ST Z
R↓
-0.356563782
RCL× ST Z
+
RCL ST Z
STO× ST Z
R↓
1.781477937
RCL× ST Z
+
RCL ST Z
STO× ST Z
R↓
-1.821255978
RCL× ST Z
+
RCL ST Z
STO× ST Z
R↓
1.330274429
RCL× ST Z
+
1
X<>Y
-
RTN
LBL "ERF"
FUNC 11
L4STK
ENTER
ENTER
2
SQRT
×
ABS
XEQ "NCDF"
2
×
1
-
X<>Y
X<0?
GTO 11
GTO 12
LBL 11
R↓
+/-
RTN
LBL 12
R↓
RTN
LBL "ERFC"
FUNC 11
L4STK
XEQ "ERF"
1
X<>Y
-
RTN
END

LBL "HYPTRG"
LBL 01
CLMENU
"SINH"
KEY 1 XEQ 03
"COSH"
KEY 2 XEQ 04
"TANH"
KEY 3 XEQ 05
"ASINH"
KEY 4 XEQ 06
"ACOSH"
KEY 5 XEQ 07
"ATANH"
KEY 6 XEQ 08
KEY 7 GTO 02
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"CSCH"
KEY 1 XEQ "CSCH"
"SECH"
KEY 2 XEQ "SECH"
"COTH"
KEY 3 XEQ "COTH"
"ACSCH"
KEY 4 XEQ "ACSCH"
"ASECH"
KEY 5 XEQ "ASECH"
"ACOTH"
KEY 6 XEQ "ACOTH"
KEY 7 GTO 01
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 00
EXITALL
RTN
LBL 03
SINH
RTN
LBL 04
COSH
RTN
LBL 05
TANH
RTN
LBL 06
ASINH
RTN
LBL 07
ACOSH
RTN
LBL 08
ATANH
RTN
LBL "CSCH"
FUNC 11
L4STK
SINH
1/X
RTN
LBL "SECH"
FUNC 11
L4STK
COSH
1/X
RTN
LBL "COTH"
FUNC 11
L4STK
TANH
1/X
RTN
LBL "ACSCH"
FUNC 11
L4STK
1/X
ASINH
RTN
LBL "ASECH"
FUNC 11
L4STK
1/X
ACOSH
RTN
LBL "ACOTH"
FUNC 11
L4STK
1/X
ATANH
RTN
END

LBL "TRIG"
LBL 01
CLMENU
"SIN"
KEY 1 XEQ 04
"COS"
KEY 2 XEQ 05
"TAN"
KEY 3 XEQ 06
"ASIN"
KEY 4 XEQ 07
"ACOS"
KEY 5 XEQ 08
"ATAN"
KEY 6 XEQ 09
KEY 7 GTO 03
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"CSC"
KEY 1 XEQ "CSC"
"SEC"
KEY 2 XEQ "SEC"
"COT"
KEY 3 XEQ "COT"
"ACSC"
KEY 4 XEQ "ACSC"
"ASEC"
KEY 5 XEQ "ASEC"
"ACOT"
KEY 6 XEQ "ACOT"
KEY 7 GTO 01
KEY 8 GTO 03
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03
CLMENU
"TAN2"
KEY 1 XEQ "TAN2"
"HYPOT"
KEY 2 XEQ "HYPOT"
"HAV"
KEY 3 XEQ "HAV"
"AHAV"
KEY 4 XEQ "AHAV"
KEY 7 GTO 02
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 03
LBL 00
EXITALL
RTN
LBL 04
SIN
RTN
LBL 05
COS
RTN
LBL 06
TAN
RTN
LBL 07
ASIN
RTN
LBL 08
ACOS
RTN
LBL 09
ATAN
RTN
LBL "AHAV"
FUNC 11
L4STK
SQRT
ASIN
2
×
RTN
LBL "HAV"
FUNC 11
L4STK
COS
-2
÷
0.5
+
RTN
LBL "HYPOT"
FUNC 21
L4STK
ABS
X<>Y
ABS
COMPLEX
ABS
RTN
LBL "CSC"
FUNC 11
L4STK
SIN
1/X
RTN
LBL "SEC"
FUNC 11
L4STK
COS
1/X
RTN
LBL "COT"
FUNC 11
L4STK
TAN
1/X
RTN
LBL "ACSC"
FUNC 11
L4STK
1/X
ASIN
RTN
LBL "ASEC"
FUNC 11
L4STK
1/X
ACOS
RTN
LBL "ACOT"
FUNC 11
L4STK
1/X
ATAN
RTN
LBL "TAN2"
FUNC 21
L4STK
X>0?
GTO 81
X=0?
GTO 82
X<>Y
X<0?
GTO 83
X<>Y
÷
ATAN
PI
+
RTN
LBL 83
X<>Y
÷
ATAN
PI
-
RTN
LBL 82
X<>Y
X=0?
GTO 84
X>0?
GTO 85
PI
-2
÷
RTN
LBL 85
PI
2
÷
RTN
LBL 84
÷
RTN
LBL 81
÷
ATAN
RTN
END

LBL "LPRT"
LBL 01
CLMENU
"lnYX"
KEY 1 XEQ "lnYX"
"ln2"
KEY 3 XEQ "ln2"
"LN"
KEY 4 XEQ 04
"LOG"
KEY 5 XEQ 05
KEY 7 GTO 03
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"Y↑X"
KEY 1 XEQ 06
"2↑X"
KEY 3 XEQ 97
"EXP"
KEY 4 XEQ "EXP"
"10↑X"
KEY 5 XEQ 07
KEY 7 GTO 01
KEY 8 GTO 03
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03
CLMENU
"Y√X"
KEY 1 XEQ "YROOT"
"2√X"
KEY 3 XEQ 95
"3√X"
KEY 4 XEQ 94
"5√X"
KEY 5 XEQ 93
"7√X"
KEY 6 XEQ 92
KEY 7 GTO 02
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 03
LBL 00
EXITALL
RTN
LBL 04
LN
RTN
LBL 05
LOG
RTN
LBL 06
Y↑X
RTN
LBL 07
10↑X
RTN
LBL "lnYX"
FUNC 21
L4STK
LN
X<>Y
LN
÷
RTN
LBL "ln2"
FUNC 11
L4STK
LN
2
LN
÷
RTN
LBL 97
FUNC 11
L4STK
2
X<>Y
Y^X
RTN
LBL 95
FUNC 11
2
X<>Y
XEQ "YROOT"
RTN
LBL 94
FUNC 11
3
X<>Y
XEQ "YROOT"
RTN
LBL 93
FUNC 11
5
X<>Y
XEQ "YROOT"
RTN
LBL 92
FUNC 11
7
X<>Y
XEQ "YROOT"
RTN
LBL "YROOT"
FUNC 21
L4STK
REAL?
GTO 81
GTO 84
LBL 81
X≥0?
GTO 84
RCL ST Y
REAL?
GTO 82
GTO 83
LBL 82
FP
X≠0?
GTO 83
R↓
ABS
RCL ST Y
X<>Y
XEQ 84
+/-
X<>Y
2
÷
FP
X=0?
GTO 86
GTO 85
LBL 86
R↓
-1
SQRT
×
RTN
LBL 85
R↓
RTN
LBL 83
R↓
LBL 84
X<>Y
1/X
Y↑X
RTN
END

LBL "NLA"
LBL 01
CLMENU
"IDEN"
KEY 1 XEQ "MATIDN"
"DIAG"
KEY 2 XEQ "MATDIG"
"TR"
KEY 3 XEQ "MATTR"
"CPLY"
KEY 4 XEQ "MCPOLY"
KEY 7 GTO 02
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"MAT"
KEY 1 XEQ 98
"X"
KEY 2 XEQ 97
"EVCP"
KEY 6 XEQ "EQCPLY"
KEY 7 GTO 01
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 00
EXITALL
RTN
LBL "MCPOLY"
FUNC 11
L4STK
LSTO "TMPA"
DIM?
Rv
XEQ "MATIDN"
LSTO "TMPM"
DIM?
Rv
LSTO "J"
1
+
LSTO "JP1"
ISG "JP1"
NOP
1
X<>Y
NEWMAT
LSTO "TMPP"
Rv
0
LBL 28
RCL "TMPA"
RCL "TMPM"
RCL  ST Z
XEQ 96
×
LSTO "TMPM"
XEQ "MATTR"
RCL "JP1"
RCL "J"
-
X<>Y
RCL ST Y
1
-
÷
X<>Y
1
X<>Y
INDEX "TMPP"
STOIJ
Rv
Rv
STOEL
DSE "J"
GTO 28
LBL 24
INDEX "TMPP"
-1
STOEL
RCL "TMPP"
+/-
RTN
LBL "MATTR"
FUNC 11
L4STK
LSTO "TMPM"
INDEX "TMPM"
R↓
0
LBL 27
RCLEL
+
J+
FS? 77
GTO 26
I+
GTO 27
LBL 26
RTN
LBL "MATIDN"
FUNC 11
L4STK
ENTER
NEWMAT
LSTO "TMPM"
INDEX "TMPM"
1
LBL 21
STOEL
J+
FS? 77
GTO 22
I+
GTO 21
LBL 22
RCL "TMPM"
RTN
LBL "MATDIG"
FUNC 11
L4STK
LSTO "TMPM"
INDEX "TMPM"
DIM?
×
ENTER
DIM "TMPM"
1000
÷
1
+
LSTO "I"
LBL 23
1
RCL "I"
STOIJ
R↓
R↓
RCLEL
0
STOEL
R↓
RCL "I"
RCL "I"
STOIJ
R↓
R↓
STOEL
ISG "I"
GTO 23
RCL "TMPM"
RTN
LBL "EQCPLY"
MVAR "CPM"
MVAR "X"
RCL "CPM"
RCL "X"
XEQ 96
DET
RTN
LBL 96
FUNC 21
L4STK
X<>Y
LSTO "TMPM"
INDEX "TMPM"
R↓
LBL 29
RCLEL
RCL- ST Y
STOEL
R↓
J+
FS? 77
GTO 25
I+
GTO 29
LBL 25
RCL "TMPM"
RTN
LBL 98
FS? 64
RCL "CPM"
STO "CPM"
RTN
LBL 97
FS? 64
RCL "X"
STO "X"
RTN
END

LBL "POLY"
LBL 01
CLMENU
"VIEW"
KEY 1 XEQ "VPOLY"
"EVAL"
KEY 2 XEQ "PEVAL"
"EVAL1"
KEY 3 XEQ "PEVL1"
"EVAL2"
KEY 4 XEQ "PEVL2"
"QUADE"
KEY 6 XEQ "QUADE"
KEY 7 GTO 03
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"PDEFLT"
KEY 1 XEQ "PDEFLT"
"PLGRR"
KEY 2 XEQ "PLGRR"
"PR1ST"
KEY 4 XEQ "PR1ST"
"PRNXT"
KEY 5 XEQ "PRNXT"
KEY 7 GTO 01
KEY 8 GTO 03
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03
CLMENU
"POLY"
KEY 1 XEQ 98
"X"
KEY 2 XEQ 97
"EVAL"
KEY 6 XEQ "PWRP"
KEY 7 GTO 02
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 03
LBL 00
EXITALL
RTN
LBL "QUADE"
FUNC 32
L4STK
-4
RCL× ST T
×
RCL ST Y
X↑2
+
SQRT
2
RCL× ST T
÷
LASTX
RCL ST Z
+/-
X<>Y
÷
RCL ST X
RCL ST Z
-
RCL ST Z
RCL ST Z
+
RTN
LBL "PEVAL"
FUNC 21
L4STK
X<>Y
LSTO "TMPM"
INDEX "TMPM"
WRAP
0
LBL 91
RCL× ST Z
RCLEL
+
J+
FC? 77
GTO 91
RTN
LBL "PEVL1"
FUNC 22
L4STK
X<>Y
LSTO "TMPM"
INDEX "TMPM"
R↓
WRAP
0
0
LBL 92
RCL× ST Z
RCL+ ST Y
X<>Y
RCL× ST Z
RCLEL
+
X<>Y
J+
FC? 77
GTO 92
RTN
LBL "PEVL2"
FUNC 23
L4STK
LSTO "TMPX"
R↓
LSTO "TMPM"
INDEX "TMPM"
R↓
WRAP
0
0
0
LBL 93
RCL× "TMPX"
RCL+ ST Y
X<>Y
RCL× "TMPX"
RCL+ ST Z
X<>Y
RCL ST Z
RCL× "TMPX"
RCLEL
+
STO ST T
R↓
J+
FC? 77
GTO 93
2
×
RTN
LBL "PDEFLT"
FUNC 21
L4STK
REAL?
GTO 88
X<>Y
XEQ 89
X<>Y
LBL 88
X<>Y
LSTO "TMPM"
INDEX "TMPM"
WRAP
R↓
+/-
0
LBL 96
RCL× ST Y
RCLEL
X<>Y
-
STOEL
J+
FC? 77
GTO 96
RCL "TMPM"
DIM?
×
1
-
1
X<>Y
DIM "TMPM"
RCL "TMPM"
RTN
LBL 89
FUNC 11
L4STK
LSTO "TMPM"
INDEX "TMPM"
RCLEL
REAL?
GTO 87
R↓
RTN
LBL 87
R↓
ENTER
DIM?
NEWMAT
COMPLEX
RTN
LBL "VPOLY"
FUNC 00
LSTO "TMPM"
INDEX "TMPM"
WRAP
DIM?
×
LBL 90
"X^"
AIP
├":"
RCLEL
ARCL ST X
R↓
AVIEW
STOP
1
-
J+
FC? 77
GTO 90
RTN
LBL "PR1ST"
FUNC 13
L4STK
XEQ 81
RTNERR 5
LSTO "TMPP"
RAN
RAN
COMPLEX
SF 25
RCL "ACC"
FC?C 25
1e-15
50
XEQ "PLGRR"
0≠? ST Z
RTNERR 6
RCL "TMPP"
RCL "TMPP"
RCL ST Z
XEQ "PDEFLT"
RCL ST Z
RTN
LBL "PRNXT"
FUNC 33
L4STK
RCL ST Z
LSTO "TMPP"
Rv
RCL ST Y
LSTO "TMPDP"
XEQ 81
RTNERR 5
Rv
SF 25
RCL "ACC"
FC?C 25
1e-15
50
XEQ "PLGRR"
0≠? ST Z
RTNERR 6
RCL "TMPP"
X<>Y
-1
5
XEQ "PLGRR"
RCL ST X
RCL "TMPDP"
X<>Y
XEQ "PDEFLT"
RCL "TMPP"
X<>Y
RCL ST Z
RTN
LBL 81
FUNC 00
MAT?
GTO 82
RTNYES
LBL 82
DIM?
×
2
X>Y?
RTNYES
RTNNO
LBL "PLGRR"
FUNC 43
L4STK
LSTO "TMPI"
R↓
LSTO "TMPT"
R↓
LSTO "TMPG"
R↓
LSTO "TMPP"
DIM?
×
LSTO "TMPN"
1
-
LSTO "TMPNM1"
R↓
LBL 94
RCL "TMPP"
RCL "TMPG"
XEQ "PEVL2"
RCL ST Z
ABS
X<? "TMPT"
GTO 95
R↓
RCL÷ ST Y
R↓
÷
X<>Y
R↓
+/-
X<>Y
RCL× ST Y
RCL× "TMPN"
RCL÷ "TMPNM1"
1
+
SQRT
RCL× "TMPNM1"
RCL÷ "TMPN"
RCL "TMPN"
1/X
+
1/X
×
STO+ "TMPG"
DSE "TMPI"
GTO 94
3
RCL "TMPP"
RCL "TMPG"
XEQ "PEVAL"
RCL "TMPG"
RTN
LBL 95
R↓
R↓
R↓
0
X<>Y
RCL "TMPG"
RTN
LBL "PWRP"
MVAR "X"
RCL "WRPP"
RCL "X"
XEQ "PEVAL"
RTN
LBL 98
FS? 64
RCL "WRPP"
STO "WRPP"
RTN
LBL 97
FS? 64
RCL "X"
STO "X"
RTN
END

LBL "VEC3"
LBL 01
CLMENU
"→V"
KEY 1 XEQ 99
"V→"
KEY 2 XEQ 98
"DOT"
KEY 3 XEQ 02
"CROSS"
KEY 4 XEQ 03
"MAG"
KEY 5 XEQ 04
"VVIEW"
KEY 6 XEQ 96
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00
EXITALL
RTN
LBL 02
DOT
RTN
LBL 03
CROSS
RTN
LBL 04
FNRM
RTN
LBL 99
FUNC 31
XEQ 95
LSTO "TMPM"
R↓
INDEX "TMPM"
WRAP
J-
LBL 51
STOEL
R↓
J-
FC? 77
GTO 51
RCL "TMPM"
RTN
LBL 98
FUNC 13
LSTO "TMPM"
R↓
INDEX "TMPM"
WRAP
LBL 52
RCLEL
J+
FC? 77
GTO 52
RTN
LBL 96
FUNC 00
LSTO "TMPM"
INDEX "TMPM"
WRAP
1
LBL 54
CLA
AIP
├":"
RCLEL
ARCL ST X
R↓
AVIEW
STOP
1
+
J+
FC? 77
GTO 54
RTN
LBL 95
FUNC 01
1
3
NEWMAT
RTN
END

