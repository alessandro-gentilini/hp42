LBL "SFUN"
LBL 01
CLMENU
"L&P"
KEY 1 GTO 03
"CPLX"
KEY 2 GTO 04
"TRIG"
KEY 3 GTO 05
"HYP"
KEY 4 GTO 06
"PERC"
KEY 5 GTO 07
"INT"
KEY 6 GTO 08
KEY 7 GTO 02
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"BINO"
KEY 1 GTO 09
"ERR"
KEY 2 GTO 10
"MISC"
KEY 3 GTO 11
KEY 7 GTO 01
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03
CLMENU
"lnYX"
KEY 1 XEQ "lnYX"
"ln2"
KEY 3 XEQ "ln2"
"LN"
KEY 4 XEQ 14
"LOG"
KEY 5 XEQ 15
KEY 7 GTO 13
KEY 8 GTO 12
KEY 9 GTO 01
MENU
STOP
GTO 03
LBL 12
CLMENU
"Y↑X"
KEY 1 XEQ 16
"2↑X"
KEY 3 XEQ 58
"EXP"
KEY 4 XEQ "EXP"
"10↑X"
KEY 5 XEQ 17
KEY 7 GTO 03
KEY 8 GTO 13
KEY 9 GTO 01
MENU
STOP
GTO 12
LBL 13
CLMENU
"Y√X"
KEY 1 XEQ "YROOT"
"2√X"
KEY 3 XEQ 57
"3√X"
KEY 4 XEQ 56
"5√X"
KEY 5 XEQ 55
"7√X"
KEY 6 XEQ 54
KEY 7 GTO 12
KEY 8 GTO 03
KEY 9 GTO 01
MENU
STOP
GTO 13
LBL 04
CLMENU
"RPART"
KEY 1 XEQ "RPART"
"IPART"
KEY 2 XEQ "IPART"
"CONJ"
KEY 3 XEQ "CONJ"
"CABS"
KEY 4 XEQ "CABS"
"CARG"
KEY 5 XEQ "CARG"
KEY 9 GTO 01
MENU
STOP
GTO 04
LBL 05
CLMENU
"SIN"
KEY 1 XEQ 20
"COS"
KEY 2 XEQ 21
"TAN"
KEY 3 XEQ 22
"ASIN"
KEY 4 XEQ 23
"ACOS"
KEY 5 XEQ 24
"ATAN"
KEY 6 XEQ 25
KEY 7 GTO 19
KEY 8 GTO 18
KEY 9 GTO 01
MENU
STOP
GTO 05
LBL 18
CLMENU
"CSC"
KEY 1 XEQ "CSC"
"SEC"
KEY 2 XEQ "SEC"
"COT"
KEY 3 XEQ "COT"
"ACSC"
KEY 4 XEQ "ACSC"
"ASEC"
KEY 5 XEQ "ASEC"
"ACOT"
KEY 6 XEQ "ACOT"
KEY 7 GTO 05
KEY 8 GTO 19
KEY 9 GTO 01
MENU
STOP
GTO 18
LBL 19
CLMENU
"TAN2"
KEY 1 XEQ "TAN2"
"HYPOT"
KEY 2 XEQ "HYPOT"
"HAV"
KEY 3 XEQ "HAV"
"AHAV"
KEY 4 XEQ "AHAV"
KEY 7 GTO 18
KEY 8 GTO 05
KEY 9 GTO 01
MENU
STOP
GTO 19
LBL 06
CLMENU
"SINH"
KEY 1 XEQ 27
"COSH"
KEY 2 XEQ 28
"TANH"
KEY 3 XEQ 29
"ASINH"
KEY 4 XEQ 30
"ACOSH"
KEY 5 XEQ 31
"ATANH"
KEY 6 XEQ 32
KEY 7 GTO 26
KEY 8 GTO 26
KEY 9 GTO 01
MENU
STOP
GTO 06
LBL 26
CLMENU
"CSCH"
KEY 1 XEQ "CSCH"
"SECH"
KEY 2 XEQ "SECH"
"COTH"
KEY 3 XEQ "COTH"
"ACSCH"
KEY 4 XEQ "ACSCH"
"ASECH"
KEY 5 XEQ "ASECH"
"ACOTH"
KEY 6 XEQ "ACOTH"
KEY 7 GTO 06
KEY 8 GTO 06
KEY 9 GTO 01
MENU
STOP
GTO 26
LBL 07
CLMENU
"%T"
KEY 1 XEQ "%T"
"%CH"
KEY 2 XEQ 33
"%"
KEY 3 XEQ 34
KEY 9 GTO 01
MENU
STOP
GTO 07
LBL 08
CLMENU
"FLOR"
KEY 1 XEQ "FLOOR"
"CEIL"
KEY 2 XEQ "CEIL"
"ROND"
KEY 3 XEQ "ROUND"
"TRUN"
KEY 4 XEQ 36
"FP"
KEY 6 XEQ 37
KEY 7 GTO 35
KEY 8 GTO 35
KEY 9 GTO 01
MENU
STOP
GTO 08
LBL 35
CLMENU
"GCD"
KEY 1 XEQ "GCD"
"LCM"
KEY 2 XEQ "LCM"
"DIV"
KEY 5 XEQ 38
"REM"
KEY 6 XEQ 39
KEY 7 GTO 08
KEY 8 GTO 08
KEY 9 GTO 01
MENU
STOP
GTO 35
LBL 09
CLMENU
"COMB"
KEY 1 XEQ 40
"PERM"
KEY 2 XEQ 41
"!"
KEY 3 XEQ 42
"GAM"
KEY 5 XEQ 43
"BETA"
KEY 6 XEQ "BETA"
KEY 9 GTO 02
MENU
STOP
GTO 09
LBL 10
CLMENU
"ERF"
KEY 1 XEQ "ERF"
"ERFC"
KEY 2 XEQ "ERFC"
"NPDF"
KEY 4 XEQ "NPDF"
"NCDF"
KEY 5 XEQ "NCDF"
KEY 9 GTO 02
MENU
STOP
GTO 10
LBL 11
CLMENU
"||"
KEY 1 XEQ "||"
KEY 9 GTO 02
MENU
STOP
GTO 11
LBL 00
EXITALL
RTN
LBL 14
LN
RTN
LBL 15
LOG
RTN
LBL 16
Y↑X
RTN
LBL 17
10↑X
RTN
LBL 20
SIN
RTN
LBL 21
COS
RTN
LBL 22
TAN
RTN
LBL 23
ASIN
RTN
LBL 24
ACOS
RTN
LBL 25
ATAN
RTN
LBL 27
SINH
RTN
LBL 28
COSH
RTN
LBL 29
TANH
RTN
LBL 30
ASINH
RTN
LBL 31
ACOSH
RTN
LBL 32
ATANH
RTN
LBL 33
%CH
RTN
LBL 34
%
RTN
LBL 36
IP
RTN
LBL 37
FP
RTN
LBL 38
BASE÷
RTN
LBL 39
MOD
RTN
LBL 40
COMB
RTN
LBL 41
PERM
RTN
LBL 42
N!
RTN
LBL 43
GAMMA
RTN
LBL "RPART"
FUNC 11
L4STK
ENTER
XEQ 89
X=0?
RTNERR 4
1/X
68
+
GTO IND ST X
LBL 69
LBL 70
R↓
RTN
LBL 71
LBL 72
R↓
XEQ 98
X<>Y
RTN
LBL "IPART"
FUNC 11
L4STK
ENTER
XEQ 89
X=0?
RTNERR 4
1/X
72
+
GTO IND ST X
LBL 73
0
RTN
LBL 74
R↓
DIM?
NEWMAT
RTN
LBL 75
LBL 76
R↓
XEQ 98
RTN
LBL "CONJ"
FUNC 11
L4STK
ENTER
XEQ 89
X=0?
RTNERR 4
1/X
76
+
GTO IND ST X
LBL 77
LBL 78
R↓
RTN
LBL 79
LBL 80
R↓
COMPLEX
+/-
COMPLEX
RTN
LBL "CABS"
FUNC 11
L4STK
ENTER
XEQ 89
X=0?
RTNERR 4
1/X
80
+
GTO IND ST X
LBL 81
LBL 82
R↓
ABS
RTN
LBL 83
LBL 84
R↓
XEQ 98
X<>Y
RTN
LBL "CARG"
FUNC 11
L4STK
ENTER
XEQ 89
X=0?
RTNERR 4
1/X
84
+
GTO IND ST X
LBL 85
0
RTN
LBL 86
R↓
DIM?
NEWMAT
RTN
LBL 87
LBL 88
R↓
XEQ 98
RTN
LBL 98
FUNC 12
L4STK
ENTER
FS? 73
GTO 97
COMPLEX
RTN
LBL 97
RECT
COMPLEX
POLAR
RTN
LBL 98
FUNC 12
L4STK
ENTER
FS? 73
GTO 99
POLAR
COMPLEX
RECT
RTN
LBL 99
COMPLEX
RTN
LBL 89
FUNC 11
L4STK
MAT?
GTO 90
GTO 91
LBL 90
CLA
ARCL ST X
67
POSA
X>0?
GTO 96
GTO 97
LBL 96
4
RTN
LBL 97
2
RTN
LBL 91
REAL?
GTO 92
GTO 93
LBL 92
1
RTN
LBL 93
CPX?
GTO 94
GTO 95
LBL 94
3
RTN
LBL 95
0
RTN
LBL "%T"
FUNC 21
L4STK
X<>Y
÷
100
×
RTN
LBL "ROUND"
FUNC 11
L4STK
ENTER
FP
ABS
0.5
-
X<>Y
SIGN
LASTX
IP
0=? ST Z
GTO 67
0<? ST Z
+
RTN
LBL 67
XEQ 68
+
RTN
LBL 68
FUNC 00
L4STK
2
÷
FP
X=0?
RTNNO
RTNYES
LBL "FLOOR"
FUNC 11
L4STK
FP
LASTX
IP
0≤? ST Y
RTN
1
-
RTN
LBL "CEIL"
FUNC 11
L4STK
FP
LASTX
IP
0≥? ST Y
RTN
1
+
RTN
LBL "GCD"
FUNC 21
L4STK
ABS
IP
X<>Y
ABS
IP
X>Y?
X<>Y
LBL 66
STO ST Z
MOD
X>0?
GTO 66
R↓
RTN
LBL "LCM"
FUNC 21
L4STK
ABS
IP
X=0?
RTN
X<>Y
ABS
IP
X=0?
RTN
RCL ST Y
RCL ST Y
×
RCL ST Z
RCL ST Z
XEQ "GCD"
÷
RTN
LBL "BETA"
FUNC 21
L4STK
RCL ST Y
GAMMA
RCL ST Y
GAMMA
×
RCL ST Z
RCL ST Z
+
GAMMA
÷
RTN
LBL "NPDF"
FUNC 11
L4STK
X↑2
-2
÷
E↑X
2
PI
×
SQRT
÷
RTN
LBL "NCDF"
FUNC 11
L4STK
0.2316419
RCL× ST Y
1
+
1/X
X<>Y
X↑2
-2
÷
E↑X
2
PI
×
SQRT
÷
RCL  ST Y
×
0.319381530
RCL× ST Y
RCL ST Z
STO× ST Z
R↓
-0.356563782
RCL× ST Z
+
RCL ST Z
STO× ST Z
R↓
1.781477937
RCL× ST Z
+
RCL ST Z
STO× ST Z
R↓
-1.821255978
RCL× ST Z
+
RCL ST Z
STO× ST Z
R↓
1.330274429
RCL× ST Z
+
1
X<>Y
-
RTN
LBL "ERF"
FUNC 11
L4STK
ENTER
ENTER
2
SQRT
×
ABS
XEQ "NCDF"
2
×
1
-
X<>Y
X<0?
GTO 64
GTO 65
LBL 64
R↓
+/-
RTN
LBL 65
R↓
RTN
LBL "ERFC"
FUNC 11
L4STK
XEQ "ERF"
1
X<>Y
-
RTN
LBL "CSCH"
FUNC 11
L4STK
SINH
1/X
RTN
LBL "SECH"
FUNC 11
L4STK
COSH
1/X
RTN
LBL "COTH"
FUNC 11
L4STK
TANH
1/X
RTN
LBL "ACSCH"
FUNC 11
L4STK
1/X
ASINH
RTN
LBL "ASECH"
FUNC 11
L4STK
1/X
ACOSH
RTN
LBL "ACOTH"
FUNC 11
L4STK
1/X
ATANH
RTN
LBL "AHAV"
FUNC 11
L4STK
SQRT
ASIN
2
×
RTN
LBL "HAV"
FUNC 11
L4STK
COS
-2
÷
0.5
+
RTN
LBL "HYPOT"
FUNC 21
L4STK
ABS
X<>Y
ABS
COMPLEX
ABS
RTN
LBL "CSC"
FUNC 11
L4STK
SIN
1/X
RTN
LBL "SEC"
FUNC 11
L4STK
COS
1/X
RTN
LBL "COT"
FUNC 11
L4STK
TAN
1/X
RTN
LBL "ACSC"
FUNC 11
L4STK
1/X
ASIN
RTN
LBL "ASEC"
FUNC 11
L4STK
1/X
ACOS
RTN
LBL "ACOT"
FUNC 11
L4STK
1/X
ATAN
RTN
LBL "TAN2"
FUNC 21
L4STK
X>0?
GTO 59
X=0?
GTO 60
X<>Y
X<0?
GTO 61
X<>Y
÷
ATAN
PI
+
RTN
LBL 61
X<>Y
÷
ATAN
PI
-
RTN
LBL 60
X<>Y
X=0?
GTO 62
X>0?
GTO 63
PI
-2
÷
RTN
LBL 63
PI
2
÷
RTN
LBL 62
÷
RTN
LBL 59
÷
ATAN
RTN
LBL "lnYX"
FUNC 21
L4STK
LN
X<>Y
LN
÷
RTN
LBL "ln2"
FUNC 11
L4STK
LN
2
LN
÷
RTN
LBL 58
FUNC 11
L4STK
2
X<>Y
Y^X
RTN
LBL 57
FUNC 11
2
X<>Y
XEQ "YROOT"
RTN
LBL 56
FUNC 11
3
X<>Y
XEQ "YROOT"
RTN
LBL 55
FUNC 11
5
X<>Y
XEQ "YROOT"
RTN
LBL 54
FUNC 11
7
X<>Y
XEQ "YROOT"
RTN
LBL "YROOT"
FUNC 21
L4STK
REAL?
GTO 48
GTO 51
LBL 48
X≥0?
GTO 51
RCL ST Y
REAL?
GTO 49
GTO 50
LBL 49
FP
X≠0?
GTO 50
R↓
ABS
RCL ST Y
X<>Y
XEQ 51
+/-
X<>Y
2
÷
FP
X=0?
GTO 53
GTO 52
LBL 53
R↓
-1
SQRT
×
RTN
LBL 52
R↓
RTN
LBL 50
R↓
LBL 51
X<>Y
1/X
Y↑X
RTN
LBL "||"
FUNC 21
L4STK
1/X
X<>Y
1/X
+
1/X
RTN
END

LBL "NLA"
LBL 01
CLMENU
"IDEN"
KEY 1 XEQ "MXIDN"
"DIAG"
KEY 2 XEQ "MXDIAG"
"TR"
KEY 3 XEQ "MXTR"
"CPLY"
KEY 4 XEQ "MXCPLY"
"EDIT"
KEY 6 XEQ 96
KEY 7 GTO 02
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"MAT"
KEY 1 XEQ 98
"X"
KEY 2 XEQ 97
"EVCP"
KEY 6 XEQ "EQCPLY"
KEY 7 GTO 01
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 00
EXITALL
RTN
LBL "MXCPLY"
FUNC 11
L4STK
LSTO "_A"
DIM?
XEQ "MXIDN"
LSTO "_M"
LSTO "_I"
R↓
1
+
1
X<>Y
NEWMAT
LSTO "_P"
INDEX "_P"
R↓
-1
STOEL
J+
+/-
LSTO "_CTR"
0
LBL 28
RCL "_A"
RCL "_M"
RCL "_I"
RCL× ST T
-
×
LSTO "_M"
XEQ "MXTR"
RCL "_CTR"
÷
STOEL
ISG "_CTR"
NOP
J+
FC? 77
GTO 28
RCL "_P"
+/-
RTN
LBL "MXTR"
FUNC 11
L4STK
LSTO "_M"
DIM?
1
+
DIM "_M"
1
1
NEWMAT
SIGN
LSTO "_P"
R↓
1
X<>Y
DIM "_P"
RCL "_P"
RCL "_M"
TRANS
×
RSUM
DET
RTN
LBL "MXIDN"
FUNC 11
L4STK
1
NEWMAT
SIGN
XEQ "MXDIAG"
RTN
LBL "MXDIAG"
FUNC 11
L4STK
LSTO "_M"
DIM?
×
1
X=Y?
GTO 23
RCL+ ST Y
X<>Y
DIM "_M"
RCL "_M"
TRANS
STO "_M"
R↓
ENTER
DIM "_M"
LBL 23
                @@@ All done.  Return
RCL "_M"
RTN
LBL "EQCPLY"
MVAR "CPM"
MVAR "X"
RCL "CPM"
RCL "X"
RCL "CPM"
DIM?
R↓
XEQ "MXIDN"
×
-
DET
RTN
LBL 98
FS? 64
RCL "CPM"
STO "CPM"
RTN
LBL 97
FS? 64
RCL "X"
STO "X"
RTN
LBL 96
FUNC 11
EDIT
"Enter data; R/S"
├" to end"
PROMPT
EXITALL
RTN
END

LBL "POLY"
LBL 01
CLMENU
"NEW"
KEY 1 XEQ "NEWPLY"
"INTRP"
KEY 2 XEQ "PINTRP"
"EDIT"
KEY 5 XEQ 78
"VIEW"
KEY 6 XEQ "VPOLY"
KEY 7 GTO 04
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"SLV2"
KEY 1 XEQ "P2SLV"
"SLV1"
KEY 2 XEQ "P1SLV"
"R1ST"
KEY 3 XEQ "PR1ST"
"RNXT"
KEY 4 XEQ "PRNXT"
"VIEW"
KEY 6 XEQ "VPOLY"
KEY 7 GTO 01
KEY 8 GTO 03
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03
CLMENU
"DFALT"
KEY 1 XEQ "PDEFLT"
"EVAL"
KEY 2 XEQ "PEVAL"
"EVAL1"
KEY 3 XEQ "PEVL1"
"EVAL2"
KEY 4 XEQ "PEVL2"
"LGRR"
KEY 5 XEQ "PLGRR"
"VIEW"
KEY 6 XEQ "VPOLY"
KEY 7 GTO 02
KEY 8 GTO 04
KEY 9 GTO 00
MENU
STOP
GTO 03
LBL 04
CLMENU
"POLY"
KEY 1 XEQ 98
"X"
KEY 2 XEQ 97
"EVAL"
KEY 6 XEQ "PWRP"
KEY 7 GTO 03
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 04
LBL 00
EXITALL
RTN
LBL "PINTRP"
FUNC 21
L4STK
LSTO "_YDAT"
DIM?
×
1
DIM "_YDAT"
R↓
R↓
LSTO "_XDAT"
XEQ "MXDIAG"
LSTO "_MUL"
DIM?
R↓
1
NEWMAT
SIGN
LSTO "_TPL"
DIM?
R↓
ENTER
NEWMAT
LSTO "_VM"
DIM?
R↓
INDEX "_VM"
1
X<>Y
STOIJ
LBL 79
RCL "_TPL"
PUTM
RCL "_MUL"
X<>Y
×
STO "_TPL"
J-
FC? 77
GTO 79
RCL "_YDAT"
RCL÷ "_VM"
RTN
LBL "NEWPLY"
FUNC 11
L4STK
1
X<>Y
1
+
NEWMAT
RTN
LBL 78
FUNC 11
L4STK
EDIT
"Enter data; R/S"
├" to end"
PROMPT
EXITALL
RTN
LBL "P2SLV"
FUNC 12
L4STK
LSTO "_M"
R↓
INDEX "_M"
WRAP
LBL 77
RCLEL
J+
FC? 77
GTO 77
RCL ST Z
ABS
X=0?
RTNERR 3
R↓
2
RCL× ST T
R↓
RCL÷ ST T
X<>Y
RCL÷ ST T
+/-
ENTER
X↑2
RCL- ST Z
RCL- ST Z
SQRT
RCL ST Y
RCL- ST Y
RCL ST Z
RCL+ ST Z
RTN
LBL "P1SLV"
FUNC 11
L4STK
LSTO "_M"
R↓
INDEX "_M"
WRAP
RCLEL
J+
RCLEL
÷
+/-
RTN
LBL "PEVAL"
FUNC 21
L4STK
X<>Y
LSTO "_M"
INDEX "_M"
WRAP
0
LBL 91
RCL× ST Z
RCLEL
+
J+
FC? 77
GTO 91
RTN
LBL "PEVL1"
FUNC 22
L4STK
X<>Y
LSTO "_M"
INDEX "_M"
R↓
WRAP
0
0
LBL 92
RCL× ST Z
RCL+ ST Y
X<>Y
RCL× ST Z
RCLEL
+
X<>Y
J+
FC? 77
GTO 92
RTN
LBL "PEVL2"
FUNC 23
L4STK
LSTO "_X"
R↓
LSTO "_M"
INDEX "_M"
R↓
WRAP
0
0
0
LBL 93
RCL× "_X"
RCL+ ST Y
X<>Y
RCL× "_X"
RCL+ ST Z
X<>Y
RCL ST Z
RCL× "_X"
RCLEL
+
STO ST T
R↓
J+
FC? 77
GTO 93
2
×
RTN
LBL "PDEFLT"
FUNC 22
L4STK
REAL?
GTO 88
X<>Y
XEQ 89
X<>Y
LBL 88
X<>Y
LSTO "_M"
INDEX "_M"
WRAP
R↓
+/-
0
LBL 96
RCL× ST Y
RCLEL
X<>Y
-
STOEL
J+
FC? 77
GTO 96
RCL "_M"
DIM?
×
1
-
1
X<>Y
DIM "_M"
R↓
R↓
RCL "_M"
RTN
LBL 89
FUNC 11
L4STK
LSTO "_M"
INDEX "_M"
RCLEL
REAL?
GTO 87
R↓
RTN
LBL 87
R↓
ENTER
DIM?
NEWMAT
COMPLEX
RTN
LBL "VPOLY"
FUNC 00
LSTO "_M"
INDEX "_M"
WRAP
DIM?
×
LBL 90
"X^"
AIP
├":"
RCLEL
ARCL ST X
R↓
AVIEW
STOP
1
-
J+
FC? 77
GTO 90
RTN
LBL "PR1ST"
FUNC 13
L4STK
XEQ 81
RTNERR 5
LSTO "_P"
RAN
RAN
COMPLEX
SF 25
RCL "ACC"
FC?C 25
1e-15
50
XEQ "PLGRR"
0≠? ST Z
RTNERR 6
RCL "_P"
RCL "_P"
RCL ST Z
XEQ "PDEFLT"
X<>Y
R↓
RCL ST Z
RTN
LBL "PRNXT"
FUNC 33
L4STK
RCL ST Z
LSTO "_P"
R↓
RCL ST Y
LSTO "_DP"
XEQ 81
RTNERR 5
R↓
SF 25
RCL "ACC"
FC?C 25
1e-15
50
XEQ "PLGRR"
RCL ST Z
X≠0?
RTNERR 6
R↓
RCL "_P"
X<>Y
-1
5
XEQ "PLGRR"
RCL ST X
RCL "_DP"
X<>Y
XEQ "PDEFLT"
X<>Y
R↓
RCL "_P"
X<>Y
RCL ST Z
RTN
LBL 81
FUNC 00
MAT?
GTO 82
RTNYES
LBL 82
DIM?
×
2
X>Y?
RTNYES
RTNNO
LBL "PLGRR"
FUNC 43
L4STK
LSTO "_I"
R↓
LSTO "_T"
R↓
LSTO "_G"
R↓
LSTO "_P"
DIM?
×
LSTO "_N"
1
-
LSTO "_NM1"
R↓
LBL 94
RCL "_P"
RCL "_G"
XEQ "PEVL2"
RCL ST Z
XEQ 76
GTO 95
R↓
RCL÷ ST Y
R↓
÷
X<>Y
R↓
+/-
X<>Y
RCL× ST Y
RCL× "_N"
RCL÷ "_NM1"
1
+
SQRT
RCL× "_NM1"
RCL÷ "_N"
RCL "_N"
1/X
+
1/X
×
STO+ "_G"
DSE "_I"
GTO 94
3
RCL "_P"
RCL "_G"
XEQ "PEVAL"
RCL "_G"
RTN
LBL 95
R↓
R↓
R↓
0
X<>Y
RCL "_G"
RTN
LBL 76
FUNC 00
ABS
RCL "_T"
X>Y?
RTNYES
RTNNO
LBL "PWRP"
MVAR "X"
RCL "WRPP"
RCL "X"
XEQ "PEVAL"
RTN
LBL 98
FS? 64
RCL "WRPP"
STO "WRPP"
RTN
LBL 97
FS? 64
RCL "X"
STO "X"
RTN
LBL 80
FUNC 11
L4STK
ABS
1e-10
X>Y?
RTNYES
RTNNO
END

LBL "VEC3"
LBL 01
CLMENU
"→V"
KEY 1 XEQ 99
"V→"
KEY 2 XEQ 98
"DOT"
KEY 3 XEQ 02
"CROSS"
KEY 4 XEQ 03
"MAG"
KEY 5 XEQ 04
"VVIEW"
KEY 6 XEQ 96
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00
EXITALL
RTN
LBL 02
DOT
RTN
LBL 03
CROSS
RTN
LBL 04
FNRM
RTN
LBL 99
FUNC 31
XEQ 95
LSTO "_M"
R↓
INDEX "_M"
WRAP
J-
LBL 51
STOEL
R↓
J-
FC? 77
GTO 51
RCL "_M"
RTN
LBL 98
FUNC 13
LSTO "_M"
R↓
INDEX "_M"
WRAP
LBL 52
RCLEL
J+
FC? 77
GTO 52
RTN
LBL 96
FUNC 00
LSTO "_M"
INDEX "_M"
WRAP
1
LBL 54
CLA
AIP
├":"
RCLEL
ARCL ST X
R↓
AVIEW
STOP
1
+
J+
FC? 77
GTO 54
RTN
LBL 95
FUNC 01
1
3
NEWMAT
RTN
END

LBL "STATR"
LBL 01
CLMENU
"Σx"
KEY 1 XEQ 04
"Σx↑2"
KEY 2 XEQ 05
"Σy"
KEY 3 XEQ 06
"Σy↑2"
KEY 4 XEQ 07
"Σxy"
KEY 5 XEQ 08
"n"
KEY 6 XEQ 09
KEY 7 GTO 03
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"Σlnx"
KEY 1 XEQ 10
"Σlnx↑2"
KEY 2 XEQ 11
"Σlny"
KEY 3 XEQ 12
"Σlny↑2"
KEY 4 XEQ 13
"Σlnxlny"
KEY 5 XEQ 14
"Σxlny"
KEY 6 XEQ 15
KEY 7 GTO 01
KEY 8 GTO 03
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03
CLMENU
"Σylnx"
KEY 1 XEQ 16
KEY 7 GTO 02
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 03
LBL 00
EXITALL
RTN
LBL 04
FUNC 01
L4STK
ΣREG?
0
+
RCL IND ST X
"Σx="
ARCL ST X
AVIEW
RTN
LBL 05
FUNC 01
L4STK
ΣREG?
1
+
RCL IND ST X
"Σx^2="
ARCL ST X
AVIEW
RTN
LBL 06
FUNC 01
L4STK
ΣREG?
2
+
RCL IND ST X
"Σy="
ARCL ST X
AVIEW
RTN
LBL 07
FUNC 01
L4STK
ΣREG?
3
+
RCL IND ST X
"Σy^2="
ARCL ST X
AVIEW
RTN
LBL 08
FUNC 01
L4STK
ΣREG?
4
+
RCL IND ST X
"Σxy="
ARCL ST X
AVIEW
RTN
LBL 09
FUNC 01
L4STK
ΣREG?
5
+
RCL IND ST X
"n="
ARCL ST X
AVIEW
RTN
LBL 10
FUNC 01
L4STK
ΣREG?
6
+
RCL IND ST X
"Σlnx="
ARCL ST X
AVIEW
RTN
LBL 11
FUNC 01
L4STK
ΣREG?
7
+
RCL IND ST X
"Σ(lnx)^2="
ARCL ST X
AVIEW
RTN
LBL 12
FUNC 01
L4STK
ΣREG?
8
+
RCL IND ST X
"Σlny="
ARCL ST X
AVIEW
RTN
LBL 13
FUNC 01
L4STK
ΣREG?
9
+
RCL IND ST X
"Σ(lny)^2="
ARCL ST X
AVIEW
RTN
LBL 14
FUNC 01
L4STK
ΣREG?
10
+
RCL IND ST X
"Σlnxlny="
ARCL ST X
AVIEW
RTN
LBL 15
FUNC 01
L4STK
ΣREG?
11
+
RCL IND ST X
"Σxlny="
ARCL ST X
AVIEW
RTN
LBL 16
FUNC 01
L4STK
ΣREG?
12
+
RCL IND ST X
"Σylnx="
ARCL ST X
AVIEW
RTN
END

