LBL "ATYP"
FUNC 11
L4STK
MAT?
GTO 01
GTO 02
LBL 01
CLA
ARCL ST X
67
POSA
X>0?
GTO 07
GTO 08
LBL 07
4
RTN
LBL 08
2
RTN
LBL 02
REAL?
GTO 03
GTO 04
LBL 03
1
RTN
LBL 04
CPX?
GTO 05
GTO 06
LBL 05
3
RTN
LBL 06
0
RTN
END

LBL "RPART"
FUNC 11
L4STK
ENTER
XEQ "ATYP"
GTO IND ST X
LBL 00
RTNERR 4
LBL 01
LBL 02
R↓
RTN
LBL 03
R↓
FS? 73
→REC
COMPLEX
X<>Y
RTN
LBL 04
R↓
FS? 73
GTO 05
GTO 06
LBL 05
RECT
COMPLEX
X<>Y
POLAR
RTN
LBL 06
COMPLEX
X<>Y
RTN
END

LBL "IPART"
FUNC 11
L4STK
ENTER
XEQ "ATYP"
GTO IND ST X
LBL 00
RTNERR 4
LBL 01
0
RTN
LBL 02
R↓
DIM?
NEWMAT
RTN
LBL 03
R↓
FS? 73
→REC
COMPLEX
RTN
LBL 04
R↓
FS? 73
GTO 05
GTO 06
LBL 05
RECT
COMPLEX
POLAR
RTN
LBL 06
COMPLEX
RTN
END

LBL "CONJ"
FUNC 11
L4STK
ENTER
XEQ "ATYP"
GTO IND ST X
LBL 00
RTNERR 4
LBL 01
LBL 02
R↓
RTN
LBL 03
LBL 04
R↓
COMPLEX
+/-
COMPLEX
RTN
END

LBL "CABS"
FUNC 11
L4STK
ENTER
XEQ "ATYP"
GTO IND ST X
LBL 00
RTNERR 4
LBL 01
LBL 02
R↓
ABS
RTN
LBL 03
R↓
FC? 73
→POL
COMPLEX
X<>Y
RTN
LBL 04
R↓
FC? 73
GTO 05
GTO 06
LBL 05
POLAR
COMPLEX
X<>Y
RECT
RTN
LBL 06
COMPLEX
X<>Y
RTN
END

LBL "CARG"
FUNC 11
L4STK
ENTER
XEQ "ATYP"
GTO IND ST X
LBL 00
RTNERR 4
LBL 01
0
RTN
LBL 02
R↓
DIM?
NEWMAT
RTN
LBL 03
R↓
FC? 73
→POL
COMPLEX
RTN
LBL 04
R↓
FC? 73
GTO 05
GTO 06
LBL 05
POLAR
COMPLEX
RECT
RTN
LBL 06
COMPLEX
RTN
END

LBL "%T"
FUNC 21
L4STK
X<>Y
÷
100
×
RTN
END

LBL "NPDF"
FUNC 11
L4STK
X↑2
-2
÷
E↑X
2
PI
×
SQRT
÷
RTN
END

LBL "NCDF"
FUNC 11
L4STK
ENTER
XEQ 00
×
1
+
1/X
LSTO "T"
1.005
LSTO "I"
0
LBL 11
XEQ IND "I"
RCL× "T"
+
RCL ST Y
STO× "T"
R↓
ISG "I"
GTO 11
RCL ST Z
XEQ "NPDF"
×
1
X<>Y
-
RTN
LBL 00
0.2316419
RTN
LBL 01
0.319381530
RTN
LBL 02
-0.356563782
RTN
LBL 03
1.781477937
RTN
LBL 04
-1.821255978
 RTN
LBL 05
1.330274429
RTN
END

LBL "ERF"
FUNC 11
L4STK
ENTER
ENTER
2
SQRT
×
ABS
XEQ "NCDF"
2
×
1
-
X<>Y
X<0?
GTO 01
GTO 02
LBL 01
R↓
+/-
RTN
LBL 02
R↓
RTN
END

LBL "ERFC"
FUNC 11
L4STK
XEQ "ERF"
1
X<>Y
-
RTN
END

LBL "GCD"
FUNC 21
L4STK
ABS
IP
X<>Y
ABS
IP
X>Y?
X<>Y
LBL 01
STO ST Z
MOD
X>0?
GTO 01
R↓
RTN
END

LBL "LCM"
FUNC 21
L4STK
ABS
IP
X≠0?
GTO 01
0
RTN
LBL 01
LSTO "TMPX"
X<>Y
ABS
IP
X≠0?
GTO 02
0
RTN
LBL 02
LSTO "TMPY"
XEQ "GCD"
RCL "TMPX"
X<>Y
÷
RCL× "TMPY"
RTN
END

LBL "BETA"
FUNC 21
L4STK
RCL ST Y
GAMMA
RCL ST Y
GAMMA
×
RCL ST Z
RCL ST Z
+
GAMMA
÷
RTN
END

LBL "TRIG"
LBL 01
CLMENU
"SIN"
KEY 1 XEQ 04
"COS"
KEY 2 XEQ 05
"TAN"
KEY 3 XEQ 06
"ASIN"
KEY 4 XEQ 07
"ACOS"
KEY 5 XEQ 08
"ATAN"
KEY 6 XEQ 09
KEY 7 GTO 03
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"CSC"
KEY 1 XEQ 91
"SEC"
KEY 2 XEQ 92
"COT"
KEY 3 XEQ 93
"ACSC"
KEY 4 XEQ 94
"ASEC"
KEY 5 XEQ 95
"ACOT"
KEY 6 XEQ 96
KEY 7 GTO 01
KEY 8 GTO 03
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03
CLMENU
"TAN2"
KEY 1 XEQ 97
"HYPOT"
KEY 2 XEQ 98
"HAV"
KEY 3 XEQ 90
"AHAV"
KEY 4 XEQ 89
KEY 7 GTO 02
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 03
LBL 00
EXITALL
RTN
LBL 04
SIN
RTN
LBL 05
COS
RTN
LBL 06
TAN
RTN
LBL 07
ASIN
RTN
LBL 08
ACOS
RTN
LBL 09
ATAN
RTN
LBL 89
FUNC 11
L4STK
SQRT
ASIN
2
×
RTN
LBL 90
FUNC 11
L4STK
COS
-2
÷
0.5
+
RTN
LBL 98
FUNC 21
L4STK
ABS
X<>Y
ABS
COMPLEX
ABS
RTN
LBL 91
FUNC 11
L4STK
SIN
1/X
RTN
LBL 92
FUNC 11
L4STK
COS
1/X
RTN
LBL 93
FUNC 11
L4STK
TAN
1/X
RTN
LBL 94
FUNC 11
L4STK
1/X
ASIN
RTN
LBL 95
FUNC 11
L4STK
1/X
ACOS
RTN
LBL 96
FUNC 11
L4STK
1/X
ATAN
RTN
LBL 97
FUNC 21
L4STK
X>0?
GTO 81
X=0?
GTO 82
X<>Y
X<0?
GTO 83
X<>Y
÷
ATAN
PI
+
RTN
LBL 83
X<>Y
÷
ATAN
PI
-
RTN
LBL 82
X<>Y
X=0?
GTO 84
X>0?
GTO 85
PI
-2
÷
RTN
LBL 85
PI
2
÷
RTN
LBL 84
÷
RTN
LBL 81
÷
ATAN
RTN
END

LBL "LPRT"
LBL 01
CLMENU
"lnYX"
KEY 1 XEQ 99
"ln2"
KEY 3 XEQ 98
"LN"
KEY 4 XEQ 04
"LOG"
KEY 5 XEQ 05
KEY 7 GTO 03
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"Y↑X"
KEY 1 XEQ 06
"2↑X"
KEY 3 XEQ 97
"EXP"
KEY 4 XEQ 07
"10↑X"
KEY 5 XEQ 08
KEY 7 GTO 01
KEY 8 GTO 03
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03
CLMENU
"Y√X"
KEY 1 XEQ 96
"2√X"
KEY 3 XEQ 95
"3√X"
KEY 4 XEQ 94
"5√X"
KEY 5 XEQ 93
"7√X"
KEY 6 XEQ 92
KEY 7 GTO 02
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 03
LBL 00
EXITALL
RTN
LBL 04
LN
RTN
LBL 05
LOG
RTN
LBL 06
Y↑X
RTN
LBL 07
XEQ "EXP"
RTN
LBL 08
10↑X
RTN
LBL 99
FUNC 21
L4STK
LN
X<>Y
LN
÷
RTN
LBL 98
FUNC 11
L4STK
LN
2
LN
÷
RTN
LBL 97
FUNC 11
L4STK
2
X<>Y
Y^X
RTN
LBL 95
FUNC 11
2
X<>Y
XEQ 96
RTN
LBL 94
FUNC 11
3
X<>Y
XEQ 96
RTN
LBL 93
FUNC 11
5
X<>Y
XEQ 96
RTN
LBL 92
FUNC 11
7
X<>Y
XEQ 96
RTN
LBL 96
FUNC 21
L4STK
REAL?
GTO 81
GTO 84
LBL 81
X≥0?
GTO 84
RCL ST Y
REAL?
GTO 82
GTO 83
LBL 82
FP
X≠0?
GTO 83
R↓
ABS
RCL ST Y
X<>Y
XEQ 84
+/-
X<>Y
2
÷
FP
X=0?
GTO 86
GTO 85
LBL 86
R↓
-1
SQRT
×
RTN
LBL 85
R↓
RTN
LBL 83
R↓
LBL 84
X<>Y
1/X
Y↑X
RTN
END

LBL "NLA"
LBL 01
CLMENU
"IDEN"
KEY 1 XEQ 99
"DIAG"
KEY 2 XEQ 98
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00
EXITALL
RTN
LBL 99
FUNC 11
L4STK
ENTER
NEWMAT
LSTO "TMPM"
INDEX "TMPM"
1
LBL 01
STOEL
J+
FS? 77
GTO 02
I+
GTO 01
LBL 02
RCL "TMPM"
RTN
END

LBL "DIAG"
FUNC 11
L4STK
LSTO "TMPM"
INDEX "TMPM"
DIM?
×
ENTER
DIM "TMPM"
1000
÷
1
+
LSTO "I"
LBL 01
1
RCL "I"
STOIJ
R↓
R↓
RCLEL
0
STOEL
R↓
RCL "I"
RCL "I"
STOIJ
R↓
R↓
STOEL
ISG "I"
GTO 01
RCL "TMPM"
RTN
END

LBL "POLY"
LBL 01
CLMENU
"QUADE"
KEY 1 XEQ 99
"EVAL"
KEY 2 XEQ 98
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00
EXITALL
RTN
LBL 99
FUNC 32
L4STK
-4
RCL× ST T
×
RCL ST Y
X↑2
+
SQRT
2
RCLx ST T
÷
LASTX
RCL ST Z
+/-
X<>Y
÷
RCL ST X
RCL ST Z
-
RCL ST Z
RCL ST Z
+
RTN
LBL 98
FUNC 21
L4STK
X<>Y
LSTO "TMPM"
INDEX "TMPM"
WRAP
0
LBL 97
RCL× ST Z
RCLEL
+
J+
FC? 77
GTO 97
RTN
END

LBL "PWRP"
MVAR "X"
RCL "_PLY"
RCL "X"
X<>Y
LSTO "TMPM"
INDEX "TMPM"
WRAP
0
LBL 97
RCL× ST Z
RCLEL
+
J+
FC? 77
GTO 97
RTN
END

LBL "VEC3"
LBL 01
CLMENU
"→V"
KEY 1 XEQ 99
"V→"
KEY 2 XEQ 98
"DOT"
KEY 3 XEQ 02
"CROSS"
KEY 4 XEQ 03
"MAG"
KEY 5 XEQ 04
"VVIEW"
KEY 6 XEQ 96
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00
EXITALL
RTN
LBL 02
DOT
RTN
LBL 03
CROSS
RTN
LBL 04
FNRM
RTN
LBL 99
FUNC 31
XEQ 95
LSTO "TMPM"
R↓
INDEX "TMPM"
WRAP
J-
LBL 51
STOEL
R↓
J-
FC? 77
GTO 51
RCL "TMPM"
RTN
LBL 98
FUNC 13
LSTO "TMPM"
R↓
INDEX "TMPM"
WRAP
LBL 52
RCLEL
J+
FC? 77
GTO 52
RTN
LBL 96
FUNC 00
LSTO "TMPM"
INDEX "TMPM"
WRAP
1
LBL 54
CLA
AIP
├":"
RCLEL
ARCL ST X
R↓
AVIEW
STOP
1
+
J+
FC? 77
GTO 54
RTN
LBL 95
FUNC 01
1
3
NEWMAT
RTN
END

