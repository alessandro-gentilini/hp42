LBL "DMENU"
LBL 01
CLMENU
"DATE"
KEY 1 XEQ 95
"TIME"
KEY 2 XEQ 88
"UTIME"
KEY 3 XEQ "UTIME"
"DT→U"
KEY 5 XEQ "DT→U"
"U→DT"
KEY 6 XEQ "U→DT"
KEY 7 GTO 03
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"DATE"
KEY 1 XEQ 95
"JDATE"
KEY 3 XEQ "JDATE"
"D→J"
KEY 5 XEQ "D→J"
"J→D"
KEY 6 XEQ "J→D"
KEY 7 GTO 01
KEY 8 GTO 03
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03
CLMENU
"DATE+"
KEY 1 XEQ 92
"DDAYS"
KEY 2 XEQ 04
"DOW"
KEY 3 XEQ 91
KEY 7 GTO 02
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 03
LBL 00
EXITALL
RTN
LBL 04
DDAYS
RTN
LBL 95
FUNC 01
L4STK
DATE
"Date: "
ADATE
AVIEW
RTN
LBL 88
FUNC 01
L4STK
TIME
"Time: "
ATIME
AVIEW
RTN
LBL "UTIME"
FUNC 02
L4STK
DATE
TIME
XEQ "DT→U"
RTN
LBL "DT→U"
FUNC 21
L4STK
FS? 67
1970.0101
FC? 67
1.011970
RCL ST Z
DDAYS
24
×
X<>Y
→HR
+
3600
×
RTN
LBL "U→DT"
FUNC 12
L4STK
3600
÷
RCL ST X
24
÷
IP
FS? 67
1970.0101
FC? 67
1.011970
X<>Y
DATE+
X<>Y
24
MOD
→HMS
X<>Y
"Date:  "
ADATE
X<>Y
├"[LF]Time: "
ATIME
AVIEW
RTN
LBL 92
FUNC 21
L4STK
DATE+
"Date: "
ADATE
AVIEW
RTN
LBL 91
FUNC 11
L4STK
DOW
"Day of Week: "
70
+
XEQ IND ST X
AVIEW
RTN
LBL 70
├"SUN"
RTN
LBL 71
├"MON"
RTN
LBL 72
├"TUE"
RTN
LBL 73
├"WED"
RTN
LBL 74
├"THR"
RTN
LBL 76
├"FRI"
RTN
LBL 77
├"SAT"
RTN
LBL "JDATE"
FUNC 01
L4STK
DATE
XEQ "D→J"
RTN
LBL "J→D"
FUNC 11
L4STK
2440587.5
-
FS? 67
1970.0101
FC? 67
1.011970
X<>Y
DATE+
"Date: "
ADATE
AVIEW
RTN
LBL "D→J"
FUNC 11
L4STK
FS? 67
1970.0101
FC? 67
1.011970
X<>Y
DDAYS
2440587.5
+
RTN
END

