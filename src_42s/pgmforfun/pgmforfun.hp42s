LBL "WRPF"
MVAR "X"
RCL "X"
XEQ IND "IEqV"
RTN
END

LBL "PICF"
FUNC 10
L4STK
CLV "EqP"
CLV "EqV"
CLV "EqD"
X≠0?
GTO 08
1
STO "EqD"
LBL 08
EXITALL
PGMMENU
"Pick Function"
AVIEW
STOP
ASTO "EqP"
EXITALL
VARMENU IND "EqP"
"Set Var; "
├"Pick Free Var"
AVIEW
STOP
ASTO "EqV"
EXITALL
SF 25
RCL "EqD"
FS?C 25
GTO 07
"Select Return"
├" Count"
AVIEW
CLMENU
"1"
KEY 1 XEQ 02
"2"
KEY 2 XEQ 03
"3"
KEY 3 XEQ 04
"4"
KEY 4 XEQ 05
MENU
STOP
EXITALL
STO "EqD"
LBL 07
"F: "
ARCL "EqP"
├"("
ARCL "EqV"
├"):→"
ARCL "EqD"
AVIEW
RTN
LBL 02
1
RTN
LBL 03
2
RTN
LBL 04
3
RTN
LBL 05
4
RTN
END

LBL "FSPT"
CF 82
SF 83
1
XEQ "PICF"
R↓
LBL 01
CLMENU
"LLIM"
KEY 1 XEQ 03
"ULIM"
KEY 2 XEQ 04
"STEP"
KEY 3 XEQ 05
"Σ"
KEY 4 XEQ 06
"π"
KEY 5 XEQ 07
"▒"
KEY 6 XEQ 08
KEY 7 GTO 02
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"EQ"
KEY 1 XEQ 09
FS? 82
"LN•"
FC? 82
"LN"
KEY 2 XEQ 10
FS? 83
"IND•"
FC? 83
"IND"
KEY 3 XEQ 11
KEY 7 GTO 01
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 00
EXITALL
RTN
LBL 03
FS? 64
RCL "LLIM"
STO "LLIM"
"LLIM: "
ARCL ST X
AVIEW
RTN
LBL 04
FS? 64
RCL "ULIM"
STO "ULIM"
"ULIM: "
ARCL ST X
AVIEW
RTN
LBL 05
FS? 64
RCL "STEP"
STO "STEP"
"STEP: "
ARCL ST X
AVIEW
RTN
LBL 06
CF 84
SF 85
GTO 20
RTN
LBL 07
CF 84
CF 85
GTO 20
RTN
LBL 08
SF 84
GTO 20
RTN
LBL 09
1
XEQ "PICF"
R↓
RTN
LBL 10
FS?C 82
RTN
SF 82
RTN
LBL 11
FS?C 83
RTN
SF 83
RTN
LBL 20
1
SF 25
RCL "LLIM"
FC?C 25
STO "LLIM"
SF 25
RCL "STEP"
FC?C 25
STO "STEP"
RCL "LLIM"
RCL "ULIM"
X>Y?
GTO 12
"ERR: LLIM ≥"
├" ULIM"
AVIEW
RTN
LBL 12
RCL "STEP"
X>0?
GTO 14
"ERR: STEP ≤ 0"
AVIEW
RTN
LBL 14
RCL "EqD"
X>0?
GTO 15
"ERR: EqD ≤ 0"
AVIEW
RTN
LBL 15
4
X≥Y?
GTO 16
"ERR: EqD > 4"
AVIEW
RTN
LBL 16
FS? 84
GTO 21
GTO 22
LBL 22
FS? 85
0
FC? 85
1
LSTO "SoP"
GTO 23
LBL 21
1
ENTER
ENTER
RCL "EqD"
FS? 83
+
NEWMAT
LSTO "TMAT"
R↓
INDEX "TMAT"
GROW
LBL 23
RCL "LLIM"
STO IND "EqV"
LBL 24
CLA
ARCL "EqV"
├"="
ARCL IND "EqV"
AVIEW
FC? 84
GTO 32
RCL "EqD"
LSTO "CTR"
FC? 83
GTO 32
RCL IND "EqV"
STOEL
J+
LBL 32
SF 25
XEQ IND "EqP"
FS?C 25
GTO 17
"ERR: Func Eval"
AVIEW
RTN
LBL 17
FS? 84
GTO 26
GTO 27
LBL 27
FC? 82
GTO 29
SF 25
LN
FS?C 25
GTO 29
"ERR: Bad Log"
AVIEW
LBL 29
FS? 85
STO+ "SoP"
FC? 85
STO× "SoP"
GTO 28
LBL 26
LBL 30
FC? 82
GTO 31
SF 25
LN
FS?C 25
GTO 31
"ERR: Bad Log"
AVIEW
LBL 31
STOEL
J+
R↓
DSE "CTR"
GTO 30
LBL 28
RCL "STEP"
RCL IND "EqV"
+
STO IND "EqV"
RCL "ULIM"
X≥Y?
GTO 24
FS? 84
GTO 33
GTO 19
LBL 19
FS? 85
"SUM: "
FC? 85
"PROD: "
RCL "SoP"
GTO 18
LBL 33
CLA
RCL "TMAT"
LBL 18
ARCL ST X
AVIEW
RTN
END

LBL "FSUM"
FUNC 01
0
LSTO "Sum"
RCL "LLIM"
STO IND "EqV"
LBL 24
SF 25
XEQ IND "EqP"
FC?C 25
RTNERR 2
STO+ "Sum"
RCL "STEP"
RCL+ IND "EqV"
STO IND "EqV"
RCL "ULIM"
X≥Y?
GTO 24
RCL "Sum"
RTN
END

LBL "FPRD"
FUNC 01
1
LSTO "Prd"
RCL "LLIM"
STO IND "EqV"
LBL 24
SF 25
XEQ IND "EqP"
FC?C 25
RTNERR 2
STO× "Prd"
RCL "STEP"
RCL+ IND "EqV"
STO IND "EqV"
RCL "ULIM"
X≥Y?
GTO 24
RCL "Prd"
RTN
END

LBL "FSLV"
FUNC 04
PGMSLV IND "EqP"
RCL "LLIM"
STO IND "EqV"
RCL "ULIM"
SOLVE IND "EqV"
RTN
END

LBL "FINT"
FUNC 03
PGMINT IND "EqP"
INTEG IND "EqV"
RTN
END

LBL "FEVL"
STO IND "EqV"
XEQ IND "EqP"
RTN
END

LBL "FPLT"
FC? 04
3
FS? 04
2
STO "GrMod"
R↓
131
STO "ResX"
R↓
16
STO "ResY"
R↓
XEQ 74
LBL 01
CF 83
CLMENU
"EQ"
KEY 1 XEQ 02
"XRNG"
KEY 2 XEQ 03
"YRNG"
KEY 3 XEQ 04
"GRID"
KEY 4 XEQ 05
"COMP"
KEY 5 XEQ 06
"PLOT"
KEY 6 XEQ 07
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00
EXITALL
RTN
LBL 02
0
XEQ "PICF"
RTN
LBL 03
FUNC 00
FS? 64
GTO 10
X≠Y?
GTO 24
X=0?
GTO 26
RCL "ULIM"
RCL- "LLIM"
2
÷
×
RCL "ULIM"
RCL+ "LLIM"
2
÷
RCL ST X
RCL ST Z
-
STO "LLIM"
R↓
+
STO "ULIM"
GTO 10
LBL 26
CLV "LLIM"
CLV "ULIM"
XEQ 74
GTO 10
LBL 24
STO "ULIM"
X<>Y
STO "LLIM"
X<>Y
LBL 10
"XMIN: "
RCL "LLIM"
ARCL ST X
├"[LF]XMAX: "
RCL "ULIM"
ARCL ST X
AVIEW
RTN
LBL 04
FUNC 00
FS? 64
GTO 50
X≠Y?
GTO 22
X=0?
GTO 23
RCL "YMAX"
RCL- "YMIN"
2
÷
×
RCL "YMAX"
RCL+ "YMIN"
2
÷
RCL ST X
RCL ST Z
-
STO "YMIN"
R↓
+
STO "YMAX"
GTO 50
LBL 23
CLV "YMIN"
CLV "YMAX"
XEQ 74
GTO 50
LBL 22
STO "YMAX"
X<>Y
STO "YMIN"
X<>Y
LBL 50
"YMIN: "
RCL "YMIN"
ARCL ST X
├"[LF]YMAX: "
RCL "YMAX"
ARCL ST X
AVIEW
RTN
LBL 05
FUNC 00
FS? 64
GTO 49
STO "YSTP"
X<>Y
STO "STEP"
LBL 49
"XSTP: "
RCL "STEP"
ARCL ST X
├"[LF]YSTP: "
RCL "YSTP"
ARCL ST X
AVIEW
RTN
LBL 06
LBL 30
CLMENU
"ROOT"
KEY 1 XEQ 32
"INTG"
KEY 2 XEQ 33
"EVAL"
KEY 3 XEQ 34
"R<>B"
KEY 6 XEQ 37
KEY 9 GTO 31
MENU
STOP
GTO 30
LBL 31
RTN
LBL 32
FUNC 04
XEQ "FSLV"
RTN
LBL 33
FUNC 01
RCL "ULIM"
RCL- "LLIM"
RCL÷ "ResX"
RCL "YMAX"
RCL- "YMIN"
RCL÷ "ResY"
×
XEQ "FINT"
RTN
LBL 34
XEQ "FEVL"
RTN
LBL 35
RTN
LBL 36
RTN
LBL 37
XEQ 39
RTN
RCL "GrMod"
LBL 07
CF 34
CF 35
XEQ 74
SF 25
RCL "EqV"
FC?C 25
GTO 76
R↓
SF 25
RCL "EqP"
FC?C 25
GTO 76
R↓
GTO 77  @@ All good
LBL 76
"ERR: Pick An"
├" EQ First!"
AVIEW
RTN
LBL 77
RCL "YMAX"
RCL- "YMIN"
1
RCL "ResY"
-
÷
STO "YWID"
RCL "ULIM"
RCL- "LLIM"
RCL "ResX"
1
-
÷
STO "XWID"
CLLCD
CLMENU
EXITALL
FC? 83
GTO 17
RCL "ResX"
14
÷
IP
1
-
LSTO "I"
LBL 15
RCL "ResY"
2
÷
9
-
RCL "I"
14
×
"@`px|~├~|xp`@"
AGRAPH
"×∫π→•?├?•→π∫×"
X<>Y
12
+
X<>Y
AGRAPH
DSE "I"
GTO 15
GTO 20
LBL 17
RCL "STEP"
X<0?
GTO 11
0
XEQ 56
XEQ 58
1
-
XEQ 58
2
+
XEQ 58
LBL 11
RCL "STEP"
X≤0?
GTO 08
RCL "XWID"
10
×
X>Y?
GTO 08
R↓
RCL "ULIM"
RCL÷ "STEP"
IP
RCL× "STEP"
RCL+ "STEP"
RCL "LLIM"
RCL÷ "STEP"
IP
RCL× "STEP"
RCL- "STEP"
LBL 09
ENTER
XEQ 46
XEQ 48
R↓
RCL+ "STEP"
X<Y?
GTO 09
LBL 08
RCL "YSTP"
X<0?
GTO 12
0
XEQ 46
XEQ 48
1
-
XEQ 48
2
+
XEQ 48
LBL 12
RCL "YSTP"
X≤0?
GTO 20
RCL "YWID"
-10
×
X>Y?
GTO 20
R↓
RCL "YMAX"
RCL÷ "YSTP"
IP
RCL× "YSTP"
RCL+ "YSTP"
RCL "YMIN"
RCL÷ "YSTP"
IP
RCL× "YSTP"
RCL- "YSTP"
LBL 21
ENTER
XEQ 56
XEQ 58
R↓
RCL+ "YSTP"
X<Y?
GTO 21
LBL 20
"πππ"
SF 82
0
LSTO "LASTY"
R↓
RCL "ResX"
1000
÷
1
+
LSTO "XICUR"
LBL 41
RCL "XICUR"
IP
XEQ 47
STO IND "EqV"
SF 25
XEQ IND "EqP"
FC?C 25
GTO 29
FC? 82
GTO 43
LSTO "YPMAX"
LSTO "YPMIN"
CF 82
LBL 43
RCL "YPMAX"
X<>Y
X>Y?
LSTO "YPMAX"
RCL "YPMIN"
X<>Y
X<Y?
LSTO "YPMIN"
FC? 83
GTO 78
RCL "ResY"
2
÷
1
-
RCL "XICUR"
AGRAPH
R↓
R↓
GTO 19
LBL 78
XEQ 56
XEQ 55
GTO 29
RCL "XICUR"
XEQ 51              @@@ XICUR YICUR
R↓
FS? 03
GTO 19
FS? 83
GTO 19
RCL "LASTY"
X=0?
GTO 19
X<>Y
RCL "XICUR"
XEQ 40
LBL 19
R↓
LSTO "LASTY"
GTO 42
LBL 29
0
LSTO "LASTY"
LBL 42
ISG "XICUR"
GTO 41
10
LSTO "FPZSPD"
1
LSTO "ZBTOP"
LSTO "ZBLFT"
RCL "ResY"
LSTO "ZBBOT"
RCL "ResX"
LSTO "ZBRGT"
FS?C 83
GTO 68
LBL 16
RCL "FPZSPD"
LSTO "CTR"
LBL 52
RCL "ZBRGT"
RCL "CTR"
+
XEQ 48
DSE "CTR"
GTO 52
RCL "FPZSPD"
LSTO "CTR"
LBL 53
RCL "ZBBOT"
RCL "CTR"
+
XEQ 58
DSE "CTR"
GTO 53
RCL "FPZSPD"
LSTO "CTR"
LBL 54
RCL "ZBTOP"
RCL "CTR"
-
XEQ 58
DSE "CTR"
GTO 54
RCL "FPZSPD"
LSTO "CTR"
LBL 59
RCL "ZBLFT"
RCL "CTR"
-
XEQ 48
DSE "CTR"
GTO 59
LBL 44
GETKEY1
13
X=Y?
GTO 73
R↓
17
X=Y?
GTO 60
R↓
32
X=Y?
GTO 66
R↓
37
X=Y?
GTO 65
R↓
24
X=Y?
GTO 61
R↓
26
X=Y?
GTO 62
R↓
20
X=Y?
GTO 63
R↓
30
X=Y?
GTO 64
R↓
14
X=Y?
GTO 68
R↓
22
X=Y?
GTO 67
R↓
35
X=Y?
GTO 75
R↓
27
X=Y?
GTO 69
GTO 44
LBL 67
0.5
ENTER
ENTER
XEQ 03
XEQ 04
GTO 07
LBL 69
2.0
ENTER
ENTER
XEQ 03
XEQ 04
GTO 07
LBL 68
1
RCL "ZBLFT"
-
ABS
RCL "ResX"
RCL "ZBRGT"
-
ABS
+
X=0?
GTO 14
RCL "ZBLFT"
XEQ 47
RCL "ZBRGT"
XEQ 47
STO "ULIM"
X<>Y
STO "LLIM"
SF 83
GTO 07
LBL 14
RCL "YPMAX"
-5
RCL× "YWID"
+
RCL "YPMIN"
5
RCL× "YWID"
+
STO "YMIN"
X<>Y
STO "YMAX"
GTO 07
LBL 75
FS?C 03
GTO 07
SF 03
GTO 07
LBL 65
10
LSTO "FPZSPD"
GTO 16
LBL 66
1
LSTO "FPZSPD"
GTO 16
LBL 61
RCL "ZBRGT"
-2
RCL× "FPZSPD"
+
RCL "FPZSPD"
RCL+ "ZBLFT"
X<Y?
LSTO "ZBLFT"
XEQ 47
STO "ZLLIM"
GTO 16
LBL 62
RCL "ZBLFT"
2
RCL× "FPZSPD"
+
RCL "FPZSPD"
+/-
RCL+ "ZBRGT"
X>Y?
LSTO "ZBRGT"
XEQ 47
STO "ZULIM"
GTO 16
LBL 63
RCL "ZBBOT"
-2
RCL× "FPZSPD"
+
RCL "FPZSPD"
RCL+ "ZBTOP"         @@@ New_ZBTOP Lower_limit
X<Y?
LSTO "ZBTOP"
GTO 16
LBL 64
RCL "ZBTOP"
2
RCL× "FPZSPD"
+
RCL "FPZSPD"
+/-
RCL+ "ZBBOT"         @@@ New_ZBBOT Lower_limit
X>Y?
LSTO "ZBBOT"
GTO 16
LBL 73
RCL "ZBTOP"
XEQ 57
RCL "ZBBOT"
XEQ 57
STO "YMIN"
R↓
STO "YMAX"
RCL "ZBRGT"
XEQ 47
RCL "ZBLFT"
XEQ 47
STO "LLIM"
X<>Y
STO "ULIM"
GTO 07
LBL 60
RTN
LBL 40
FUNC 00
LSTO "XNUE"
R↓
RCL ST Y
RCL- ST Y
ABS
3
X>Y?
RTN
R↓
R↓
LSTO "YNUE"
X<>Y
LSTO "YOLD"
+
2
÷
IP
LSTO "YCTR"
RCL "XNUE"
1
-
RCL "YOLD"
RCL "YCTR"
XEQ 70
R↓
R↓
R↓
RCL "XNUE"
RCL "YNUE"
RCL "YCTR"
XEQ 70
R↓
R↓
R↓
RTN
LBL 70
FUNC 00
X<Y?
GTO 71
1
STO- ST T
R↓
1
STO- ST Z
R↓
3
-
LBL 72
RCL ST Z
AGRAPH
R↓
3
-
X>Y?
GTO 72
RTN
LBL 71
1
STO- ST T
R↓
1
STO- ST Z
R↓
LBL 18
RCL ST Z
AGRAPH
R↓
3
+
X<Y?
GTO 18
RTN
LBL 51
FUNC 00
1
-
X<>Y
1
-
X<>Y
AGRAPH
RTN
LBL 58
FUNC 00
XEQ 55
GTO 27
+/-
1
PIXEL
LBL 27
RTN
LBL 48
FUNC 00
XEQ 45
GTO 28
+/-
1
X<>Y
PIXEL
LBL 28
RTN
LBL 45
FUNC 00
X≤0?
RTNYES
RCL "ResX"
X<Y?
RTNYES
RTNNO
LBL 55
FUNC 00
X≤0?
RTNYES
RCL "ResY"
X<Y?
RTNYES
RTNNO
LBL 46
FUNC 11
RCL- "LLIM"
RCL÷ "XWID"
1
+
RTN
LBL 47
FUNC 11
1
-
RCL× "XWID"
RCL+ "LLIM"
RTN
LBL 56
FUNC 11
RCL- "YMAX"
RCL÷ "YWID"
1
+
RTN
LBL 57
FUNC 11
1
-
RCL× "YWID"
RCL+ "YMAX"
RTN
LBL 39
FUNC 00
SF 25
RCL "ZLLIM"
FC?C 25
GTO 38
SF 25
RCL "ZULIM"
FC?C 25
GTO 38
SF 25
RCL "LLIM"
FC?C 25
GTO 38
SF 25
RCL "ULIM"
FC?C 25
GTO 38
STO "ZULIM"
R↓
STO "ZLLIM"
R↓
STO "ULIM"
X<>Y
STO "LLIM"
"XMIN: "
ARCL ST X
R↓
├"[LF]XMAX: "
ARCL ST X
R↓
AVIEW
RTN
LBL 38
"ERR: Range or "
"├ unset"
AVIEW
RTN
LBL 74
10
SF 25
RCL "ULIM"
FC?C 25
STO "ULIM"
-10
SF 25
RCL "LLIM"
FC?C 25
STO "LLIM"
6
SF 25
RCL "YMAX"
FC?C 25
STO "YMAX"
-6
SF 25
RCL "YMIN"
FC?C 25
STO "YMIN"
0
SF 25
RCL "YSTP"
FC?C 25
STO "YSTP"
0
SF 25
RCL "STEP"
FC?C 25
STO "STEP"
RTN
END

LBL "FVARM"
MVAR "LLIM"
MVAR "ULIM"
MVAR "STEP"
MVAR "YMIN"
MVAR "YMAX"
MVAR "YSTP"
MVAR "EqP"
MVAR "EqV"
MVAR "EqD"
MVAR "ACC"
MVAR "IEqV"
RTN
END

LBL "FMENU"
LBL 01
CLMENU
"FSPT"
KEY 1 XEQ 03
"FPLT"
KEY 2 XEQ 04
"EVAL"
KEY 3 XEQ 05
"PICF"
KEY 5 XEQ 07
"VARS"
KEY 6 XEQ 08
KEY 7 GTO 02
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
"FSLV"
KEY 1 XEQ 09
"FINT"
KEY 2 XEQ 10
"FSUM"
KEY 3 XEQ 11
"FPRD"
KEY 4 XEQ 12
"PICF"
KEY 5 XEQ 13
"VARS"
KEY 6 XEQ 14
KEY 7 GTO 01
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 00
EXITALL
RTN
LBL 03
XEQ "FSPT"
RTN
LBL 04
XEQ "FPLT"
RTN
LBL 05
XEQ "FEVL"
RTN
LBL 06
XEQ "RSV1"
RTN
LBL 07
1
XEQ "PICF"
RTN
LBL 08
VARMENU "FVARM"
STOP
RTN
LBL 09
XEQ "FSLV"
RTN
LBL 10
XEQ "FINT"
RTN
LBL 11
XEQ "FSUM"
RTN
LBL 12
XEQ "FPRD"
RTN
LBL 13
1
XEQ "PICF"
RTN
LBL 14
VARMENU "FVARM"
STOP
RTN
END

