# -*- Mode:Org; Coding:utf-8; fill-column:158 -*-
#+TITLE:       free42 Random Math Stuff
#+AUTHOR:      Mitch Richling
#+EMAIL:       http://www.mitchr.me/
#+DATE:        2021-03-19
#+DESCRIPTION: Some simple math stuff for free42
#+LANGUAGE:    en
#+OPTIONS:     num:t toc:nil \n:nil @:t ::t |:t ^:nil -:t f:t *:t <:t skip:nil d:nil todo:t pri:nil H:5 p:t author:t html-scripts:nil
#+HTML_HEAD: <style>body { width: 95%; margin: 2% auto; font-size: 18px; line-height: 1.4em; font-family: Georgia, serif; color: black; background-color: white; }</style>
#+HTML_HEAD: <style>body { min-width: 500px; max-width: 1024px; }</style>
#+HTML_HEAD: <style>h1,h2,h3,h4,h5,h6 { color: #A5573E; line-height: 1em; font-family: Helvetica, sans-serif; }</style>
#+HTML_HEAD: <style>h1,h2,h3 { line-height: 1.4em; }</style>
#+HTML_HEAD: <style>h1.title { font-size: 3em; }</style>
#+HTML_HEAD: <style>h4,h5,h6 { font-size: 1em; }</style>
#+HTML_HEAD: <style>.org-src-container { border: 1px solid #ccc; box-shadow: 3px 3px 3px #eee; font-family: Lucida Console, monospace; font-size: 80%; margin: 0px; padding: 0px 0px; position: relative; }</style>
#+HTML_HEAD: <style>.org-src-container>pre { line-height: 1.2em; padding-top: 1.5em; margin: 0.5em; background-color: #404040; color: white; overflow: auto; }</style>
#+HTML_HEAD: <style>.org-src-container>pre:before { display: block; position: absolute; background-color: #b3b3b3; top: 0; right: 0; padding: 0 0.2em 0 0.4em; border-bottom-left-radius: 8px; border: 0; color: white; font-size: 100%; font-family: Helvetica, sans-serif;}</style>
#+HTML_HEAD: <style>pre.example { white-space: pre-wrap; white-space: -moz-pre-wrap; white-space: -o-pre-wrap; font-family: Lucida Console, monospace; font-size: 80%; background: #404040; color: white; display: block; padding: 0em; border: 2px solid black; }</style>
#+HTML_LINK_HOME: https://www.mitchr.me/
#+HTML_LINK_UP: https://richmit.github.io/hp42/
#+EXPORT_FILE_NAME: ../docs/math

#+ATTR_HTML: :border 2 solid #ccc :frame hsides :align center
|        <r> | <l>              |
|  *Author:* | /{{{author}}}/ |
| *Updated:* | /{{{time(%Y-%m-%d %H:%M:%S)}}}/ |
#+ATTR_HTML: :align center
Copyright {{{time(%Y)}}} Mitch Richling. All rights reserved.

#+TOC: headlines 5

#        #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #
#   00   #    10   #    20   #    30   #    40   #    50   #    60   #    70   #    80   #    90   #   100   #   110   #   120   #   130   #   140   #   150   #   160   #
# 234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789
#        #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #
#        #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #

# Provde links to programs like this: https://richmit.github.io/hp42/math.html#coderef-lnXY

* Metadata

This home for this HTML file is: https://richmit.github.io/hp42/math.html

Files related to this document may be found on github: https://github.com/richmit/hp42

Directory contents:
#+ATTR_HTML: :border 0 :frame none :rules none :align center
   | =src=     | - | The org-mode file that generated this HTML document            |
   | =src_42s= | - | Ready to convert source listings for 42s code in this document |
   | =docs=    | - | This html document                                             |
   | =bin=     | - | Importable RAW program files                                   |

* Introduction

This org-mode file collects together a handful of mathematical stuff I find useful.  I keep this stuff in my [[https://richmit.github.io/hp42/math.html][custom menu]].

* Arithmetic Types

Many of the simple math functions here work on what I call an "arithmetic type" -- that is a thing the 42s can do arithmetic with: real numbers, complex
numbers, real matrices, & complex matrices.  In order to simplify the code the =ATYP= function spits out a small integer that can be used in a computed =GOTO=
to handle these four special cases.

** =ATYP=: Return type of object

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:ATYP)
@@@@ DSC: Arithmetic Type
@@@@ IN:  X: an object
@@@@ OUT: X: 1 if input X was a real number
@@@@         2 if input X was a real matrix
@@@@         3 if input X was a complex number
@@@@         4 if input X was a complex matrix
@@@@         0 if none of the above are true
@@@@ TST: free42_3.0
@@@@ FAQ: Even on free42<2.5.24 or a real 42s, X, Y, & Z are preserved. T & Last X not so much.
@@@@ BUG: A 2 will be returned for a real matrix containing strings
@@@@ BUG: In infinite stack mode with an empty stack 2 will be returned as X=0 when we do L4STK
@@@@ UPD: 2021-02-23
LBL "ATYP"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
MAT?
GTO 01
GTO 02
LBL 01               @@@@ Matrix
CLA
ARCL ST X
67
POSA
X>0?
GTO 07
GTO 08
LBL 07               @@@@ Complex Matrix
4
RTN
LBL 08               @@@@ Real/String Matrix
2
RTN
LBL 02               @@@@ Not matrix
REAL?
GTO 03
GTO 04
LBL 03               @@@@ Real number
1
RTN
LBL 04               @@@@ Not matrix or real
CPX?
GTO 05
GTO 06
LBL 05               @@@@ Complex number
3
RTN
LBL 06               @@@@ Not matrix, real, or complex
0
RTN
END
#+END_SRC

* Complex Number Functions

** =RPART=: Real Part

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:RPART)
@@@@ DSC: Real part
@@@@ IN:  X: Number or numeric matrix (element-wise)
@@@@ OUT: X: rpart(x)
@@@@ TST: free42_3.0
@@@@ UPD: 2021-02-23
LBL "RPART"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
XEQ "ATYP"
GTO IND ST X
LBL 00
RTNERR 4             @@## REQ:free42>=2.5.24
LBL 01               @@@@ Real Number
LBL 02               @@@@ Real Matrix
R↓
RTN
LBL 03               @@@@ Complex Number
R↓
FS? 73
→REC
COMPLEX
X<>Y
RTN
LBL 04               @@@@ Complex Matrix
R↓
FS? 73
GTO 05
GTO 06
LBL 05               @@@@ POLAR MODE Complex Matrix
RECT
COMPLEX
X<>Y
POLAR
RTN
LBL 06               @@@@ RECT MODE Complex Matrix
COMPLEX
X<>Y
RTN
END
#+END_SRC

** =IPART=: Imaginary Part

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:IPART)
@@@@ DSC: Imaginary Part
@@@@ IN:  X: Number or numeric matrix (element-wise)
@@@@ OUT: X: ipart(x)
@@@@ TST: free42_3.0
@@@@ UPD: 2021-02-23
LBL "IPART"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
XEQ "ATYP"
GTO IND ST X
LBL 00
RTNERR 4             @@## REQ:free42>=2.5.24
LBL 01               @@@@ Real Number
0
RTN
LBL 02               @@@@ Real Matrix
R↓
DIM?
NEWMAT
RTN
LBL 03               @@@@ Complex Number
R↓
FS? 73
→REC
COMPLEX
RTN
LBL 04               @@@@ Complex Matrix
R↓
FS? 73
GTO 05
GTO 06
LBL 05               @@@@ POLAR MODE Complex Matrix
RECT
COMPLEX
POLAR
RTN
LBL 06               @@@@ RECT MODE Complex Matrix
COMPLEX
RTN
END
#+END_SRC

** =CONJ=: Complex Conjugate

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:CONJ)
@@@@ DSC: Complex Conjugate
@@@@ IN:  X: Number or numeric matrix (element-wise)
@@@@ OUT: X: conj(x)
@@@@ TST: free42_3.0
@@@@ BUG: Fails on alpha string matrix
@@@@ UPD: 2021-02-23
LBL "CONJ"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
XEQ "ATYP"
GTO IND ST X
LBL 00
RTNERR 4             @@## REQ:free42>=2.5.24
LBL 01               @@@@ Real Number
LBL 02               @@@@ Real Matrix
R↓
RTN
LBL 03               @@@@ Complex Number
LBL 04               @@@@ Complex Matrix
R↓
COMPLEX
+/-
COMPLEX
RTN
END
#+END_SRC

** =CABS=: Magnitude/absolute value

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:CABS)
@@@@ DSC: Magnitude/absolute value
@@@@ IN:  X: Number or numeric matrix (element-wise) -- built in ABS won't work with a complex matrix
@@@@ OUT: X: |x|
@@@@ TST: CPXRES free42_3.0
@@@@ BUG: Returns 0 for real 0 input
@@@@ UPD: 2021-02-23
LBL "CABS"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
XEQ "ATYP"
GTO IND ST X
LBL 00
RTNERR 4             @@## REQ:free42>=2.5.24
LBL 01               @@@@ Real Number
LBL 02               @@@@ Real Matrix
R↓
ABS
RTN
LBL 03               @@@@ Complex Number
R↓
FC? 73
→POL
COMPLEX
X<>Y
RTN
LBL 04               @@@@ Complex Matrix
R↓
FC? 73
GTO 05
GTO 06
LBL 05               @@@@ RECT MODE Complex Matrix
POLAR
COMPLEX
X<>Y
RECT
RTN
LBL 06               @@@@ POLAR MODE Complex Matrix
COMPLEX
X<>Y
RTN
END
#+END_SRC

** =CARG=: Complex Argument

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:CARG)
@@@@ DSC: Complex Argument
@@@@ IN:  X: Number or numeric matrix (element-wise)
@@@@ OUT: X: arg(x)
@@@@ TST: CPXRES free42_3.0
@@@@ BUG: Returns 0 for real 0 input
@@@@ UPD: 2021-02-23
LBL "CARG"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
XEQ "ATYP"
GTO IND ST X
LBL 00
RTNERR 4             @@## REQ:free42>=2.5.24
LBL 01               @@@@ Real Number
0
RTN
LBL 02               @@@@ Real Matrix
R↓
DIM?
NEWMAT
RTN
LBL 03               @@@@ Complex Number
R↓
FC? 73
→POL
COMPLEX
RTN
LBL 04               @@@@ Complex Matrix
R↓
FC? 73
GTO 05
GTO 06
LBL 05               @@@@ RECT MODE Complex Matrix
POLAR
COMPLEX
RECT
RTN
LBL 06               @@@@ POLAR MODE Complex Matrix
COMPLEX
RTN
END
#+END_SRC

* Math Functions

** =%T=: Percentage of total (just like hp-12c button)

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:PTOT)
@@@@ DSC: Percentage of total (just like hp-12c button)
@@@@ IN:  Y: Real Number -- Total
@@@@ IN:  X: Real Number -- Part
@@@@ OUT: X: 100*X/Y
@@@@ UPD: 2021-03-12
LBL "%T"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
X<>Y
÷
100
×
RTN
END
#+END_SRC

** =SGN=: Sign function

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:SGN)
@@@@ DSC: Sign function
@@@@ IN:  X: a number
@@@@ OUT: X:  0 when input was 0
@@@@         -1 when input was negative
@@@@          1 when input was positive
@@@@ UPD: 2021-02-26
@@@@ TST: free42_3.0
LBL "SGN"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
ABS
X≠0?
GTO 01
@@@@ zero
0
RTN
LBL 01
@@@@ Non-zero
X<>Y
REAL?
GTO 02
@@@@ Complex non-zero
X<>Y
÷
RTN
LBL 02
@@@@ Real non-zero
X<0?
GTO 03
@@@@ Real positive
1
RTN
LBL 03
@@@@ Real negative
-1
RTN
END
#+END_SRC

** =lnXY=: Base Y Logarithm of X

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:lnXY)
@@@@ DSC: Base Y Logarithm of X
@@@@ IN:  Y: logarithm base
@@@@      X: number or matrix (element-wise)
@@@@ OUT: X: log_y(x)
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
LBL "lnXY"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
LN
X<>Y
LN
X<>Y
÷
RTN
END
#+END_SRC

** =ln2=: Base 2 Logarithm

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:ln2)
@@@@ DSC: Base 2 Logarithm
@@@@ IN:  X: number or matrix (element-wise)
@@@@ OUT: X: log_2(x)
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
LBL "ln2"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
LN
2
LN
÷
RTN
END
#+END_SRC

** =2↑X=: Raise 2 to the power of X

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:POW2)
@@@@ DSC: Raise 2 to the power of X
@@@@ IN:  X: number or matrix (element-wise)
@@@@ OUT: X: 2^X
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
LBL "2↑X"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
2
X<>Y
Y^X
RTN
END
#+END_SRC

** =GCD=: Greatest Common Denominator

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:GCD)
@@@@ DSC: GCD
@@@@ IN:  Y: real number
@@@@      X: real number
@@@@ OUT: X: GCD(|IP(X)|, |IP(X)|)
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
LBL "GCD"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ABS
IP
X<>Y
ABS
IP
X>Y?
X<>Y
LBL 01
STO ST Z
MOD
X>0?
GTO 01
R↓
RTN
END
#+END_SRC

** =LCM=: Least Common Multiple

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:LCM)
@@@@ DSC: LCM
@@@@ IN:  Y: real number
@@@@      X: real number
@@@@ OUT: X: LCM(|IP(X)|, |IP(X)|)
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
LBL "LCM"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ABS
IP
X≠0?
GTO 01
0
RTN
LBL 01
LSTO "TMPX"
X<>Y
ABS
IP
X≠0?
GTO 02
0
RTN
LBL 02
LSTO "TMPY"
XEQ "GCD"
RCL "TMPX"
X<>Y
÷
RCL× "TMPY"
RTN
END
#+END_SRC

** =NPDF=: Standard Normal Density

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:NPDF)
@@@@ DSC: Standard Normal PDF
@@@@ IN:  X: real number
@@@@ OUT: X: Standard Normal PDF value at X
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
LBL "NPDF"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
X↑2
-2
÷
E↑X
2
PI
×
SQRT
÷
RTN
END
#+END_SRC

** =NCDF=: Standard Normal CDF

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:NCDF)
@@@@ DSC: Standard Normal CDF
@@@@ IN:  X: real number
@@@@ OUT: X: Standard Normal CDF value at X
@@@@ BUG: Only good to 7 decimal places
@@@@ REF: Zelen & Severo (1964)
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
LBL "NCDF"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
XEQ 00
×
1
+
1/X
LSTO "T"
1.005
LSTO "I"
0
LBL 11
XEQ IND "I"
RCL× "T"
+
RCL ST Y
STO× "T"
R↓
ISG "I"
GTO 11
RCL ST Z
XEQ "NPDF"
×
1
X<>Y
-
RTN
LBL 00
0.2316419
RTN
LBL 01
0.319381530
RTN
LBL 02
-0.356563782
RTN
LBL 03
1.781477937
RTN
LBL 04
-1.821255978
 RTN
LBL 05
1.330274429
RTN
END
#+END_SRC

** =ERF=: The erf (error) function

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:ERF)
@@@@ DSC: erf (error) function
@@@@ IN:  X: real number
@@@@ OUT: X: erf(X)
@@@@ FAW: Uses NCDF
@@@@ UPD: 2021-03-30
@@@@ TST: free42_3.0
LBL "ERF"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
ENTER
2
SQRT
×
ABS
XEQ "NCDF"
2
×
1
-
X<>Y
X<0?
GTO 01
GTO 02
LBL 01
R↓
+/-
RTN
LBL 02
R↓
RTN
END
#+END_SRC

** =ERFC=: The erfc (complementary error) function

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:ERFC)
@@@@ DSC: erfc (complementary error) function
@@@@ IN:  X: real number
@@@@ OUT: X: erfc(X)
@@@@ FAW: Uses ERF
@@@@ UPD: 2021-03-30
@@@@ TST: free42_3.0
LBL "ERFC"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
XEQ "ERF"
1
X<>Y
-
RTN
END
#+END_SRC

** =FLOOR=: Floor

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:FLOOR)
@@@@ DSC: Floor
@@@@ IN:  X: real number
@@@@ OUT: X: floor(X)
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
LBL "FLOOR"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
IP
LASTX
FP
X<0?
GTO 01
R↓
RTN
LBL 01
1
-
RTN
END
#+END_SRC

** =CEIL=: Ceiling

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:CEIL)
@@@@ DSC: Ceiling
@@@@ IN:  X: real number
@@@@ OUT: X: ceil(X)
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
LBL "CEIL"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
IP
LASTX
FP
X>0?
GTO 01
R↓
RTN
LBL 01
1
+
RTN
END
#+END_SRC

** =HYPOT=: The Hypot function

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:HYPOT)
@@@@ DSC: Hypot
@@@@ IN:  Y: number
@@@@ IN:  X: number
@@@@ OUT: X: sqrt(x^2+y^2)
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
@@@@ BUG: Some overflow cases could be avoided by scaling...
LBL "HYPOT"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
X↑2
X<>Y
X↑2
+
SQRT
RTN
END
#+END_SRC

** =ATAN2=: Two Argument Inverse Tangent

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:ATAN2)
@@@@ DSC: ATAN2
@@@@ IN:  Y: number
@@@@ IN:  X: number
@@@@ OUT: X: atan2(y, x)
@@@@ BUG: Only works in RAD mode
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
@@@@ TC:  atan( 1, 1) =>  pi/4
@@@@ TC:  atan(-1, 1) => -pi/4
@@@@ TC:  atan( 1,-1) =>  3*pi/4
@@@@ TC:  atan(-1,-1) => -3*pi/4
@@@@ TC:  atan( 0, 1) =>  0
@@@@ TC:  atan( 1, 0) =>  pi
@@@@ TC:  atan(-1, 0) => -pi
@@@@ TC:  atan( 0, 0) => ERROR
LBL "ATAN2"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
X>0?
GTO 01
X=0?
GTO 02
@@@@ X<0
X<>Y
X<0?
GTO 03
@@@@ X<0 & Y>=0
X<>Y
÷
ATAN
PI
+
RTN
LBL 03
@@@@ X<0 & Y<0
X<>Y
÷
ATAN
PI
-
RTN
LBL 02
X<>Y
X=0?
GTO 04
X>0?
GTO 05
@@@@ X=0 & Y<0
PI
-2
÷
RTN
LBL 05
@@@@ X=0 & Y>0
PI
2
÷
RTN
LBL 04
@@@@ X=0 & Y=0 ERROR 0/0
÷
RTN
LBL 01
@@@@ X>0
÷
ATAN
RTN
END
#+END_SRC

** =YROOT=: Nth Roots prefering real & pure imaginary results

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:YROOT)
@@@@ DSC: Nth roots prefering real & pure imaginary answers
@@@@ IN:  Y: Number
@@@@ IN:  X: Number or numeric matrix (element-wise)
@@@@ OUT: X: Yth root of X
@@@@ BUG: The principal value is not always returned (by design)
@@@@ BUG: Real integers are not recognized in complex form. i.e. 0+2i ≠ 2
@@@@ FAQ: Return is pure imaginary when Y is an odd integer and X<0
@@@@ FAQ: Return is real when  Y is an even integer and X<0
@@@@ TST: free42_3.0
@@@@ UPD: 2021-02-23
LBL "YROOT"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
REAL?
GTO 01
GTO 92
LBL 01
@@@@ X is real
X≥0?
GTO 92
@@@@ X is negative, real
RCL ST Y
REAL?
GTO 02
GTO 91
LBL 02
@@@@ X is negative, real; Y is real
FP
X≠0?
GTO 91
@@@@ X is negative, real; Y is real integer
R↓
ABS
RCL ST Y
X<>Y
XEQ 92
+/-
X<>Y
2
÷
FP
X=0?
GTO 03
GTO 04
LBL 03
@@@@ X is negative, real; Y is real integer even
R↓
-1
SQRT
×
RTN
LBL 04
@@@@ X is negative, real; Y is real integer odd
R↓
RTN
LBL 91
@@@@ Not special case.  Stack: Y X Y
R↓
LBL 92
@@@@ Not special case.  Stack: X Y
X<>Y
1/X
Y↑X
RTN
END
#+END_SRC

** =BETA=: The Beta Function

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:BETA)
@@@@ DSC: beta function
@@@@ IN:  Y: Number
@@@@ IN:  X: Number
@@@@ OUT: X: beta(x, y) = beta(y, x)
@@@@ TST: free42_3.0
@@@@ UPD: 2021-02-23
LBL "BETA"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
RCL ST Y
GAMMA
RCL ST Y
GAMMA
×
RCL ST Z
RCL ST Z
+
GAMMA
÷
RTN
END
#+END_SRC

#+END_SRC

* Polynomials

** =QUADE=: Solve Quadratic Equation (Coef on Stack)

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:QUADE)
@@@@ DSC: Solve quadratic equation: ax^2+bx+c=0
@@@@ IN:  Z: a
@@@@      Y: b
@@@@      X: c
@@@@ OUT: Y: root_1 (-b-sqrt(b^2-4ac))/(2a)
@@@@      X: root_2 (-b+sqrt(b^2-4ac))/(2a)
@@@@ TST: free42_3.0
@@@@ BUG: No error checking
@@@@ UPD: 2021-04-02
LBL "QUADE"
FUNC 32              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
-4
RCL× ST T
×
RCL ST Y
X↑2
+
SQRT
2
RCLx ST T
÷
LASTX
RCL ST Z
+/-
X<>Y
÷
RCL ST X
RCL ST Z
-
RCL ST Z
RCL ST Z
+
RTN
END
#+END_SRC

** =PLYE=: Evaluate a Polynomial (Coef in Matrix)

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:PLYE)
@@@@ DSC: Evaluate a polynomial
@@@@ IN:  Y: Matrix with polynomial coefficients.  DIM of 1xn, nx1, whatever...
@@@@      X: Value at which polynomial should be evaluated
@@@@ OUT: Y: original polynomial matrix
@@@@      X: value of polynomial evaluated at X
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-03
LBL "PLYE"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
X<>Y
LSTO "TMPM"
INDEX "TMPM"
WRAP
0
LBL 01
RCL× ST Z
RCLEL
+
J+
FC? 77
GTO 32
RTN
END
#+END_SRC

** =PWRP=: Wrap a Matrix Polynomial in a =MVAR= Function

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:PWRP)
@@@@ DSC: Make a polynomial stored in a matrix into a =MVAR= function
@@@@ IN:  X: N/A
@@@@ OUT: X: N/A
@@@@ GLB: _PLY -- Store a polynomial matrix in this variable
@@@@ TST: free42_3.0
@@@@ FAQ: Allows one to use SOLVER & INTEG on polynomials
@@@@ UPD: 2021-04-03
LBL "PWRP"
MVAR "X"
RCL "_PLY"
RCL "X"
XEQ "PLYE"
X<>Y
R↓
RTN
END
#+END_SRC

* 3D Vector Menu

This is a simple little application that makes working with 3D vectors less painfull.  

#+ATTR_HTML: :align center :frame box :rules all
|-------+---------------------------------------------------------------------|
| Menu  |                                                                     |
|-------+---------------------------------------------------------------------|
| →V    | Put stack elements X, Y, & Z into a vector: [Z, Y, X]               |
| V→    | Vector contents to stack. [A, B, C] => X: C, Y: B, Z: A             |
| DOT   | Dot product                                                         |
| CROSS | Cross product                                                       |
| MAG   | Euculidian magnitude                                                |
| VVIEW | View a vector one element at a time -- press =R/S= for next element |
|-------+---------------------------------------------------------------------|

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:VEC3)
@@@@ DSC: Handy menu making 3D vectors easy to manipulate
@@@@ IN:  N/A
@@@@ OUT: N/A
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-05
LBL "3VEC"
L4STK
LBL 01
CLMENU
"→V"
KEY 1 XEQ 02
"V→"
KEY 2 XEQ 03
"DOT"
KEY 3 XEQ 04
"CROSS"
KEY 4 XEQ 05
"MAG"
KEY 5 XEQ 06
"VVIEW"
KEY 6 XEQ 07
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00
EXITALL
RTN
LBL 02               @@@@ Action for menu key →V
FUNC 31
3
XEQ 81
LSTO "TMPM"
R↓
INDEX "TMPM"
WRAP
J-
LBL 92
STOEL
R↓
J-
FC? 77
GTO 92
RCL "TMPM"
RTN
LBL 03               @@@@ Action for menu key V→
FUNC 13
LSTO "TMPM"
R↓
INDEX "TMPM"
WRAP
LBL 91
RCLEL
J+
FC? 77
GTO 91
RTN
LBL 04               @@@@ Action for menu key DOT
FUNC 21
DOT
RTN
LBL 05               @@@@ Action for menu key CROSS
FUNC 21
CROSS
RTN
LBL 06               @@@@ Action for menu key MAG
FUNC 11
LSTO "TMPM"
R↓
INDEX "TMPM"
WRAP
0
LBL 93
RCLEL
X↑2
+
J+
FC? 77
GTO 93
RTN
SQRT
LBL 07               @@@@ Action for menu key "VVIEW"
FUNC 00
LSTO "TMPM"
INDEX "TMPM"
WRAP
1
LBL 94
CLA
ARCL ST X
├":"
RCLEL
ARCL ST X
R↓
AVIEW
STOP
1
+
J+
FC? 77
GTO 94
RTN
LBL 81 @@@@ NEWVEC
FUNC 11
1
NEWMAT
RTN
END
#+END_SRC

* WORKING                                                          :noexport:

#+BEGIN_SRC text
:::::::::::::::::::::::'##:::::'##::::'###::::'########::'##::: ##:'####:'##::: ##::'######::::::::::::::::::::::::
::::::::::::::::::::::: ##:'##: ##:::'## ##::: ##.... ##: ###:: ##:. ##:: ###:: ##:'##... ##:::::::::::::::::::::::
::::::::::::::::::::::: ##: ##: ##::'##:. ##:: ##:::: ##: ####: ##:: ##:: ####: ##: ##:::..::::::::::::::::::::::::
::::::::::::::::::::::: ##: ##: ##:'##:::. ##: ########:: ## ## ##:: ##:: ## ## ##: ##::'####::::::::::::::::::::::
::::::::::::::::::::::: ##: ##: ##: #########: ##.. ##::: ##. ####:: ##:: ##. ####: ##::: ##:::::::::::::::::::::::
::::::::::::::::::::::: ##: ##: ##: ##.... ##: ##::. ##:: ##:. ###:: ##:: ##:. ###: ##::: ##:::::::::::::::::::::::
:::::::::::::::::::::::. ###. ###:: ##:::: ##: ##:::. ##: ##::. ##:'####: ##::. ##:. ######::::::::::::::::::::::::
::::::::::::::::::::::::...::...:::..:::::..::..:::::..::..::::..::....::..::::..:::......:::::::::::::::::::::::::
#+END_SRC

Code in this section is under construction.  Most likely broken.

* EOF

# End of document.

# The following adds some space at the bottom of exported HTML
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
