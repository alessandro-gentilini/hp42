# -*- Mode:Org; Coding:utf-8; fill-column:158 -*-
#+TITLE:       free42 Random Math Stuff
#+AUTHOR:      Mitch Richling
#+EMAIL:       http://www.mitchr.me/
#+DATE:        2021-03-19
#+DESCRIPTION: Some simple math stuff for free42
#+LANGUAGE:    en
#+OPTIONS:     num:t toc:nil \n:nil @:t ::t |:t ^:nil -:t f:t *:t <:t skip:nil d:nil todo:t pri:nil H:5 p:t author:t html-scripts:nil
#+HTML_HEAD: <style>body { width: 95%; margin: 2% auto; font-size: 18px; line-height: 1.4em; font-family: Georgia, serif; color: black; background-color: white; }</style>
#+HTML_HEAD: <style>body { min-width: 500px; max-width: 1024px; }</style>
#+HTML_HEAD: <style>h1,h2,h3,h4,h5,h6 { color: #A5573E; line-height: 1em; font-family: Helvetica, sans-serif; }</style>
#+HTML_HEAD: <style>h1,h2,h3 { line-height: 1.4em; }</style>
#+HTML_HEAD: <style>h1.title { font-size: 3em; }</style>
#+HTML_HEAD: <style>h4,h5,h6 { font-size: 1em; }</style>
#+HTML_HEAD: <style>.org-src-container { border: 1px solid #ccc; box-shadow: 3px 3px 3px #eee; font-family: Lucida Console, monospace; font-size: 80%; margin: 0px; padding: 0px 0px; position: relative; }</style>
#+HTML_HEAD: <style>.org-src-container>pre { line-height: 1.2em; padding-top: 1.5em; margin: 0.5em; background-color: #404040; color: white; overflow: auto; }</style>
#+HTML_HEAD: <style>.org-src-container>pre:before { display: block; position: absolute; background-color: #b3b3b3; top: 0; right: 0; padding: 0 0.2em 0 0.4em; border-bottom-left-radius: 8px; border: 0; color: white; font-size: 100%; font-family: Helvetica, sans-serif;}</style>
#+HTML_HEAD: <style>pre.example { white-space: pre-wrap; white-space: -moz-pre-wrap; white-space: -o-pre-wrap; font-family: Lucida Console, monospace; font-size: 80%; background: #404040; color: white; display: block; padding: 0em; border: 2px solid black; }</style>
#+HTML_LINK_HOME: https://www.mitchr.me/
#+HTML_LINK_UP: https://richmit.github.io/hp42/
#+EXPORT_FILE_NAME: ../docs/math

#+ATTR_HTML: :border 2 solid #ccc :frame hsides :align center
|          <r> | <l>                                          |
|    *Author:* | /{{{author}}}/                               |
|   *Updated:* | /{{{modification-time(%Y-%m-%d %H:%M:%S)}}}/ |
#+ATTR_HTML: :align center
Copyright {{{time(%Y)}}} Mitch Richling. All rights reserved.

#+TOC: headlines 5

#        #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #
#   00   #    10   #    20   #    30   #    40   #    50   #    60   #    70   #    80   #    90   #   100   #   110   #   120   #   130   #   140   #   150   #   160   #
# 234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789
#        #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #
#        #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #

# Provde links to programs like this: https://richmit.github.io/hp42/math.html#coderef-lnXY

* Metadata

The home for this HTML file is: https://richmit.github.io/hp42/math.html

Files related to this document may be found on github: https://github.com/richmit/hp42

Directory contents:
#+ATTR_HTML: :border 0 :frame none :rules none :align center
   | =src=     | - | The org-mode file that generated this HTML document            |
   | =src_42s= | - | Ready to convert source listings for 42s code in this document |
   | =docs=    | - | This html document                                             |
   | =bin=     | - | Importable RAW program files                                   |

* Introduction
:PROPERTIES:
:CUSTOM_ID: introduction
:END:

This org-mode file collects together a handful of mathematical stuff I find useful.  I keep this stuff in my [[https://richmit.github.io/hp42/math.html][custom menu]].

* =CPLXF=: Complex Number Functions
** Menu
:PROPERTIES:
:CUSTOM_ID: menu-cplxf
:END:

#+ATTR_HTML: :align center :frame box :rules all
#+NAME:CPLXF
| Menu  | Description                                                                |
|-------+----------------------------------------------------------------------------|
| RPART | Real Part of anumber or matrix                                             |
| IPART | Imaginary Part of a number or matrix                                       |
| CONJ  | Complex conjugate of a number or matrix                                    |
| CABS  | Magnitude of a number or matrix (built in =ABS= fails on a complex matrix) |
| CARG  | Complex argument of a number or matrix                                     |
|-------+----------------------------------------------------------------------------|

** Menu Code

#+BEGIN_SRC elisp :var tbl=CPLXF :colnames y :results output verbatum :wrap "src hp42s :tangle ../src_42s/math/math.hp42s"
(MJR-generate-42-menu-code "CPLXF" 0 tbl 0 nil 'stay 'up #'MJR-custom-gen-lab #'MJR-custom-gen-sub)
#+END_SRC

#+RESULTS:
#+begin_src hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:CPLXF)
@@@@ DSC: Auto-generated menu program
LBL "CPLXF"
LBL 01            @@@@ Page 1 of menu CPLXF
CLMENU
"RPART"
KEY 1 XEQ "RPART"
"IPART"
KEY 2 XEQ "IPART"
"CONJ"
KEY 3 XEQ "CONJ"
"CABS"
KEY 4 XEQ "CABS"
"CARG"
KEY 5 XEQ "CARG"
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00 @@@@ Application Exit
EXITALL
RTN
@@@@ Free labels start at: 2
#+end_src

** Functions

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:RPART)
@@@@ DSC: Real part
@@@@ IN:  X: Number or numeric matrix (element-wise)
@@@@ OUT: X: rpart(x)
@@@@ LBL: 10-14
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-22
LBL "RPART"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
XEQ 80 @@@@ ATYP
10
+
GTO IND ST X
LBL 10
RTNERR 4             @@## REQ:free42>=2.5.24
LBL 11               @@@@ Real Number
LBL 12               @@@@ Real Matrix
R↓
RTN
LBL 13               @@@@ Complex Number
LBL 14               @@@@ Complex Matrix
R↓
XEQ 90 @@@@ C→R&I
X<>Y
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:IPART)
@@@@ DSC: Imaginary Part
@@@@ IN:  X: Number or numeric matrix (element-wise)
@@@@ OUT: X: ipart(x)
@@@@ LBL: 20-24
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-22
LBL "IPART"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
XEQ 80 @@@@ ATYP
20
+
GTO IND ST X
LBL 20
RTNERR 4             @@## REQ:free42>=2.5.24
LBL 21               @@@@ Real Number
0
RTN
LBL 22               @@@@ Real Matrix
R↓
DIM?
NEWMAT
RTN
LBL 23               @@@@ Complex Number
LBL 24               @@@@ Complex Matrix
R↓
XEQ 90 @@@@ C→R&I
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:CONJ)
@@@@ DSC: Complex Conjugate
@@@@ IN:  X: Number or numeric matrix (element-wise)
@@@@ OUT: X: conj(x)
@@@@ TST: free42_3.0
@@@@ LBL: 30-34
@@@@ BUG: Fails on alpha string matrix
@@@@ UPD: 2021-04-22
LBL "CONJ"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
XEQ 80 @@@@ ATYP
30
+
GTO IND ST X
LBL 30
RTNERR 4             @@## REQ:free42>=2.5.24
LBL 31               @@@@ Real Number
LBL 32               @@@@ Real Matrix
R↓
RTN
LBL 33               @@@@ Complex Number
LBL 34               @@@@ Complex Matrix
R↓
COMPLEX
+/-
COMPLEX
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:CABS)
@@@@ DSC: Magnitude/absolute value
@@@@ IN:  X: Number or numeric matrix (element-wise) -- built in ABS won't work with a complex matrix
@@@@ OUT: X: |x|
@@@@ TST: CPXRES free42_3.0
@@@@ LBL: 40-44
@@@@ BUG: Returns 0 for real 0 input
@@@@ UPD: 2021-04-22
LBL "CABS"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
XEQ 80 @@@@ ATYP
40
+
GTO IND ST X
LBL 40
RTNERR 4             @@## REQ:free42>=2.5.24
LBL 41               @@@@ Real Number
LBL 42               @@@@ Real Matrix
R↓
ABS
RTN
LBL 43               @@@@ Complex Number
LBL 44               @@@@ Complex Matrix
R↓
XEQ 92 @@@@ C→M&A
X<>Y
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:CARG)
@@@@ DSC: Complex Argument
@@@@ IN:  X: Number or numeric matrix (element-wise)
@@@@ OUT: X: arg(x)
@@@@ TST: CPXRES free42_3.0
@@@@ LBL: 50-54
@@@@ BUG: Returns 0 for real 0 input
@@@@ UPD: 2021-04-22
LBL "CARG"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
XEQ 80 @@@@ ATYP
50
+
GTO IND ST X
LBL 50
RTNERR 4             @@## REQ:free42>=2.5.24
LBL 51               @@@@ Real Number
0
RTN
LBL 52               @@@@ Real Matrix
R↓
DIM?
NEWMAT
RTN
LBL 53               @@@@ Complex Number
LBL 54               @@@@ Complex Matrix
R↓
XEQ 92 @@@@ C→M&A
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Complex Number -> Real Part & Imaginary Part
@@@@ NAM: C→R&I 90
@@@@ IN:  X: Complex Number or Complex Matrix
@@@@ OUT: Y: Real Part of X
@@@@      X: Imaginary Part of X
@@@@ LBL: 91
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-22
LBL 90
FUNC 12              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
FS? 73
GTO 91
@@@@ RECT MODE
COMPLEX
RTN
LBL 91
@@@@ POLAR MODE
RECT
COMPLEX
POLAR
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Complex Number -> Magnitude & Argument (angle)
@@@@ NAM: C→M&A 92
@@@@ IN:  X: Complex Number or Complex Matrix
@@@@ OUT: Y: Magnitude of X
@@@@      X: Complex Argument (angle) of X
@@@@ LBL: 93
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-22
LBL 92
FUNC 12              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
FS? 73
GTO 93
@@@@ RECT MODE
POLAR
COMPLEX
RECT
RTN
LBL 93
@@@@ POLAR MODE
COMPLEX
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:ATYP)
@@@@ DSC: Arithmetic Type
@@@@ NAM: ATYP 80
@@@@ IN:  X: an object
@@@@ OUT: X: 1 if input X was a real number
@@@@         2 if input X was a real matrix
@@@@         3 if input X was a complex number
@@@@         4 if input X was a complex matrix
@@@@         0 if none of the above are true
@@@@ TST: free42_3.0
@@@@ FAQ: Even on free42<2.5.24 or a real 42s, X, Y, & Z are preserved. T & Last X not so much.
@@@@ BUG: A 2 will be returned for a real matrix containing strings
@@@@ BUG: In infinite stack mode with an empty stack 2 will be returned as X=0 when we do L4STK
@@@@ LBL: 81-88
@@@@ UPD: 2021-02-23
LBL 80
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
MAT?
GTO 81
GTO 82
LBL 81               @@@@ Matrix
CLA
ARCL ST X
67
POSA
X>0?
GTO 87
GTO 88
LBL 87               @@@@ Complex Matrix
4
RTN
LBL 88               @@@@ Real/String Matrix
2
RTN
LBL 82               @@@@ Not matrix
REAL?
GTO 83
GTO 84
LBL 83               @@@@ Real number
1
RTN
LBL 84               @@@@ Not matrix or real
CPX?
GTO 85
GTO 86
LBL 85               @@@@ Complex number
3
RTN
LBL 86               @@@@ Not matrix, real, or complex
0
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
END
#+END_SRC

* =PERCFN=: Percentages
** Menu
:PROPERTIES:
:CUSTOM_ID: menu-percfn
:END:

#+ATTR_HTML: :align center :frame box :rules all
#+NAME:PERCFN
| Menu | Description                      |                  |                 |
|------+----------------------------------+------------------+-----------------|
| %T   | Percentage of total              | Y: TOTAL X: PART | X: % of Total   |
| %CH  | percentage of change from Y to X | Y: OLD X: New    | X: % Change     |
| %    | Percentage                       | Y: Y X: X        | Y: Y X: X% of Y |

** Menu Code

#+BEGIN_SRC elisp :var tbl=PERCFN :colnames y :results output verbatum :wrap "src hp42s :tangle ../src_42s/math/math.hp42s"
(MJR-generate-42-menu-code "PERCFN" 0 tbl 0 nil 'stay 'up #'MJR-custom-gen-lab #'MJR-custom-gen-sub)
#+END_SRC

#+RESULTS:
#+begin_src hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:PERCFN)
@@@@ DSC: Auto-generated menu program
LBL "PERCFN"
LBL 01            @@@@ Page 1 of menu PERCFN
CLMENU
"%T"
KEY 1 XEQ "%T"
"%CH"
KEY 2 XEQ 02
"%"
KEY 3 XEQ 03
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00 @@@@ Application Exit
EXITALL
RTN
LBL 02    @@@@ Action for menu key %CH
%CH
RTN
LBL 03    @@@@ Action for menu key %
%
RTN
@@@@ Free labels start at: 4
#+end_src

** Functions

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:PTOT)
@@@@ DSC: Percentage of total (just like hp-12c button)
@@@@ IN:  Y: Real Number -- Total
@@@@ IN:  X: Real Number -- Part
@@@@ OUT: X: 100*X/Y
@@@@ UPD: 2021-03-12
LBL "%T"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
X<>Y
÷
100
×
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
END
#+END_SRC

* =INTFUN=: Integer related functions
** Menu
:PROPERTIES:
:CUSTOM_ID: menu-intfun
:END:

#+ATTR_HTML: :align center :frame box :rules all
#+NAME:INTFUN
| Menu | LBL   | Description                                 |
|------+-------+---------------------------------------------|
| FLOR | FLOOR | Truncate toward negative infinity           |
| CEIL |       | Truncate toward positive infinity           |
| ROND | ROUND | Truncate toward nearest integer (even rule) |
| TRUN | IP    | Truncate toward zero                        |
|      |       |                                             |
| FP   |       |                                             |
|------+-------+---------------------------------------------|
| GCD  |       | Greatest Common Denominator                 |
| LCM  |       | Least Common Multiple                       |
|      |       |                                             |
|      |       |                                             |
| DIV  | BASE÷ |                                             |
| REM  | MOD   |                                             |
|------+-------+---------------------------------------------|

** Menu Code

#+BEGIN_SRC elisp :var tbl=INTFUN :colnames y :results output verbatum :wrap "src hp42s :tangle ../src_42s/math/math.hp42s"
(MJR-generate-42-menu-code "INTFUN" 0 tbl 0 1 'stay 'up #'MJR-custom-gen-lab #'MJR-custom-gen-sub)
#+END_SRC

#+RESULTS:
#+begin_src hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:INTFUN)
@@@@ DSC: Auto-generated menu program
LBL "INTFUN"
LBL 01            @@@@ Page 1 of menu INTFUN
CLMENU
"FLOR"
KEY 1 XEQ "FLOOR"
"CEIL"
KEY 2 XEQ "CEIL"
"ROND"
KEY 3 XEQ "ROUND"
"TRUN"
KEY 4 XEQ 03
"FP"
KEY 6 XEQ 04
KEY 7 GTO 02
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02            @@@@ Page 2 of menu INTFUN
CLMENU
"GCD"
KEY 1 XEQ "GCD"
"LCM"
KEY 2 XEQ "LCM"
"DIV"
KEY 5 XEQ 05
"REM"
KEY 6 XEQ 06
KEY 7 GTO 01
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 00 @@@@ Application Exit
EXITALL
RTN
LBL 03    @@@@ Action for menu key TRUN
IP
RTN
LBL 04    @@@@ Action for menu key FP
FP
RTN
LBL 05    @@@@ Action for menu key DIV
BASE÷
RTN
LBL 06    @@@@ Action for menu key REM
MOD
RTN
@@@@ Free labels start at: 7
#+end_src

** Functions

#+begin_src hp42s :tangle ../src_42s/math/math.hp42s

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:ROUND)
@@@@ DSC: Properly round to integer.  N.5 rounded to nearest even number.
@@@@ IN:  X: real number
@@@@ OUT: X: floor(X)
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
@@@@ TC:  -2.0 -2 →Int
@@@@ TC:  -1.6 -2 →Nearest
@@@@ TC:  -1.5 -2 →Even
@@@@ TC:  -1.4 -1 →Nearest
@@@@ TC:  -1.0 -1 →Int
@@@@ TC:  -1.6 -1 →Nearest
@@@@ TC:  -0.5  0 Even Rule
@@@@ TC:  -0.4  0 →Nearest
@@@@ TC:   0.0  0 →Int
@@@@ TC:   0.4  0 →Nearest
@@@@ TC:   0.5  0 Even Rule
@@@@ TC:   1.6  1 →Nearest
@@@@ TC:   1.0  1 →Int
@@@@ TC:   1.4  1 →Nearest
@@@@ TC:   1.5  2 →Even
@@@@ TC:   1.6  2 →Nearest
@@@@ TC:   2.0  2 →Int
LBL "ROUND"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER     @@@@ X        X
FP        @@@@ FP       X
ABS       @@@@ |FP|     X
0.5       @@@@ 1/2      |FP|     X
-         @@@@ |FP|-1/2 X
X<>Y      @@@@ X        |FP|-1/2 X
SIGN      @@@@ SGN      |FP|-1/2 X 
LASTX     @@@@ X        SGN      |FP|-1/2 X 
IP        @@@@ IP(X)    SGN      |FP|-1/2 X 
0=? ST Z
GTO 92
0<? ST Z
+
RTN
LBL 92 
@@@@ FP=1/2
XEQ 93 @@@@ ODD?
+
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: RETYES if X is odd, RTNNO otherwise
LBL 93 @@@@ ODD?
FUNC 00
L4STK
2
÷
FP
X=0?
RTNNO
RTNYES

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:FLOOR)
@@@@ DSC: Floor -- Round toward negative infinity
@@@@ IN:  X: real number
@@@@ OUT: X: floor(X)
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
LBL "FLOOR"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
FP
LASTX
IP
0≤? ST Y
RTN
1
-
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:CEIL)
@@@@ DSC: Ceiling -- Round toward positive infinity
@@@@ IN:  X: real number
@@@@ OUT: X: ceil(X)
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
LBL "CEIL"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
FP
LASTX
IP
0≥? ST Y
RTN
1
+
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:GCD)
@@@@ DSC: GCD
@@@@ IN:  Y: real number
@@@@      X: real number
@@@@ OUT: X: GCD(|IP(X)|, |IP(X)|)
@@@@ LBL: 91
@@@@ UPD: 2021-04-22
@@@@ TST: free42_3.0
LBL "GCD"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ABS
IP
X<>Y
ABS
IP
X>Y?
X<>Y
LBL 91
STO ST Z
MOD
X>0?
GTO 91
R↓
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:LCM)
@@@@ DSC: LCM
@@@@ IN:  Y: real number
@@@@      X: real number
@@@@ OUT: X: LCM(|IP(X)|, |IP(X)|)
@@@@ USE: GCD
@@@@ UPD: 2021-04-22
@@@@ TST: free42_3.0
LBL "LCM"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ABS                  @@@@ |X|             Y
IP                   @@@@ IP(|X|)         Y
X=0?                                      
RTN                                       
X<>Y                 @@@@ Y               IP(|X|) 
ABS                  @@@@ |Y|             IP(|X|) 
IP                   @@@@ IP(|Y|)         IP(|X|) 
X=0?                                      
RTN                                       
RCL ST Y             @@@@ IP(|X|)         IP(|Y|)         IP(|X|) 
RCL ST Y             @@@@ IP(|Y|)         IP(|X|)         IP(|Y|)         IP(|X|) 
×                    @@@@ IP(|Y|)*IP(|X|) IP(|Y|)         IP(|X|) 
RCL ST Z             @@@@ IP(|X|)         IP(|Y|)*IP(|X|) IP(|Y|)         IP(|X|) 
RCL ST Z             @@@@ IP(|Y|)         IP(|X|)         IP(|Y|)*IP(|X|) IP(|Y|)
XEQ "GCD"            @@@@ GCD             IP(|Y|)*IP(|X|) IP(|Y|)         IP(|Y|)
÷                    @@@@ LCM             IP(|Y|)         IP(|Y|)         IP(|Y|)
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
END
#+END_SRC

* =BINO=: Binomials, Factorals, Beta, etc...
** Menu
:PROPERTIES:
:CUSTOM_ID: menu-bino
:END:

#+ATTR_HTML: :align center :frame box :rules all
#+NAME:BINO
| Menu | LBL   | Description |
|------+-------+-------------|
| COMB |       |             |
| PERM |       |             |
| !    | FACT  | Factoral    |
| ▒▒▒▒ |       |             |
| GAM  | GAMMA |             |
| BETA |       |             |
|------+-------+-------------|

*** Menu Code

#+BEGIN_SRC elisp :var tbl=BINO :colnames y :results output verbatum :wrap "src hp42s :tangle ../src_42s/math/math.hp42s"
(MJR-generate-42-menu-code "BINO" 0 tbl 0 1 'stay 'up #'MJR-custom-gen-lab #'MJR-custom-gen-sub)
#+END_SRC

#+RESULTS:
#+begin_src hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:BINO)
@@@@ DSC: Auto-generated menu program
LBL "BINO"
LBL 01            @@@@ Page 1 of menu BINO
CLMENU
"COMB"
KEY 1 XEQ 02
"PERM"
KEY 2 XEQ 03
"!"
KEY 3 XEQ "FACT"
"GAM"
KEY 5 XEQ 04
"BETA"
KEY 6 XEQ "BETA"
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00 @@@@ Application Exit
EXITALL
RTN
LBL 02    @@@@ Action for menu key COMB
COMB
RTN
LBL 03    @@@@ Action for menu key PERM
PERM
RTN
LBL 04    @@@@ Action for menu key GAM
GAMMA
RTN
@@@@ Free labels start at: 5
#+end_src

** Functions

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:BETA)
@@@@ DSC: beta function
@@@@ IN:  Y: Number
@@@@ IN:  X: Number
@@@@ OUT: X: beta(x, y) = beta(y, x)
@@@@ TST: free42_3.0
@@@@ UPD: 2021-02-23
LBL "BETA"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
RCL ST Y
GAMMA
RCL ST Y
GAMMA
×
RCL ST Z
RCL ST Z
+
GAMMA
÷
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:FACT)
@@@@ DSC: Factoral function
@@@@ IN:  X: Number
@@@@ OUT: X: X! = Gamma(X+1)
@@@@ FAQ: Returns an error if X is not an integer
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-23
LBL "FACT"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
FP
X≠0?
RTNERR 5
R↓
1
+
GAMMA
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
END
#+END_SRC

* =ERRFUN=: Error & Standard Normal Functions
** Menu
:PROPERTIES:
:CUSTOM_ID: menu-errfun
:END:

#+ATTR_HTML: :align center :frame box :rules all
#+NAME:ERRFUN
| Menu | Description                                      |
|------+--------------------------------------------------|
| ERF  | Error Function                                   |
| ERFC | Complementary Error Function                     |
| ▒▒▒▒ |                                                  |
| NPDF | Standard Normal Probablity Density FUnction      |
| NCDF | Standard Normal Cumulative Distribution FUnction |
| ▒▒▒▒ |                                                  |

** Menu Code

#+BEGIN_SRC elisp :var tbl=ERRFUN :colnames y :results output verbatum :wrap "src hp42s :tangle ../src_42s/math/math.hp42s"
(MJR-generate-42-menu-code "ERRFUN" 0 tbl 0 nil 'stay 'up #'MJR-custom-gen-lab #'MJR-custom-gen-sub)
#+END_SRC

#+RESULTS:
#+begin_src hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:ERRFUN)
@@@@ DSC: Auto-generated menu program
LBL "ERRFUN"
LBL 01            @@@@ Page 1 of menu ERRFUN
CLMENU
"ERF"
KEY 1 XEQ "ERF"
"ERFC"
KEY 2 XEQ "ERFC"
"NPDF"
KEY 4 XEQ "NPDF"
"NCDF"
KEY 5 XEQ "NCDF"
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00 @@@@ Application Exit
EXITALL
RTN
@@@@ Free labels start at: 2
#+end_src

** Functions

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:NPDF)
@@@@ DSC: Standard Normal PDF
@@@@ IN:  X: real number
@@@@ OUT: X: Standard Normal PDF value at X
@@@@ UPD: 2021-04-22
@@@@ TST: free42_3.0
@@@@ TC:  -2 0.05399096651318805195056
@@@@ TC:  -1 0.2419707245191433497978
@@@@ TC:   0 0.3989422804014326779399
@@@@ TC:   1 0.2419707245191433497978
@@@@ TC:   2 0.05399096651318805195056
LBL "NPDF"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
X↑2   @@@@ X^2
-2    @@@@ -2                     X^2
÷     @@@@ -X^2/2
E↑X   @@@@ EXP(-X^2/2)
2     @@@@ 2                      EXP(-X^2/2)
PI    @@@@ PI                     2             EXP(-X^2/2)
×     @@@@ PI*2                   EXP(-X^2/2)
SQRT  @@@@ SQRT(PI*2)             EXP(-X^2/2)
÷     @@@@ EXP(-X^2/2)/SQRT(PI*2) 
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:NCDF)
@@@@ DSC: Standard Normal CDF
@@@@ IN:  X: real number
@@@@ OUT: X: Standard Normal CDF value at X
@@@@ BUG: Only good to 7 decimal places
@@@@ FAQ: No dependancies, variables, loops, or branches
@@@@ REF: Zelen & Severo (1964)
@@@@ UPD: 2021-04-22
@@@@ TST: free42_3.0
@@@@ TC:  -2 0.02275013194817920720028
@@@@ TC:  -1 0.1586552539314570514148 
@@@@ TC:   0 0.5                      
@@@@ TC:   1 0.8413447460685429485852 
@@@@ TC:   2 0.9772498680518207927997 
LBL "NCDF"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
0.2316419      @@@@ b0                     X             ?           ?
RCL× ST Y      @@@@ b0*X                   X             ?           ?
1              @@@@ 1                      b0*X          X           ?           
+              @@@@ 1+b0*X                 X             ?           ?
1/X            @@@@ 1/(1+b0*X)             X             ?           ?
               @@@@ T                      X             ?           ?
X<>Y           @@@@ X                      T             ?           ?
X↑2            @@@@ X^2                    T             ?           ?
-2             @@@@ -2                     X^2           ?           ?
÷              @@@@ -X^2/2                 T             ?           ?
E↑X            @@@@ EXP(-X^2/2)            T             ?           ?
2              @@@@ 2                      EXP(-X^2/2)   T           ?
PI             @@@@ PI                     2             EXP(-X^2/2) T
×              @@@@ PI*2                   EXP(-X^2/2)   T           T
SQRT           @@@@ SQRT(PI*2)             EXP(-X^2/2)   T           T
÷              @@@@ EXP(-X^2/2)/SQRT(PI*2) T             T           T
               @@@@ N                      T             T           T
RCL  ST Y      @@@@ T                      N             T           T
×              @@@@ NT                     T             T           T
0.319381530    @@@@ b1                     NT            T           T
RCL× ST Y      @@@@ PR                     NT            T           T
RCL ST Z       @@@@ T                      PR            NT          T
STO× ST Z      @@@@ T                      PR            NT^2        T
R↓             @@@@ PR                     NT^2          T           T
-0.356563782   @@@@ b2                     PR            NT^2        T
RCL× ST Z      @@@@ NT                     PR            NT^2        T
+              @@@@ PR                     NT^2          T           T
RCL ST Z       @@@@ T                      PR            NT^2        T
STO× ST Z      @@@@ T                      PR            NT^3        T
R↓             @@@@ PR                     NT^3          T           T
1.781477937    @@@@ b3                     PR            NT^3        T
RCL× ST Z      @@@@ NT                     PR            NT^3        T
+              @@@@ PR                     NT^3          T           T
RCL ST Z       @@@@ T                      PR            NT^3        T
STO× ST Z      @@@@ T                      PR            NT^4        T
R↓             @@@@ PR                     NT^4          T           T
-1.821255978   @@@@ b4                     PR            NT^4        T
RCL× ST Z      @@@@ NT                     PR            NT^4        T
+              @@@@ PR                     NT^4          T           T
RCL ST Z       @@@@ T                      PR            NT^4        T
STO× ST Z      @@@@ T                      PR            NT^5        T
R↓             @@@@ PR                     NT^5          T           T
1.330274429    @@@@ b5                     PR            NT^5        T
RCL× ST Z      @@@@ NT                     PR            NT^5        T
+              @@@@ PR                     NT^5          T           T
1              @@@@ 1                      PR            NT^5        T
X<>Y           @@@@ PR                     1             NT^5        T
-              @@@@ 1-PR                   NT^5          T           T
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:ERF)
@@@@ DSC: erf (error) function
@@@@ IN:  X: real number
@@@@ OUT: X: erf(X)
@@@@ USE: NCDF
@@@@ LBL: Use: 11-12
@@@@ UPD: 2021-03-30
@@@@ TST: free42_3.0
@@@@ TC: -1 -0.8427007929497148693412
@@@@ TC:  0  0.0
@@@@ TC:  1  0.8427007929497148693412
@@@@ TC:  2  0.9953222650189527341621
LBL "ERF"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
ENTER
2
SQRT
×
ABS
XEQ "NCDF"
2
×
1
-
X<>Y
X<0?
GTO 11
GTO 12
LBL 11
R↓
+/-
RTN
LBL 12
R↓
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:ERFC)
@@@@ DSC: erfc (complementary error) function
@@@@ IN:  X: real number
@@@@ OUT: X: erfc(X)
@@@@ USE: ERF
@@@@ UPD: 2021-03-30
@@@@ TST: free42_3.0
@@@@ TC: -1 1.842700792949714869341
@@@@ TC:  0 1.0
@@@@ TC:  1 0.1572992070502851306588
@@@@ TC:  2 0.004677734981047265837931
LBL "ERFC"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
XEQ "ERF"
1
X<>Y
-
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
END
#+END_SRC



* =HYPTRG=: Hyperbolic Trigonometric Functions
** Menu
:PROPERTIES:
:CUSTOM_ID: menu-hyptrg
:END:

#+ATTR_HTML: :align center :frame box :rules all
#+NAME:HYPTRG
| Menu  | Description |
|-------+-------------|
| SINH  |             |
| COSH  |             |
| TANH  |             |
| ASINH |             |
| ACOSH |             |
| ATANH |             |
|-------+-------------|
| CSCH  | 1/SINH(X)   |
| SECH  | 1/COSH(X)   |
| COTH  | 1/TANH(X)   |
| ACSCH | SINH(1/X)   |
| ASECH | COSH(1/X)   |
| ACOTH | TANH(1/X)   |
|-------+-------------|

** Menu Code

#+BEGIN_SRC elisp :var tbl=HYPTRG :colnames y :results output verbatum :wrap "src hp42s :tangle ../src_42s/math/math.hp42s"
(MJR-generate-42-menu-code "HYPTRG" 0 tbl 0 nil 'stay 'up #'MJR-custom-gen-lab #'MJR-custom-gen-sub)
#+END_SRC

#+RESULTS:
#+begin_src hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:HYPTRG)
@@@@ DSC: Auto-generated menu program
LBL "HYPTRG"
LBL 01            @@@@ Page 1 of menu HYPTRG
CLMENU
"SINH"
KEY 1 XEQ 03
"COSH"
KEY 2 XEQ 04
"TANH"
KEY 3 XEQ 05
"ASINH"
KEY 4 XEQ 06
"ACOSH"
KEY 5 XEQ 07
"ATANH"
KEY 6 XEQ 08
KEY 7 GTO 02
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02            @@@@ Page 2 of menu HYPTRG
CLMENU
"CSCH"
KEY 1 XEQ "CSCH"
"SECH"
KEY 2 XEQ "SECH"
"COTH"
KEY 3 XEQ "COTH"
"ACSCH"
KEY 4 XEQ "ACSCH"
"ASECH"
KEY 5 XEQ "ASECH"
"ACOTH"
KEY 6 XEQ "ACOTH"
KEY 7 GTO 01
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 00 @@@@ Application Exit
EXITALL
RTN
LBL 03    @@@@ Action for menu key SINH
SINH
RTN
LBL 04    @@@@ Action for menu key COSH
COSH
RTN
LBL 05    @@@@ Action for menu key TANH
TANH
RTN
LBL 06    @@@@ Action for menu key ASINH
ASINH
RTN
LBL 07    @@@@ Action for menu key ACOSH
ACOSH
RTN
LBL 08    @@@@ Action for menu key ATANH
ATANH
RTN
@@@@ Free labels start at: 9
#+end_src

** Functions

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: 1/SIN(X)
LBL "CSCH"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
SINH
1/X
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: 1/COS(X)
LBL "SECH"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
COSH
1/X
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: 1/TAN(X)
LBL "COTH"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
TANH
1/X
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: SIN(1/X)
LBL "ACSCH"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
1/X
ASINH
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: COS(1/X)
LBL "ASECH"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
1/X
ACOSH
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: TAN(1/X)
LBL "ACOTH"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
1/X
ATANH
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
END
#+END_SRC

* =TRIG=: Trigonometric Functions
** Menu
:PROPERTIES:
:CUSTOM_ID: menu-trig
:END:

#+ATTR_HTML: :align center :frame box :rules all
#+NAME:TRIG
| Menu  | Description                         |
|-------+-------------------------------------|
| SIN   |                                     |
| COS   |                                     |
| TAN   |                                     |
| ASIN  |                                     |
| ACOS  |                                     |
| ATAN  |                                     |
|-------+-------------------------------------|
| CSC   | 1/SIN(X)                            |
| SEC   | 1/COS(X)                            |
| COT   | 1/TAN(X)                            |
| ACSC  | SIN(1/X)                            |
| ASEC  | COS(1/X)                            |
| ACOT  | TAN(1/X)                            |
|-------+-------------------------------------|
| TAN2  |                                     |
| HYPOT |                                     |
| HAV   | Haversine = 1/2-cos(X)/2            |
| AHAV  | Inverse Haversine = 2*asin(sqrt(X)) |
| ▒▒▒▒  |                                     |
| ▒▒▒▒  |                                     |

** Menu Code
#+BEGIN_SRC elisp :var tbl=TRIG :colnames y :results output verbatum :wrap "src hp42s :tangle ../src_42s/math/math.hp42s"
(MJR-generate-42-menu-code "TRIG" 0 tbl 0 nil 'stay 'up #'MJR-custom-gen-lab #'MJR-custom-gen-sub)
#+END_SRC

#+RESULTS:
#+begin_src hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:TRIG)
@@@@ DSC: Auto-generated menu program
LBL "TRIG"
LBL 01            @@@@ Page 1 of menu TRIG
CLMENU
"SIN"
KEY 1 XEQ 04
"COS"
KEY 2 XEQ 05
"TAN"
KEY 3 XEQ 06
"ASIN"
KEY 4 XEQ 07
"ACOS"
KEY 5 XEQ 08
"ATAN"
KEY 6 XEQ 09
KEY 7 GTO 03
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02            @@@@ Page 2 of menu TRIG
CLMENU
"CSC"
KEY 1 XEQ "CSC"
"SEC"
KEY 2 XEQ "SEC"
"COT"
KEY 3 XEQ "COT"
"ACSC"
KEY 4 XEQ "ACSC"
"ASEC"
KEY 5 XEQ "ASEC"
"ACOT"
KEY 6 XEQ "ACOT"
KEY 7 GTO 01
KEY 8 GTO 03
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03            @@@@ Page 3 of menu TRIG
CLMENU
"TAN2"
KEY 1 XEQ "TAN2"
"HYPOT"
KEY 2 XEQ "HYPOT"
"HAV"
KEY 3 XEQ "HAV"
"AHAV"
KEY 4 XEQ "AHAV"
KEY 7 GTO 02
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 03
LBL 00 @@@@ Application Exit
EXITALL
RTN
LBL 04    @@@@ Action for menu key SIN
SIN
RTN
LBL 05    @@@@ Action for menu key COS
COS
RTN
LBL 06    @@@@ Action for menu key TAN
TAN
RTN
LBL 07    @@@@ Action for menu key ASIN
ASIN
RTN
LBL 08    @@@@ Action for menu key ACOS
ACOS
RTN
LBL 09    @@@@ Action for menu key ATAN
ATAN
RTN
@@@@ Free labels start at: 10
#+end_src

** Functions

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Inverse Haversine
@@@@ IN:  X: number
@@@@ OUT: X: ahav(X)=2*asin(sqrt(X))
@@@@ UPD: 2021-04-18
@@@@ TST: free42_3.0
LBL "AHAV"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
SQRT
ASIN
2
×
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Haversine
@@@@ IN:  X: number
@@@@ OUT: X: hav(X)=1/2-cos(X)/2
@@@@ UPD: 2021-04-18
@@@@ TST: free42_3.0
LBL "HAV"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
COS
-2
÷
0.5
+
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Hypot
@@@@ IN:  Y: number
@@@@ IN:  X: number
@@@@ OUT: X: sqrt(abs(x)^2+abs(y)^2)
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
LBL "HYPOT"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ABS
X<>Y
ABS
COMPLEX
ABS
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: 1/SIN(X)
LBL "CSC"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
SIN
1/X
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: 1/COS(X)
LBL "SEC"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
COS
1/X
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: 1/TAN(X)
LBL "COT"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
TAN
1/X
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: SIN(1/X)
LBL "ACSC"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
1/X
ASIN
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: COS(1/X)
LBL "ASEC"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
1/X
ACOS
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: TAN(1/X)
LBL "ACOT"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
1/X
ATAN
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: ATAN2
@@@@ IN:  Y: number
@@@@ IN:  X: number
@@@@ OUT: X: atan2(y, x)
@@@@ BUG: Only works in RAD mode
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
@@@@ TC:  atan( 1, 1) =>  pi/4     =   45°
@@@@ TC:  atan(-1, 1) => -pi/4     =  -45°
@@@@ TC:  atan( 1,-1) =>  3*pi/4   =  135°
@@@@ TC:  atan(-1,-1) => -3*pi/4   = -135°
@@@@ TC:  atan( 0, 1) =>  0        =    0°
@@@@ TC:  atan( 1, 0) =>  pi       =   90°
@@@@ TC:  atan(-1, 0) => -pi       =  -90°
@@@@ TC:  atan( 0, 0) => ERROR
@@@@ LBL: Used 81-85
LBL "TAN2"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
X>0?
GTO 81
X=0?
GTO 82
@@@@ X<0
X<>Y
X<0?
GTO 83
@@@@ X<0 & Y>=0
X<>Y
÷
ATAN
PI
+
RTN
LBL 83
@@@@ X<0 & Y<0
X<>Y
÷
ATAN
PI
-
RTN
LBL 82
X<>Y
X=0?
GTO 84
X>0?
GTO 85
@@@@ X=0 & Y<0
PI
-2
÷
RTN
LBL 85
@@@@ X=0 & Y>0
PI
2
÷
RTN
LBL 84
@@@@ X=0 & Y=0 ERROR 0/0
÷
RTN
LBL 81
@@@@ X>0
÷
ATAN
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
END
#+END_SRC

* =LPRT=: Logs, Powers & Roots
** Menu
:PROPERTIES:
:CUSTOM_ID: menu-lprt
:END:

#+ATTR_HTML: :align center :frame box :rules all
#+NAME:LPRT
| Menu | Lab    | Description                                          |
|------+--------+------------------------------------------------------|
| lnYX | lnYX   | Base Y Logarithm of X                                |
| ▒▒▒▒ |        |                                                      |
| ln2  | ln2    | Base 2 logarithm                                     |
| LN   |        | Base 3 logarithm                                     |
| LOG  |        | Base 10 logarithm                                    |
| ▒▒▒▒ |        |                                                      |
|------+--------+------------------------------------------------------|
| Y↑X  |        | Raise Y to the power of X                            |
| ▒▒▒▒ |        |                                                      |
| 2↑X  | LBL 97 | Raise 2 to the power of X                            |
| EXP  |        | Raise e to the power of X                            |
| 10↑X |        | Raise 10 to the power of X                           |
| ▒▒▒▒ |        |                                                      |
|------+--------+------------------------------------------------------|
| Y√X  | YROOT  | Yth roots prefering real & pure imaginary answers    |
| ▒▒▒▒ |        |                                                      |
| 2√X  | LBL 95 | Square root prefering real & pure imaginary answers  |
| 3√X  | LBL 94 | Cube root prefering real & pure imaginary answers    |
| 5√X  | LBL 93 | Fifth root prefering real & pure imaginary answers   |
| 7√X  | LBL 92 | Seventh root prefering real & pure imaginary answers |

** Menu Code
#+BEGIN_SRC elisp :var tbl=LPRT :colnames y :results output verbatum :wrap "src hp42s :tangle ../src_42s/math/math.hp42s"
(MJR-generate-42-menu-code "LPRT" 0 tbl 0 1 'stay 'up #'MJR-custom-gen-lab #'MJR-custom-gen-sub)
#+END_SRC

#+RESULTS:
#+begin_src hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:LPRT)
@@@@ DSC: Auto-generated menu program
LBL "LPRT"
LBL 01            @@@@ Page 1 of menu LPRT
CLMENU
"lnYX"
KEY 1 XEQ "lnYX"
"ln2"
KEY 3 XEQ "ln2"
"LN"
KEY 4 XEQ 04
"LOG"
KEY 5 XEQ 05
KEY 7 GTO 03
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02            @@@@ Page 2 of menu LPRT
CLMENU
"Y↑X"
KEY 1 XEQ 06
"2↑X"
KEY 3 XEQ 97
"EXP"
KEY 4 XEQ "EXP"
"10↑X"
KEY 5 XEQ 07
KEY 7 GTO 01
KEY 8 GTO 03
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03            @@@@ Page 3 of menu LPRT
CLMENU
"Y√X"
KEY 1 XEQ "YROOT"
"2√X"
KEY 3 XEQ 95
"3√X"
KEY 4 XEQ 94
"5√X"
KEY 5 XEQ 93
"7√X"
KEY 6 XEQ 92
KEY 7 GTO 02
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 03
LBL 00 @@@@ Application Exit
EXITALL
RTN
LBL 04    @@@@ Action for menu key LN
LN
RTN
LBL 05    @@@@ Action for menu key LOG
LOG
RTN
LBL 06    @@@@ Action for menu key Y↑X
Y↑X
RTN
LBL 07    @@@@ Action for menu key 10↑X
10↑X
RTN
@@@@ Free labels start at: 8
#+end_src

** Functions

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Base Y Logarithm of X
@@@@ IN:  Y: logarithm base
@@@@      X: number or matrix (element-wise)
@@@@ OUT: X: log_y(x)
@@@@ UPD: 2021-04-14
@@@@ TST: free42_3.0
LBL "lnYX"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
LN
X<>Y
LN
÷
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Base 2 Logarithm
@@@@ IN:  X: number or matrix (element-wise)
@@@@ OUT: X: log_2(x)
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
LBL "ln2"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
LN
2
LN
÷
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Raise 2 to the power of X
@@@@ NAM: 2↑X 97
@@@@ IN:  X: number or matrix (element-wise)
@@@@ OUT: X: 2^X
@@@@ UPD: 2021-02-23
@@@@ TST: free42_3.0
LBL 97
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
2
X<>Y
Y^X
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Square root prefering real & pure imaginary answers 
@@@@ NAM: ROOT2 95  
@@@@ IN:  X: Number or numeric matrix (element-wise)
@@@@ OUT: X: root of X
@@@@ FAQ: See XYROOT for details
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-14
LBL 95
FUNC 11              @@## REQ:free42>=2.5.24
2
X<>Y
XEQ "YROOT"
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Cube root prefering real & pure imaginary answers   
@@@@ NAM: ROOT3 94  
@@@@ IN:  X: Number or numeric matrix (element-wise)
@@@@ OUT: X: root of X
@@@@ FAQ: See XYROOT for details
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-14
LBL 94
FUNC 11              @@## REQ:free42>=2.5.24
3
X<>Y
XEQ "YROOT"
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Fifth root prefering real & pure imaginary answers  
@@@@ NAM: ROOT5 93  
@@@@ IN:  X: Number or numeric matrix (element-wise)
@@@@ OUT: X: root of X
@@@@ FAQ: See XYROOT for details
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-14
LBL 93
FUNC 11              @@## REQ:free42>=2.5.24
5
X<>Y
XEQ "YROOT"
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Seventh root prefering real & pure imaginary answers
@@@@ NAM: ROOT7 92  
@@@@ IN:  X: Number or numeric matrix (element-wise)
@@@@ OUT: X: root of X
@@@@ FAQ: See XYROOT for details
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-14
LBL 92
FUNC 11              @@## REQ:free42>=2.5.24
7
X<>Y
XEQ "YROOT"
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Nth roots prefering real & pure imaginary answers
@@@@ IN:  Y: Number
@@@@ IN:  X: Number or numeric matrix (element-wise)
@@@@ OUT: X: Yth root of X
@@@@ BUG: The principal value is not always returned (by design)
@@@@ BUG: Real integers are not recognized in complex form. i.e. 0+2i ≠ 2
@@@@ FAQ: Return is pure imaginary when Y is an odd integer and X<0
@@@@ FAQ: Return is real when  Y is an even integer and X<0
@@@@ TST: free42_3.0
@@@@ UPD: 2021-02-23
LBL "YROOT"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
REAL?
GTO 81
GTO 84
LBL 81
@@@@ X is real
X≥0?
GTO 84
@@@@ X is negative, real
RCL ST Y
REAL?
GTO 82
GTO 83
LBL 82
@@@@ X is negative, real; Y is real
FP
X≠0?
GTO 83
@@@@ X is negative, real; Y is real integer
R↓
ABS
RCL ST Y
X<>Y
XEQ 84
+/-
X<>Y
2
÷
FP
X=0?
GTO 86
GTO 85
LBL 86
@@@@ X is negative, real; Y is real integer even
R↓
-1
SQRT
×
RTN
LBL 85
@@@@ X is negative, real; Y is real integer odd
R↓
RTN
LBL 83
@@@@ Not special case.  Stack: Y X Y
R↓
LBL 84
@@@@ Not special case.  Stack: X Y
X<>Y
1/X
Y↑X
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
END

#+END_SRC

* =NLA=: Linear Algebra
** Menu
:PROPERTIES:
:CUSTOM_ID: menu-nla
:END:

#+ATTR_HTML: :align center :frame box :rules all
#+NAME:NLA
| Menu | Lab    |                                            | Inputs | Output             | Status     |
|------+--------+--------------------------------------------+--------+--------------------+------------|
| IDEN | IDEN   | Create an nxn identity matrix              | X: N   | X: identity matrix | *COMPLETE* |
| DIAG | DIAG   | Create diagonal matrix with given elements | X: VEC | X: diagonal matrix | *COMPLETE* |
|      |        |                                            |        |                    |            |
|      |        |                                            |        |                    |            |
|      |        |                                            |        |                    |            |
|      |        |                                            |        |                    |            |
|------+--------+--------------------------------------------+--------+--------------------+------------|
| MAT  | LBL 98 | Store/Recall Current CPOLY matrix          |        |                    |            |
| X    | LBL 97 | Store/Recall current value of "X"          |        |                    |            |
|      |        |                                            |        |                    |            |
|      |        |                                            |        |                    |            |
|      |        |                                            |        |                    |            |
| EVCP | CPOLY  | Evaluate Characteristic polynomial         |        |                    |            |

** Code for Menu

#+BEGIN_SRC elisp :var tbl=NLA :colnames y :results output verbatum :wrap "src hp42s :tangle ../src_42s/math/math.hp42s"
(MJR-generate-42-menu-code "NLA" 0 tbl 0 1 'stay 'up #'MJR-custom-gen-lab #'MJR-custom-gen-sub)
#+END_SRC

#+RESULTS:
#+begin_src hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:NLA)
@@@@ DSC: Auto-generated menu program
LBL "NLA"
LBL 01            @@@@ Page 1 of menu NLA
CLMENU
"IDEN"
KEY 1 XEQ "IDEN"
"DIAG"
KEY 2 XEQ "DIAG"
KEY 7 GTO 02
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02            @@@@ Page 2 of menu NLA
CLMENU
"MAT"
KEY 1 XEQ 98
"X"
KEY 2 XEQ 97
"EVCP"
KEY 6 XEQ "CPOLY"
KEY 7 GTO 01
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 00 @@@@ Application Exit
EXITALL
RTN
@@@@ Free labels start at: 3
#+end_src

** Functions

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:IDEN)
@@@@ DSC: Create an XxX identity matrix
@@@@ IN:  X: Size of matrix to make
@@@@ OUT: X: Identity matrix of size X
@@@@ LBL: 21-22
@@@@ UPD: 2021-03-12
LBL "IDEN"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
ENTER
NEWMAT
LSTO "TMPM"
INDEX "TMPM"
1
LBL 21
STOEL
J+
FS? 77
GTO 22
I+
GTO 21
LBL 22
RCL "TMPM"
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:DIAG)
@@@@ DSC: Create diagonal matrix with given elements
@@@@ IN:  X: Vector
@@@@ IN:  X: diagonal matrix
@@@@ LBL: 23
@@@@ UPD: 2021-03-12
LBL "DIAG"
FUNC 11              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
LSTO "TMPM"
INDEX "TMPM"
DIM?
×
ENTER
DIM "TMPM"
1000
÷
1
+
LSTO "I"
LBL 23
1
RCL "I"
STOIJ
R↓
R↓
RCLEL
0
STOEL
R↓
RCL "I"
RCL "I"
STOIJ
R↓
R↓
STOEL
ISG "I"
GTO 23
RCL "TMPM"
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:CPOLY)
@@@@ DSC: Evaluate Chararstic Polynomial of a Matrix: DET(M-X*I)
@@@@ I/O: N/A MVAR program
@@@@ VAR: CPM a square matrix
@@@@ VAR: X a real or complex number
@@@@ LAB: 24-25
@@@@ FAQ: Can be used 
@@@@ UPD: 2021-04-23
LBL "CPOLY"
MVAR "CPM"
MVAR "X"
RCL "CPM"
LSTO "TMPM"
INDEX "TMPM"
R↓
RCL "X"
LBL 24
RCLEL
RCL- ST Y
STOEL
R↓
J+
FS? 77
GTO 25
I+
GTO 24
LBL 25
RCL "TMPM"
DET
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ Store/Recall variable "CPM"
LBL 98 
FS? 64
RCL "CPM"
STO "CPM"
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ Store/Recall variable "X"
LBL 97
FS? 64
RCL "X"
STO "X"
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
END
#+END_SRC

* =POLY=: A collection of polynomial tools
** Menu
:PROPERTIES:
:CUSTOM_ID: menu-poly
:END:

#+ATTR_HTML: :align center :frame box :rules all
#+NAME:POLY
| Menu  | Program |                                   | Inputs         | Output              | Status     |
|-------+---------+-----------------------------------+----------------+---------------------+------------|
| QUADE | QUADE   | Solve quadratic: Ax^2+Bx+C=0      | Z: A Y: B X: C | Y: root 1 X: root 2 | *complete* |
| EVAL  | PEVAL   | Evaluate polynomial P @ X         | Y: P  X: X     | X: P(x)             | *complete* |
| ▒▒▒▒  |         |                                   |                |                     |            |
| ▒▒▒▒  |         |                                   |                |                     |            |
| ▒▒▒▒  |         |                                   |                |                     |            |
| ▒▒▒▒  |         |                                   |                |                     |            |
|-------+---------+-----------------------------------+----------------+---------------------+------------|
| POLY  | LBL 98  | Store/Recall Current Polynomial   |                |                     |            |
| X     | LBL 97  | Store/Recall current value of "X" |                |                     |            |
| ▒▒▒▒  |         |                                   |                |                     |            |
| ▒▒▒▒  |         |                                   |                |                     |            |
| ▒▒▒▒  |         |                                   |                |                     |            |
| EVAL  | PWRP    | Evaluate wrapped polynomial at X  | N/A            | X: P("X")           | *TODO*     |

** Code for Menu

#+BEGIN_SRC elisp :var tbl=POLY :colnames y :results output verbatum :wrap "src hp42s :tangle ../src_42s/math/math.hp42s"
(MJR-generate-42-menu-code "POLY" 0 tbl 0 1 'stay 'up #'MJR-custom-gen-lab #'MJR-custom-gen-sub)
#+END_SRC

#+RESULTS:
#+begin_src hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:POLY)
@@@@ DSC: Auto-generated menu program
LBL "POLY"
LBL 01            @@@@ Page 1 of menu POLY
CLMENU
"QUADE"
KEY 1 XEQ "QUADE"
"EVAL"
KEY 2 XEQ "PEVAL"
KEY 7 GTO 02
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02            @@@@ Page 2 of menu POLY
CLMENU
"POLY"
KEY 1 XEQ 98
"X"
KEY 2 XEQ 97
"EVAL"
KEY 6 XEQ "PWRP"
KEY 7 GTO 01
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 00 @@@@ Application Exit
EXITALL
RTN
@@@@ Free labels start at: 3
#+end_src

** Local functions 

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Solve quadratic equation: ax^2+bx+c=0
@@@@ IN:  Z: a
@@@@      Y: b
@@@@      X: c
@@@@ OUT: Y: root_1 (-b-sqrt(b^2-4ac))/(2a)
@@@@      X: root_2 (-b+sqrt(b^2-4ac))/(2a)
@@@@ TST: free42_3.0
@@@@ BUG: No error checking
@@@@ UPD: 2021-04-02
LBL "QUADE"
FUNC 32              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
-4
RCL× ST T
×
RCL ST Y
X↑2
+
SQRT
2
RCL× ST T
÷
LASTX
RCL ST Z
+/-
X<>Y
÷
RCL ST X
RCL ST Z
-
RCL ST Z
RCL ST Z
+
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Evaluate a polynomial
@@@@ IN:  Y: Matrix with polynomial coefficients.  DIM of 1xn, nx1, whatever...
@@@@      X: Value at which polynomial should be evaluated
@@@@ OUT: Y: original polynomial matrix
@@@@      X: value of polynomial evaluated at X
@@@@ LBL: 91
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-03
LBL "PEVAL"
FUNC 21              @@## REQ:free42>=2.5.24
L4STK                @@## REQ:free42>=3.0
X<>Y
LSTO "TMPM"
INDEX "TMPM"
WRAP
0
LBL 91
RCL× ST Z
RCLEL
+
J+
FC? 77
GTO 91
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:PWRP)
@@@@ DSC: Make a polynomial stored in a matrix into a =MVAR= function
@@@@ IN:  X: N/A
@@@@ OUT: X: N/A
@@@@ GLB: WRPP -- Store a polynomial matrix in this variable
@@@@ TST: free42_3.0
@@@@ FAQ: Allows one to use SOLVER & INTEG on polynomials
@@@@ USE: PEVAL
@@@@ UPD: 2021-04-03
LBL "PWRP"
MVAR "X"
RCL "WRPP"
RCL "X"
XEQ "PEVAL"
RTN


@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ Store/Recall variable "WRPP"
LBL 98 
FS? 64
RCL "WRPP"
STO "WRPP"
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ Store/Recall variable "X"
LBL 97
FS? 64
RCL "X"
STO "X"
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
END
#+END_SRC

* =VEC3=: 3D Real Vector Application

This is a simple little application that makes working with 3D vectors less painfull.  
** Menu
:PROPERTIES:
:CUSTOM_ID: menu-vec3
:END:

#+ATTR_HTML: :align center :frame box :rules all
#+NAME:VEC3
| Menu  | Target |                                                                     |
|-------+--------+---------------------------------------------------------------------|
| →V    | LBL 99 | Put stack elements X, Y, & Z into a vector: [Z, Y, X]               |
| V→    | LBL 98 | Vector contents to stack. [A, B, C] => X: C, Y: B, Z: A             |
| DOT   |        | Dot product                                                         |
| CROSS |        | Cross product                                                       |
| MAG   | FNRM   | Euculidian magnitude                                                |
| VVIEW | LBL 96 | View a vector one element at a time -- press =R/S= for next element |
|-------+--------+---------------------------------------------------------------------|

** Code for Menu

#+BEGIN_SRC elisp :var tbl=VEC3 :colnames y :results output verbatum :wrap "src hp42s :tangle ../src_42s/math/math.hp42s"
(MJR-generate-42-menu-code "VEC3" 0 tbl 0 1 'stay 'up #'MJR-custom-gen-lab #'MJR-custom-gen-sub)
#+END_SRC

#+RESULTS:
#+begin_src hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ (ref:VEC3)
@@@@ DSC: Auto-generated menu program
LBL "VEC3"
LBL 01            @@@@ Page 1 of menu VEC3
CLMENU
"→V"
KEY 1 XEQ 99
"V→"
KEY 2 XEQ 98
"DOT"
KEY 3 XEQ 02
"CROSS"
KEY 4 XEQ 03
"MAG"
KEY 5 XEQ 04
"VVIEW"
KEY 6 XEQ 96
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 00 @@@@ Application Exit
EXITALL
RTN
LBL 02    @@@@ Action for menu key DOT
DOT
RTN
LBL 03    @@@@ Action for menu key CROSS
CROSS
RTN
LBL 04    @@@@ Action for menu key MAG
FNRM
RTN
@@@@ Free labels start at: 5
#+end_src

** Local functions

#+BEGIN_SRC hp42s :tangle ../src_42s/math/math.hp42s
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Create a vector from stack contents
@@@@ NAM: →V 99
@@@@ IN:  Z: real number
@@@@      Y: real number
@@@@      X: real number
@@@@ OUT: X: 1x3 matrix
@@@@ LBL: Used: 51
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-03
LBL 99
FUNC 31
XEQ 95
LSTO "TMPM"
R↓
INDEX "TMPM"
WRAP
J-
LBL 51
STOEL
R↓
J-
FC? 77
GTO 51
RCL "TMPM"
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Put vector elements on stack
@@@@ NAM: V→ 98
@@@@ IN:  X: 1x3 matrix V
@@@@ OUT: Z: First component of V
@@@@      Y: Second component of V
@@@@      X: Third component of V
@@@@ LBL: Used: 52
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-03
LBL 98
FUNC 13
LSTO "TMPM"
R↓
INDEX "TMPM"
WRAP
LBL 52
RCLEL
J+
FC? 77
GTO 52
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: View elements of vector
@@@@ NAM: VVIEW 96
@@@@ IN:  X: 1x3 matrix V
@@@@ OUT: N/A
@@@@ LBL: Used: 53
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-03
LBL 96
FUNC 00
LSTO "TMPM"
INDEX "TMPM"
WRAP
1
LBL 54
CLA
AIP
├":"
RCLEL
ARCL ST X
R↓
AVIEW
STOP
1
+
J+
FC? 77
GTO 54
RTN

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@ DSC: Make a 3D vector full of zeros
@@@@ NAM: VVIEW 95
@@@@ IN:  N/A
@@@@ OUT: X: 1x3 Matrix
@@@@ LBL: Used: 53
@@@@ TST: free42_3.0
@@@@ UPD: 2021-04-03
LBL 95
FUNC 01
1
3
NEWMAT
RTN


@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
END
#+END_SRC

* WORKING                                                          :noexport:

#+BEGIN_SRC text
:::::::::::::::::::::::'##:::::'##::::'###::::'########::'##::: ##:'####:'##::: ##::'######::::::::::::::::::::::::
::::::::::::::::::::::: ##:'##: ##:::'## ##::: ##.... ##: ###:: ##:. ##:: ###:: ##:'##... ##:::::::::::::::::::::::
::::::::::::::::::::::: ##: ##: ##::'##:. ##:: ##:::: ##: ####: ##:: ##:: ####: ##: ##:::..::::::::::::::::::::::::
::::::::::::::::::::::: ##: ##: ##:'##:::. ##: ########:: ## ## ##:: ##:: ## ## ##: ##::'####::::::::::::::::::::::
::::::::::::::::::::::: ##: ##: ##: #########: ##.. ##::: ##. ####:: ##:: ##. ####: ##::: ##:::::::::::::::::::::::
::::::::::::::::::::::: ##: ##: ##: ##.... ##: ##::. ##:: ##:. ###:: ##:: ##:. ###: ##::: ##:::::::::::::::::::::::
:::::::::::::::::::::::. ###. ###:: ##:::: ##: ##:::. ##: ##::. ##:'####: ##::. ##:. ######::::::::::::::::::::::::
::::::::::::::::::::::::...::...:::..:::::..::..:::::..::..::::..::....::..::::..:::......:::::::::::::::::::::::::
#+END_SRC

Code in this section is under construction.  Most likely broken.

* EOF

# End of document.

# The following adds some space at the bottom of exported HTML
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
