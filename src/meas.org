# -*- Mode:Org; Coding:utf-8; fill-column:158 -*-
#+TITLE:       free42 Repeated Measurements
#+AUTHOR:      Mitch Richling
#+EMAIL:       http://www.mitchr.me/
#+DATE:        2021-03-24
#+DESCRIPTION: Description of some free42/hp-42s/DM42 programs repeated measurements and uncertainty reporting
#+LANGUAGE:    en
#+OPTIONS:     num:t toc:nil \n:nil @:t ::t |:t ^:nil -:t f:t *:t <:t skip:nil d:nil todo:t pri:nil H:5 p:t author:t html-scripts:nil
#+HTML_HEAD: <style>body { width: 95%; margin: 2% auto; font-size: 18px; line-height: 1.4em; font-family: Georgia, serif; color: black; background-color: white; }</style>
#+HTML_HEAD: <style>body { min-width: 500px; max-width: 1024px; }</style>
#+HTML_HEAD: <style>h1,h2,h3,h4,h5,h6 { color: #A5573E; line-height: 1em; font-family: Helvetica, sans-serif; }</style>
#+HTML_HEAD: <style>h1,h2,h3 { line-height: 1.4em; }</style>
#+HTML_HEAD: <style>h1.title { font-size: 3em; }</style>
#+HTML_HEAD: <style>h4,h5,h6 { font-size: 1em; }</style>
#+HTML_HEAD: <style>.org-src-container { border: 1px solid #ccc; box-shadow: 3px 3px 3px #eee; font-family: Lucida Console, monospace; font-size: 80%; margin: 0px; padding: 0px 0px; position: relative; }</style>
#+HTML_HEAD: <style>.org-src-container>pre { line-height: 1.2em; padding-top: 1.5em; margin: 0.5em; background-color: #404040; color: white; overflow: auto; }</style>
#+HTML_HEAD: <style>.org-src-container>pre:before { display: block; position: absolute; background-color: #b3b3b3; top: 0; right: 0; padding: 0 0.2em 0 0.4em; border-bottom-left-radius: 8px; border: 0; color: white; font-size: 100%; font-family: Helvetica, sans-serif;}</style>
#+HTML_HEAD: <style>pre.example { white-space: pre-wrap; white-space: -moz-pre-wrap; white-space: -o-pre-wrap; font-family: Lucida Console, monospace; font-size: 80%; background: #404040; color: white; display: block; padding: 0em; border: 2px solid black; }</style>
#+HTML_LINK_HOME: https://www.mitchr.me/
#+HTML_LINK_UP: https://richmit.github.io/hp42/
#+EXPORT_FILE_NAME: ../docs/meas

#+ATTR_HTML: :border 2 solid #ccc :frame hsides :align center
|        <r> | <l>              |
|  *Author:* | /{{{author}}}/ |
| *Updated:* | /{{{time(%Y-%m-%d %H:%M:%S)}}}/ |
#+ATTR_HTML: :align center
Copyright {{{time(%Y)}}} Mitch Richling. All rights reserved.

#+TOC: headlines 5

#        #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #
#   00   #    10   #    20   #    30   #    40   #    50   #    60   #    70   #    80   #    90   #   100   #   110   #   120   #   130   #   140   #   150   #   160   #
# 234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789
#        #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #
#        #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #         #

* Metadata

This home for this HTML file is: https://richmit.github.io/hp42/meas.html

Files related to this document may be found on github: https://github.com/richmit/hp42

Directory contents:
#+ATTR_HTML: :border 0 :frame none :rules none :align center
   | =src=     | - | The org-mode file that generated this HTML document            |
   | =src_42s= | - | Ready to convert source listings for 42s code in this document |
   | =docs=    | - | This html document                                             |
   | =bin=     | - | Importable RAW program files                                   |

* Introduction

I frequently take repeated measurements, but don't feel the need to record all the measurements in my laboratory notebook.  For example, when working on my
stamp collection I will measure stamp thickness at 5 locations and use the average value.  The application described below is designed to make that process
more automated and less error prone.

* Supported Methods

When estimating & reporting measurement uncertainty, two methods are most common.  The first assumes the measurements are statistically independent and
normally distributed.  In this case the measurement is taken to be the mean, and the standard uncertainty (u) is defined as the population standard deviation.  The
second method assumes a rectangular error distribution.  In this case the measurement is assumed to be the midrange=(max+min)/2, and the standard uncertainty is
defined as u=(max-min)/(2*sqrt(3)).  In both schemes the standard uncertainty is frequently "expanded" by a factor (k) in order to provide a probabilistic uncertainty
interval or confidence interval.

Other schemes are frequently used, and the application provides raw statistics to compute many of them: mean, midrange, range, standard deviation, population
standard deviation, number of observations, sum, & sum of squares.

* Setup

The last menu provides configuration options.

** Distribution

The distribution to be used in reports is set via the first menu button -- the current selection is displayed in the menu label (N=Normal & S=Square).  This
key cycles between normal and rectangular distributions when pressed.  Note that it also defaults the coverage factor (the second menu button)!

** Coverage Factors

The coverage factor used to compute the expanded uncertainty is set via the second menu key.  Repeatedly pressing this button will cycle through predefined
values appropriate for the already selected distribution.

This option is used both for reports and the expanded uncertainty computations on the second menu page (=[kuN]= & =[kuS]=).  It is not possible to set the
coverage factor to an arbitrary value; however, the population standard deviation (=[PSD]=) and rectangular variance (=[SqVA]=)are in the third menu.

For normally distributed errors the following coverage factors are supported.

#+ATTR_HTML: :align center
| Confidence % | Coverage Factor |
|--------------+-----------------|
|        68.27 |               1 |
|        95.45 |               2 |
|        99.73 |               3 |

For errors with a rectangular distribution the following coverage factors are supported.

#+ATTR_HTML: :align center
| Coverage Factor  | Key label |
|-----------------+-----------|
|               1 |           |
|   1.73205080757 | √3        |

** Automatic Reporting

A report can be generated upon entering a specified number of measurements.  This is very handy when a batch of items needs to be measured, and one wishes to
do a fixed number of measurements per item.  To set the automatic report count provide an integer in X, and use this menu key.  The current value is displayed
as part of the menu label.  A dash (-) means no automatic reporting is configured.  Use zero to turn off reporting.

* Reporting

I find that most of the time I use units such that measurements integer values.  For example with paper thickness measurements I will use micrometers in a
range between 700 and 1100.  For other philatelic applications I may use millimeters with one or, rarely, two decimals.  For these applications it is possible
to fit the measurement value, uncertainty, max, and min all on the screen at once assuming =FIX= has been set appropriately.  The report function attempts to
use this compact reporting method if possible.  Otherwise it uses the first line for the measurement estimate and the second line for the expanded
uncertainty.

The measurement and expanded uncertainty are reported according to the selected of distribution and coverage factor.  See the setup menu.

Reports can be automatically generated upon entry of a configurable number of measurements.  See the setup menu.

* Application Menu

#+ATTR_HTML: :align center
#+NAME: measm
|------+-------------------------------+-----------------------------------------------------------------|
| Menu | Description                   | Notes                                                           |
|------+-------------------------------+-----------------------------------------------------------------|
| M+   | Add a new measurement         |                                                                 |
| M-   | Delete last measurement       |                                                                 |
| ---  |                               |                                                                 |
| REP  | Display Report                | Reports μ & kuN or MID & kuS depending on distribution          |
| ---  |                               |                                                                 |
| CLRM | Clear all measurements        |                                                                 |
|------+-------------------------------+-----------------------------------------------------------------|
| μ    | Arithmetic Mean               | Normal Errors: Measurement estimate                             |
| kuN  | k*PSD                         | Normal Errors: Expanded uncertainty estimate                    |
| MID  | (MAX+MIN)/2                   | Rectangular (Square) Errors: Measurement estimate               |
| kuS  | k*RNG/2/SQRT(3)               | Rectangular (Square) Errors: Expanded uncertainty estimate      |
| MIN  | Minimum value                 |                                                                 |
| MAX  | Maximum value                 |                                                                 |
|------+-------------------------------+-----------------------------------------------------------------|
| SD   | Standard Deviation            |                                                                 |
| PSD  | Population Standard Deviation | This is the one with n-1 on the bottom                          |
| RNG  | Range                         | MAX-MIN                                                         |
| SqVA | Rectangular (Square) Variance | (MAX-MIN)/(2*sqrt(3))                                           |
| N    | Number of measurements        | Number of rows in MeDAT                                         |
| SUMS | Y: Sum Squared X: Sum         |                                                                 |
| ---  |                               |                                                                 |
|------+-------------------------------+-----------------------------------------------------------------|
| D=?  | Distribution                  | Cycle between N=Normal & S=Rectangular (Square).  Defaults k.   |
| k=NN | Coverage Factor               | Cycle through predefined values for distribution                |
| A=NN | Auto Report Count             | Automatically generate a report when N measurements are entered |
|------+-------------------------------+-----------------------------------------------------------------|

* Use

In use the application is quite like the built in statistics application in that =[M+]= & =[M+]= add and delete measurements.  All measurements are stored in
a matrix named =MeDAT=.  Feel free to edit this matrix with the =MATRIX= menu.

The =[REP]= key will generate a report.  Reports can also be generated automatically when a set number of measurements have been entered (see the setup menu).

Menu page two & three compute various statistics useful for uncertainty reporting.  Page two has the most common statistics, and page three is more useful for
custom uncertainty computations.

Menu page four is for setup.  The distribution impacts reporting (menu page one: =[REP]=).  The coverage factor impacts reporting (menu page one: =[REP]=) and
and the expanded uncertainty computations (menu page two: =[kuN]= & =[kuS]=).  The automatic reporting option impacts the add measurement function (menu page
one: =[M+]=) such that a report is automatically generated when a specified number of measurements have been entered.

* Future

Some things I might do some day...

 - I stored all the data in a matrix because I was thinking about adding rank statistics and a graphics
 - A graphical representation showing the points and various confidence intervals would be cool
 - It would be neat to see a live dot plot of the measurements as they are being entered
 - Arbitrary k values
 - Support the larger screen when running on the DM42
 - Make the sub-functions perfect (i.e. minimally alter stack/last X)
 - Make the setup menu not alter the stack
 - Add a printed report listing data and both normal & square estimates for all coverage factors

* The Code

#+begin_src hp42s :tangle ../src_42s/meas/meas.hp42s
@@@@ Repeated Measurements
@@@@ IN:  N/A
@@@@ OUT: N/A
@@@@ UPD: 2021-02-24
@@@@ BUG: Settings menu messes with the stack
@@@@ GBL: MeDAT -- Measurement Matrix
@@@@ GBL: MeDST -- Error Distribution
@@@@ GBL: MeCFA -- Coverage Factor
@@@@ GBL: MeTGN -- Target Measurement Count (generates a report automatically)
@@@@ TST: free42 3.0.2
LBL "MEAS"
@@@@ Initialize setup variables
0
SF 25
RCL "MeDST"
FC?C 25
STO "MeDST"
SF 25
RCL "MeCFA"
FC?C 25
STO "MeCFA"
SF 25
RCL "MeTGN"
FC?C 25
STO "MeTGN"
R↓
@@@@ Menu Setup
LBL 01               @@@@ Page 1 of menu MEAS
CLMENU
"M+"
KEY 1 XEQ 05
"M-"
KEY 2 XEQ 06
"REP"
KEY 4 XEQ 07
"CLRM"
KEY 6 XEQ 08
KEY 7 GTO 04
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02               @@@@ Page 2 of menu MEAS
CLMENU
"μ"
KEY 1 XEQ 09
"kuN"
KEY 2 XEQ 10
"MID"
KEY 3 XEQ 11
"kuS"
KEY 4 XEQ 12
"MIN"
KEY 5 XEQ 13
"MAX"
KEY 6 XEQ 14
KEY 7 GTO 01
KEY 8 GTO 03
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03               @@@@ Page 3 of menu MEAS
CLMENU
"SD"
KEY 1 XEQ 15
"PSD"
KEY 2 XEQ 16
"RNG"
KEY 3 XEQ 17
"SqVA"
KEY 4 XEQ 35
"N"
KEY 5 XEQ 18
"SUMS"
KEY 6 XEQ 19
KEY 7 GTO 02
KEY 8 GTO 04
KEY 9 GTO 00
MENU
STOP
GTO 03
LBL 04               @@@@ Page 4 of menu MEAS
CLMENU
XEQ 55
KEY 2 XEQ 21
XEQ 56
KEY 3 XEQ 22
KEY 7 GTO 03
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 04
LBL 00
EXITALL
RTN
@@@@ Menu Actions
LBL 05               @@@@ Action for menu key M+
FUNC 00
REAL?
GTO 34
"ERR: Bad Value"
AVIEW
RTN
LBL 34
SF 25                @@@@ Index & grow MeDAT
INDEX "MeDAT"
FS?C 25
GTO 33
1
1
DIM "MeDAT"
INDEX "MeDAT"
R↓
R↓
STOEL
VIEW "MeDAT"
RTN
LBL 33               @@@@ MeDAT exists.  Grow it
GROW
J-
J+
WRAP
STOEL                @@@@ Store element at new location
XEQ 18
RCL "MeTGN"
X≠Y?
GTO 43
XEQ 07
RTN
LBL 43
VIEW "MeDAT"
RTN
LBL 06               @@@@ Action for menu key M-
FUNC 00
SF 25                @@@@ Index & grow MeDAT
INDEX "MeDAT"
FC?C 25
RTN                  @@@@ MeDAT is missing
J-
RCLEL
DELR
VIEW "MeDAT"
RTN
LBL 07               @@@@ Action for menu key REP
FUNC 00
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
R↓
RCL "MeDST"
X=0?                 @@@@ IF-BEGIN
GTO 39
GTO 40
LBL 39               @@@@ IF-THEN normal
"μ="
XEQ 09
ARCL ST X
├" "
RCL "MeCFA"
70
+
XEQ IND ST X
├"×u="
XEQ 10
ARCL ST X
GTO 41
LBL 40               @@@@ IF-ELSE square
"C="
XEQ 11
ARCL ST X
├" "
RCL "MeCFA"
70
+
XEQ IND ST X
├"×u="
XEQ 12
ARCL ST X
LBL 41               @@@@ IF-END
ALENG
21
X<Y?                 @@@@ Can we keep going with 4 value report?
GTO 36
├"[LF]↓="
XEQ 13
ARCL ST X
├"  ↑="
XEQ 14
ARCL ST X
ALENG
41
X<Y?                 @@@@ Can we keep going with 4 value report?
GTO 36
AVIEW
RTN
LBL 36               @@@@ 2 value report
RCL "MeDST"
X=0?                 @@@@ IF-BEGIN
GTO 37
GTO 38
LBL 37               @@@@ IF-THEN normal
R↓
" μ="
XEQ 09
ARCL ST X
├"[LF]"
RCL "MeCFA"
70
+
XEQ IND ST X
├"×u="
XEQ 10
ARCL ST X
GTO 39
LBL 38               @@@@ IF-ELSE square
R↓
" C="
XEQ 11
ARCL ST X
├"[LF]"
RCL "MeCFA"
70
+
XEQ IND ST X
├"×u="
XEQ 12
ARCL ST X
LBL 39               @@@@ IF-END
AVIEW
RTN
LBL 08               @@@@ Action for menu key CLRM
FUNC 00
SF 25
INDEX "MeDAT"
CLV "MeDAT"
CF 25
"MeDAT Cleared"
AVIEW
RTN
LBL 09               @@@@ Action for menu key μ (mean)
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
XEQ 19
X<>Y
R↓
XEQ 18
÷
RTN
LBL 10               @@@@ Action for menu key kuN (expanded uncertainty for normally distributed data)
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
XEQ 16
RCL "MeCFA"
60
+
XEQ IND ST X
X<>Y
R↓
×
RTN
LBL 11               @@@@ Action for menu key MID (midrange)
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
XEQ 14
XEQ 13
+
2
÷
RTN
LBL 12               @@@@ Action for menu key kuS (expanded uncertainty for rectangular distributed data)
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
XEQ 35
RCL "MeCFA"
60
+
XEQ IND ST X
X<>Y
R↓
×
RTN
LBL 35               @@@@ Action for menu key SqVA
FUNC 0
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
XEQ 17
2
÷
3
SQRT
÷
RTN
LBL 13               @@@@ Action for menu key MIN
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
[MIN]
X<>Y
R↓
RTN
LBL 14               @@@@ Action for menu key MAX
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
[MAX]
X<>Y
R↓
RTN
LBL 15               @@@@ Action for menu key SD
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
XEQ 19
XEQ 18
X<>Y
X↑2
RCL÷ ST Y
RCL÷ ST Y
RCL ST Z
RCL ST Z
÷
X<>Y
-
SQRT
RTN
LBL 16               @@@@ Action for menu key PSD
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
XEQ 19
XEQ 18
X<>Y
X↑2
RCL÷ ST Y
RCL ST Z
X<>Y
-
X<>Y
1
-
÷
SQRT
X<>Y
R↓
RTN
LBL 17               @@@@ Action for menu key RNG
FUNC 01
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
XEQ 14
XEQ 13
-
RTN
LBL 18               @@@@ Action for menu key N
FUNC 01
SF 25
RCL "MeDAT"
FS?C 25
GTO 54
0
RTN
LBL 54
DIM?
R↓
RTN
LBL 19               @@@@ Action for menu key SUMS
FUNC 02
SF 25
INDEX "MeDAT"
FC?C 25
GTO 99
0
0
LBL 32
RCLEL
STO+ ST Y
X↑2
STO+ ST Z
R↓
J+
FC? 77
GTO 32
RTN
LBL 20               @@@@ Action for menu key D:
FUNC 00
1
RCL+ "MeDST"
2
MOD
STO "MeDST"
R↓
0
STO "MeCFA"
R↓
RTN
LBL 21               @@@@ Action for menu key k:
FUNC 00
1
RCL+ "MeCFA"
RCL "MeDST"
X=0?
GTO 29
GTO 30
LBL 29
4
GTO 31
LBL 30
2
LBL 31
X<>Y
R↓
MOD
STO "MeCFA"
R↓
RTN
LBL 22               @@@@ Action for menu key A:
FUNC 00
1
X≠Y?
GTO 42
"ERR: Bad Value"
AVIEW
RTN
LBL 42
R↓
STO "MeTGN"
RTN
@@@@ Coverage values
LBL 60               @@@@ Value for coverage factor 0
1
RTN
LBL 61               @@@@ Value for coverage factor 1
RCL "MeDST"
X=0?
GTO 24
R↓
2
RTN
LBL 24
R↓
3
SQRT
RTN
LBL 62               @@@@ Value for coverage factor 2
3
RTN
LBL 63               @@@@ Value for coverage factor 3
4
RTN
@@@@ Coverage Names
LBL 70               @@@@ Name for coverage factor 0
├"1"
RTN
LBL 71               @@@@ Name for coverage factor 1
RCL "MeDST"
X=0?
├"2"
X≠0?
├"√3"
R↓
RTN
LBL 72               @@@@ Name for coverage factor 2
├"3"
RTN
LBL 73               @@@@ Name for coverage factor 3
├"4"
RTN
@@@@ distribution Names
LBL 80               @@@@ Name for distribution 0
├"N"
RTN
LBL 81               @@@@ Name for distribution 1
├"S"
RTN
@@@@ Error States
LBL 99               @@@@ ERROR message for missing MeDAT
"ERR: No MeDAT"
AVIEW
RTN
LBL 55
FUNC 00
"D:"
RCL "MeDST"
80
+
XEQ IND ST X
R↓
KEY 1 XEQ 20
"k:"
RCL "MeCFA"
70
+
XEQ IND ST X
R↓
RTN
LBL 56
FUNC 00
"A:"
RCL "MeTGN"
X=0?
├"-"
X≠0?
AIP
R↓
RTN
END
#+end_src

* Median

This function computes the median of MeDAT.  Someday I may integrate it into the main =MEAS= application.

#+begin_src hp42s :tangle ../src_42s/meas/meas.hp42s
LBL "MEASM"
RCL "MeDAT"
STO "TMP"
R↓
SF 25
INDEX "TMP"
FC?C 25
GTO 99
LBL 44
[MIN]
R↓
RCLIJ
R↓
R<>R
J+
FC? 77
GTO 44
RCL "TMP"
DIM?
R↓
ENTER
ENTER
2
÷
FP
X=0?
GTO 45
"odd"
R↓
1
+
2
÷
IP
1
STOIJ
RCLEL
RTN
LBL 45
"even"
R↓
ENTER
2
÷
1
STOIJ
R↓
RCLEL
X<>Y
1
+
1
STOIJ
R↓
R↓
RCLEL
+
2
÷
RTN
END
#+END_SRC

* WORKING                                                          :noexport:

#+BEGIN_SRC text
:::::::::::::::::::::::'##:::::'##::::'###::::'########::'##::: ##:'####:'##::: ##::'######::::::::::::::::::::::::
::::::::::::::::::::::: ##:'##: ##:::'## ##::: ##.... ##: ###:: ##:. ##:: ###:: ##:'##... ##:::::::::::::::::::::::
::::::::::::::::::::::: ##: ##: ##::'##:. ##:: ##:::: ##: ####: ##:: ##:: ####: ##: ##:::..::::::::::::::::::::::::
::::::::::::::::::::::: ##: ##: ##:'##:::. ##: ########:: ## ## ##:: ##:: ## ## ##: ##::'####::::::::::::::::::::::
::::::::::::::::::::::: ##: ##: ##: #########: ##.. ##::: ##. ####:: ##:: ##. ####: ##::: ##:::::::::::::::::::::::
::::::::::::::::::::::: ##: ##: ##: ##.... ##: ##::. ##:: ##:. ###:: ##:: ##:. ###: ##::: ##:::::::::::::::::::::::
:::::::::::::::::::::::. ###. ###:: ##:::: ##: ##:::. ##: ##::. ##:'####: ##::. ##:. ######::::::::::::::::::::::::
::::::::::::::::::::::::...::...:::..:::::..::..:::::..::..::::..::....::..::::..:::......:::::::::::::::::::::::::
#+END_SRC

Code in this section is under construction.  Most likely broken.
* EOF

# End of document.

# The following adds some space at the bottom of exported HTML
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
#+HTML: <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br /> <br />
